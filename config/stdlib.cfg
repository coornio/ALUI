///////////////////////////////////////////////////////////////////////////////
//  Standard Variable Definitions                                            //
///////////////////////////////////////////////////////////////////////////////

//nodebug [
    // scoreboard variables
    defvarp showscore   0  1  1
    defvarp showkills   0  1  1
    defvarp showdeaths  0  0  1
    defvarp showip      0  0  1
    defvarp sbopaque    0  1  1

    // value placeholders
    defvarp .autosortservers  0   1   1
    defvarp .hidehud          0   0   1
    defvarp .crosshairsize    0  15  50
    defvarp .showmat          0   1   1
    defvarp .entediting       0   1   1
    
    // other variables
    defvarp zoomtoggle       0  0  1
    defvarp confirmquit      0  1  1
    defvarp confirmoverwrite 0  1  1
    defvar  playingmusic     0  0  1
    
    defvarp  .followplayer  0  0    1
    defvar  .followcn      -1  0  159
    defsvar .followlist    ""
    defsvarp modelist      "edit rdm pdm rtdm ptdm rctf pctf"

    // UI variables
    defvarp .UI_useimg  0  1  1
    defvarp .UI_taskbar 0  0  1
//]




///////////////////////////////////////////////////////////////////////////////
//  Keybind & Combobind Definitions                                          //
///////////////////////////////////////////////////////////////////////////////

defkey = [
    local def cus
    def = [:def:@arg1] ; cus = [:cus:@arg1]
    $def = $arg2 ; $cus = []
    bind $arg1 [ keyswitch @arg1 @def @cus ]
]

defeditkey = [
    local def cus
    def = [:edef:@arg1] ; cus = [:ecus:@arg1]
    $def = $arg2 ; $cus = []
    editbind $arg1 [ keyswitch @arg1 @def @cus ]
]

defspeckey = [
    local def cus
    def = [:sdef:@arg1] ; cus = [:scus:@arg1]
    $def = $arg2 ; $cus = []
    specbind $arg1 [ keyswitch @arg1 @def @cus ]
]

setkey     = [ [:cus:@arg1]  = $arg2 ]
seteditkey = [ [:ecus:@arg1] = $arg2 ]
setspeckey = [ [:scus:@arg1] = $arg2 ]

keyswitch = [
    [:switch:@arg1] = 1
    onrelease [ [:switch:@@arg1] = 0 ]
    if (=s (getalias $arg3) "") (getalias $arg2) (getalias $arg3)
]

iskeyheld = [
    local k ; k = 0
    nodebug [
        loop+ i 1 $numargs [
            k = (+ $k $[:switch:@[arg@i]])
        ]
    ]
    = $k $numargs
]




///////////////////////////////////////////////////////////////////////////////
//  Standard Texture Commands (*.tex)                                        //
///////////////////////////////////////////////////////////////////////////////

texsky      = [ setshader "stdworld" ; texture 0 "game/sky.png" ]
texerror    = [ setshader "stdworld" ; texture 0 "game/notexture.png" ]
texnorender = [ setshader "stdworld" ; texture 0 "game/norender.png" ]
texload     = [
    if (! (exec [media/texture/@arg1.tex] 0)) [
        error [texload error: @arg1.tex not found]
        texerror
    ]
]
decalload = [
    if (! (exec [media/decal/@arg1.cfg] 0)) [
        error [decalload error: @arg1.cfg not found]
    ]
]
