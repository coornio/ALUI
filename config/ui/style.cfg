
///////////////////////////////////////////////////////////////////////////////
//  Text Brightness & Default Paddings                                       //
///////////////////////////////////////////////////////////////////////////////

textbright 92


.UI_pad_DSXL = 0.48 // SXL * 10
.UI_pad_D6XL = 0.40 // 6XL * 10
.UI_pad_D5XL = 0.32 // 5XL * 10
.UI_pad_D4XL = 0.24 // 4XL * 10
.UI_pad_D3XL = 0.20 // 3XL * 10
.UI_pad_D2XL = 0.16 // 2XL * 10
.UI_pad_DXL  = 0.12 // XL  * 10
.UI_pad_DL   = 0.10 // L   * 10
.UI_pad_DM   = 0.08 // M   * 10
.UI_pad_DS   = 0.06 // S   * 10
.UI_pad_DSS  = 0.05 // SS  * 10
.UI_pad_DXS  = 0.03 // XS  * 10

.UI_pad_SXL = 0.048 // S * 8
.UI_pad_6XL = 0.040 // L * 4
.UI_pad_5XL = 0.032 // M * 4
.UI_pad_4XL = 0.024 // S * 4
.UI_pad_3XL = 0.020 // L * 2
.UI_pad_2XL = 0.016 // M * 2
.UI_pad_XL  = 0.012 // S * 2
.UI_pad_L   = 0.010
.UI_pad_M   = 0.008
.UI_pad_S   = 0.006
.UI_pad_SS  = 0.005 // L / 2
.UI_pad_XS  = 0.003 // S / 2




///////////////////////////////////////////////////////////////////////////////
//  Preset Colors                                                            //
///////////////////////////////////////////////////////////////////////////////

	c_menu   = 0x202020   // solid: window background
	c_menu_m = 0x1C1C1C   // solid: window background with #c_boxmod
	c_diag1  = 0x80202020 // alpha: dialog background
	c_diag2  = 0xC0202020 // alpha: dialog background selected

	c_main1  = 0x282828   // solid: main title gradient top
	c_main2  = 0x161616   // solid: main title gradient bottom

	c_line   = 0x303030   // solid: basic line
	c_fill   = 0xF0E8E0   // solid: white area
	c_focus  = 0xF81008   // solid: focus area

	c_cb0    = 0x484848   // solid: checkbox off
	c_cb1    = 0x2BC4F3   // solid: checkbox on
	c_rd0    = 0x484848   // solid: radio off
	c_rd1    = 0x48BACE   // solid: radio on

	c_boxmod = 0xDDDDDD   // modulate: box
	c_grdmod = 0xB8B8B8   // modulate: gradient

	c_green   = 0x40FF80   
	c_blue    = 0x60A0FF   
	c_yellow  = 0xFFC040   
	c_red     = 0xFF4040   
	c_gray    = 0x808080
	c_gray_l  = 0xB8B8B8
	c_magenta = 0xC040C0
	c_orange  = 0xFF8000
	c_white   = 0xFFFFFF
	c_cyan_t  = 0x50CFE5




///////////////////////////////////////////////////////////////////////////////
//  Generic Styles                                                           //
///////////////////////////////////////////////////////////////////////////////

style_generic_default = [ style_generic_box ]
style_generic_none = []

// arg1 (d): background color   | d: default bg color      | n: no bg color      | else: bg color of choice
// arg2 (n): modulo color       | d: default mod color     | n: no mod color     | else: mod color of choice
// arg3 (d): background pattern | d: default UI pattern    | n: no UI pattern    | else: UI pattern of choice
// arg4 (d): outline color      | d: default outline color | n: no outline/pulse | else: outline color of choice
// arg5 (d): pulse frequency    | d: 500 ms  | p: -500 ms  | n: no pulse         | else: pulse frequency of choice
// arg6 (d): pulse color        | d: default pulse color   | n: no pulse         | else: pulse color of choice
style_generic_box = [
	cases (? (< $numargs 1) "d" $arg1) "d" [ uicolor $c_menu ] "n" [] () [ uicolor $arg1 ]
	cases (? (< $numargs 2) "n" $arg2) "d" [ uimodcolor $c_boxmod ] "n" [] () [ uimodcolor $arg2 ]
	cases (? (< $numargs 3) "d" $arg3) "d" [
		uitiledimage "media/interface/ui/ui_bg2.png" $.UI_pad_SXL $.UI_pad_SXL
	] "n" [] () [
		uitiledimage (concatword "media/interface/ui/ui_" $arg3 ".png") $.UI_pad_SXL $.UI_pad_SXL
	]
	cases (? (< $numargs 4) "d" $arg4) "d" [ arg4 = $c_line ] "n" [ arg4 = 0 ]
	if $arg4 [
		cases (? (< $numargs 5) "d" $arg5) "d" [ arg5 =  500    ] "n" [ arg5 = 0 ] "p" [ arg5 = -500 ]
		cases (? (< $numargs 6) "d" $arg6) "d" [ arg6 = $c_cb0  ] "n" [ arg6 = 0 ]
		if (&& $arg5 $arg6) [
			uioutline (coswave.INT $arg4 $arg6 (
				? (|| [< $arg5 0] [&& $uihover? [! $uihold?] [! $uieschold?]]) (abs $arg5) 0
			))
		] [ uioutline $arg4 ]
	]
]

style_generic_gradient1 = [ uimodvgradient $c_grdmod 0xFFFFFF ]
style_generic_gradient2 = [ uimodvgradient 0xFFFFFF $c_grdmod ]
style_generic_gradient3 = [ uimodhgradient $c_grdmod 0xFFFFFF ]
style_generic_gradient4 = [ uimodhgradient 0xFFFFFF $c_grdmod ]

style_generic_linesh = [
	uivlist $arg2 [
		loop i $arg1 [ uiline $c_line $arg3 0 ]
	]
]

style_generic_linesv = [
	uihlist $arg2 [
		loop i $arg1 [ uiline $c_line 0 $arg3 ]
	]
]




///////////////////////////////////////////////////////////////////////////////
//  Button Styles                                                            //
///////////////////////////////////////////////////////////////////////////////

style_button_default = [ style_button_hold1 ]
style_button_none = []

// arg1 (d): background color   | d: default bg color      | n: no bg color      | else: bg color of choice
// arg2 (d): modulo color       | d: default mod color     | n: no mod color     | else: mod color of choice
// arg3 (d): background pattern | d: default UI pattern    | n: no UI pattern    | else: UI pattern of choice
// arg4 (d): outline color      | d: default outline color | n: no outline/pulse | else: outline color of choice
// arg5 (d): pulse frequency    | d: 500 ms  | p: -500 ms  | n: no pulse         | else: pulse frequency of choice
// arg6 (d): pulse color        | d: default pulse color   | n: no pulse         | else: pulse color of choice
style_button_addargs = [
	cases (? (< $numargs 1) "d" $arg1) "d" [ uicolor $c_menu ] "n" [] () [ uicolor $arg1 ]
	cases (? (< $numargs 2) "d" $arg2) "d" [ uimodcolor $c_boxmod ] "n" [] () [ uimodcolor $arg2 ]
	cases (? (< $numargs 3) "d" $arg3) "d" [
		uitiledimage "media/interface/ui/ui_bg1.png" $.UI_pad_SXL $.UI_pad_SXL
	] "n" [] () [
		uitiledimage (concatword "media/interface/ui/ui_" $arg3 ".png") $.UI_pad_SXL $.UI_pad_SXL
	]
	cases (? (< $numargs 4) "d" $arg4) "d" [ arg4 = $c_line ] "n" [ arg4 = 0 ]
	if $arg4 [
		cases (? (< $numargs 5) "d" $arg5) "d" [ arg5 =  500    ] "n" [ arg5 = 0 ] "p" [ arg5 = -500 ]
		cases (? (< $numargs 6) "d" $arg6) "d" [ arg6 = $c_cb0  ] "n" [ arg6 = 0 ]
		if (&& $arg5 $arg6) [
			uioutline (coswave.INT $arg4 $arg6 (
				? (|| [< $arg5 0] [&& $uihover? [! $uihold?] [! $uieschold?]]) (abs $arg5) 0
			))
		] [ uioutline $arg4 ]
	]
]

style_button_hold1 = [// H
	do $style_button_addargs
	uihold [ style_generic_gradient1 ] [
		uihover [ style_generic_gradient2 ]
	]
]

style_button_hold2 = [// H, HOVER
	if (|| $uihover? $uihold?) [
		do $style_button_addargs
		uihold [ style_generic_gradient1 ] [
			style_generic_gradient2
		]
	]
]

style_button_hold3 = [// H, ESC
	do $style_button_addargs
	if (|| $uihold? $uieschold?) [
		style_generic_gradient1
	] [ uihover [ style_generic_gradient2 ] ]
]

style_button_hold4 = [// H, HOVER, ESC
	if (|| $uihover? $uihold? $uieschold) [
		do $style_button_addargs
		if (|| $uihold? $uieschold?) [ style_generic_gradient1 ] [
			style_generic_gradient2
		]
	]
]

style_button_hold5 = [// V
	do $style_button_addargs
	uihold [ style_generic_gradient3 ] [
		uihover [ style_generic_gradient4 ]
	]
]

style_button_hold6 = [// V, HOVER
	if (|| $uihover? $uihold?) [
		do $style_button_addargs
		uihold [ style_generic_gradient3 ] [
			style_generic_gradient4
		]
	]
]

style_button_hold7 = [// V, ESC
	do $style_button_addargs
	if (|| $uihold? $uieschold?) [
		style_generic_gradient3
	] [ uihover [ style_generic_gradient4 ] ]
]

style_button_hold8 = [// V, HOVER, ESC
	if (|| $uihover? $uihold? $uieschold) [
		do $style_button_addargs
		if (|| $uihold? $uieschold?) [ style_generic_gradient3 ] [
			style_generic_gradient4
		]
	]
]
