///////////////////////////////////////////////////////////////////////////////
//  Text Brightness & Default Paddings                                       //
///////////////////////////////////////////////////////////////////////////////

textbright 92

.UI_pad_3XL = 0.020
.UI_pad_2XL = 0.016
.UI_pad_XL  = 0.012
.UI_pad_L   = 0.010
.UI_pad_M   = 0.008
.UI_pad_S   = 0.006
.UI_pad_XS  = 0.003




///////////////////////////////////////////////////////////////////////////////
//  Preset Colors                                                            //
///////////////////////////////////////////////////////////////////////////////

	c_menu   = 0x202020   // solid: window background
	c_menu_m = 0x1C1C1C   // solid: window background with #c_boxmod
	c_diag1  = 0x80202020 // alpha: dialog background
	c_diag2  = 0xC0202020 // alpha: dialog background selected

	c_main1  = 0x282828   // solid: main title gradient top
	c_main2  = 0x161616   // solid: main title gradient bottom

	c_line   = 0x303030   // solid: basic line
	c_fill   = 0xF0E8E0   // solid: white area
	c_focus  = 0xF81008   // solid: focus area

	c_cb0    = 0x484848   // solid: checkbox off
	c_cb1    = 0x2BC4F3   // solid: checkbox on
	c_rd0    = 0x484848   // solid: radio off
	c_rd1    = 0x48BACE   // solid: radio on

	c_boxmod = 0xDDDDDD   // modulate: box
	c_grdmod = 0xB8B8B8   // modulate: gradient




///////////////////////////////////////////////////////////////////////////////
//  Generic Styles                                                           //
///////////////////////////////////////////////////////////////////////////////

style_generic_default = [ style_generic_box ]
style_generic_none = []

// generic styles can now accept a multitude of arguments!
// beware, different behavior per argument existence and value:

// arg1 (d): background color   | d: default bg color      | n: no bg color   | else: bg color of choice
// arg2 (n): modulo color       | d: default mod color     | n: no mod color  | else: mod color of choice
// arg3 (d): background pattern | d: default UI pattern    | n: no UI pattern | else: UI pattern of choice
// arg4 (d): outline color      | d: default outline color | n: no outline    | else: outline color of choice
style_generic_box = [
	cases (? (< $numargs 1) "d" $arg1) "d" [ uicolor $c_menu ] "n" [] () [ uicolor $arg1 ]
	cases (? (< $numargs 2) "n" $arg2) "d" [ uimodcolor $c_boxmod ] "n" [] () [ uimodcolor $arg2 ]
	cases (? (< $numargs 3) "d" $arg3) "d" [
		uitiledimage "media/interface/ui/ui_bg2.png" $.UI_pad_XL $.UI_pad_XL
	] "n" [] () [
		uitiledimage (concatword "media/interface/ui/ui_" $arg3 ".png") $.UI_pad_XL $.UI_pad_XL
	]
	cases (? (< $numargs 4) "d" $arg4) "d" [ uioutline $c_line ] "n" [] () [ uioutline $arg4 ]
]

style_generic_gradient1 = [ uimodvgradient $c_grdmod 0xFFFFFF ]
style_generic_gradient2 = [ uimodvgradient 0xFFFFFF $c_grdmod ]
style_generic_gradient3 = [ uimodhgradient $c_grdmod 0xFFFFFF ]
style_generic_gradient4 = [ uimodhgradient 0xFFFFFF $c_grdmod ]

style_generic_linesh = [
	uivlist $arg2 [
		loop i $arg1 [ uiline $c_line $arg3 0 ]
	]
]

style_generic_linesv = [
	uihlist $arg2 [
		loop i $arg1 [ uiline $c_line 0 $arg3 ]
	]
]




///////////////////////////////////////////////////////////////////////////////
//  Button Styles                                                            //
///////////////////////////////////////////////////////////////////////////////

style_button_default = [ style_button_hold1 ]
style_button_none = []

// button styles can now accept a multitude of arguments!
// beware, different behavior per argument existence and value:

// arg1 (d): background color   | d: default bg color   | n: no bg color   | else: bg color of choice
// arg2 (d): modulo color       | d: default mod color  | n: no mod color  | else: mod color of choice
// arg3 (d): background pattern | d: default UI pattern | n: no UI pattern | else: UI pattern of choice
style_button_addargs = [
	cases (? (< $numargs 1) "d" $arg1) "d" [ uicolor $c_menu ] "n" [] () [ uicolor $arg1 ]
	cases (? (< $numargs 2) "d" $arg2) "d" [ uimodcolor $c_boxmod ] "n" [] () [ uimodcolor $arg2 ]
	cases (? (< $numargs 3) "d" $arg3) "d" [
		uitiledimage "media/interface/ui/ui_bg1.png" $.UI_pad_XL $.UI_pad_XL
	] "n" [] () [
		uitiledimage (concatword "media/interface/ui/ui_" $arg3 ".png") $.UI_pad_XL $.UI_pad_XL
	]
]

style_button_hold1 = [// H
	do $style_button_addargs
	uihold [ style_generic_gradient1 ] [
		uihover [ style_generic_gradient2 ]
	]
	uioutline $c_line
]

style_button_hold2 = [// H, HOVER
	uihover [
		do $style_button_addargs
		uihold [ style_generic_gradient1 ] [
			style_generic_gradient2
		]
		uioutline $c_line
	]
]

style_button_hold3 = [// H, ESC
	do $style_button_addargs
	uihold [ style_generic_gradient1 ] [
		uieschold [ style_generic_gradient1 ] [
			uihover [ style_generic_gradient2 ]
		]
	]
	uioutline $c_line
]

style_button_hold4 = [// H, HOVER, ESC
	uihover [
		do $style_button_addargs
		uihold [ style_generic_gradient1 ] [
			uieschold [ style_generic_gradient1 ] [
				style_generic_gradient2
			]
		]
		uioutline $c_line
	]
]

style_button_hold5 = [// V
	do $style_button_addargs
	uihold [ style_generic_gradient3 ] [
		uihover [ style_generic_gradient4 ]
	]
	uioutline $c_line
]

style_button_hold6 = [// V, HOVER
	uihover [
		do $style_button_addargs
		uihold [ style_generic_gradient3 ] [
			style_generic_gradient4
		]
		uioutline $c_line
	]
]

style_button_hold7 = [// V, ESC
	do $style_button_addargs
	uihold [ style_generic_gradient3 ] [
		uieschold [ style_generic_gradient3 ] [
			uihover [ style_generic_gradient4 ]
		]
	]
	uioutline $c_line
]

style_button_hold8 = [// V, HOVER, ESC
	uihover [
		do $style_button_addargs
		uihold [ style_generic_gradient3 ] [
			uieschold [ style_generic_gradient3 ] [
				style_generic_gradient4
			]
		]
		uioutline $c_line
	]
]
