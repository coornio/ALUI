//////////////////////////////////////////////////////////////////////////
//                                                                      //
//                          UI ELEMENTS LEGEND                          //
//                                                                      //
//  #1  menu, panel, stddialog, quickdialog, menutitle & title buttons  //
//  #2  fastimg, bar, triangle, checkbox, radio, arrowbutton & keybox   //
//  #3  settip, cleartip, basicfield, basiceditor & activetable         //
//  #4  box, tooltipbox, buttonarea & button                            //
//  #5  scrollbars, sliders, colorsliders & listslider                  //
//                                                                      //
//  ui*    hardcoded functions                                          //
//  UI*    custom functions                                             //
//  UI_*   variables                                                    //
//                                                                      //
//////////////////////////////////////////////////////////////////////////




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #1  menu, panel, dialog, menutitle & title buttons                                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UImenutitle [ title children ] Y [ button-area-children ]
UImenutitle = [
    if $arg2 [] [ arg2 = 0.04 ]
    uivgradient $c_main1 $c_main2 0 $arg2 [
        uiclamp 1 1
        uifont "default_outline" arg1
        if (= $numargs 3) [
            uispace $.UI_pad_S 0.001 [
                uialign 1 -1
                uihlist 0 arg3
            ]
        ]
    ]
]

UItitlebutton_mini = [
    UIbuttonarea "hold2 0x404040 n n" 0.034 0.026 [
        UIfastimg "ui/" "ui_mini" "" 0.016 0.016
        uirelease [
            if (< (indexof $.UI_taskbar $uiname) 0) [
                .UI_taskbar (concat $.UI_taskbar (escape $uiname))
            ]
            hideui $uiname
        ]
    ]
]

UItitlebutton_exit = [
    UIbuttonarea "hold2 0x801010 n n" 0.05 0.026 [
        UIfastimg "ui/" "ui_exit" "" 0.016 0.016
        uirelease [ hideui $uiname ]
    ]
]

// UImenu "NAME" [ children ] [ on-show ] [ on-hide ] [ ui-children ] [ title-children ] [ title-buttons ]
// NOTE: title buttons are formatted like so: [ [titlebutton-name arg1 arg2 ..] .. ]
UImenu = [
    newui $arg1 [
        @arg5
        @(? (>= $numargs 6) [
            uivlist 0 [
                UImenutitle [@@@arg6] 0 [
                    @@@(if (= $numargs 7) [
                        looplistconcat i [@@arg7] [ result [UItitlebutton_@i ;] ]
                    ] [ result [UItitlebutton_mini ; UItitlebutton_exit] ])
                ]
                uicolor $c_menu 0.01 0.01 [
                    uitiledimage "media/interface/ui/ui_bg0.png" 0.006 0.006
                    uispace $.UI_pad_L $.UI_pad_L [@@@@arg2]
                    uiclamp* 1 1 1 1
                ]
            ]
        ] [
            uicolor $c_menu 0.01 0.01 [
                uitiledimage "media/interface/ui/ui_bg0.png" 0.006 0.006
                uispace $.UI_pad_L $.UI_pad_L [@@@arg2]
                uiclamp* 1 1 1 1
            ]
        ])
        uioutline $c_line
        uiclamp* 1 1 1 1
    ] [
        [.UImillis_@@arg1] = $getmillis
        @arg3
    ] $arg4
]

// UIpanel "NAME" [ children ] [ on-show ] [ on-hide ] [ ui-children ]
UIpanel = [
    newui $arg1 [
        @arg5
        uicolor $c_diag2 0.01 0.01 [
            uispace $.UI_pad_L $.UI_pad_L [ @@@arg2 ]
            uioutline $c_line
            uiclamp* 1 1 1 1
        ]
        uiclamp* 1 1 1 1
    ] [
        [.UImillis_@@arg1] = $getmillis
        @arg3
    ] $arg4
]

// UIstddialog "NAME" [ children ] [ true ] [ false ] [ on-show ] [ on-hide ]
UIstddialog = [
    newui $arg1 [
        uispace 0.025 0 [
            uiclamp 1 1 1 1
            UIbox "" 0 0 [ uiclamp 1 1 1 1 ]
        ]
        uispace 0 $.UI_pad_L [
            uihlist 0 [
                uifill 0 0.065 [
                    uialign -2 -1
                    UIbuttonarea "" 0.05 0.05 [
                        uialign -2 1
                        local s ; s = (uihold? 0.04 (uihover? 0.044 0.04))
                        UIfastimg "ui/" "yes" "" $s $s
                        uirelease [ @@@@@@arg3 ; hideui $uiname ]
                    ]
                ]
                @@@arg2
                uifill 0 0.065 [
                    uialign -2 -1
                    UIbuttonarea "" 0.05 0.05 [
                        uialign -2 1
                        local s ; s = (uihold? 0.04 (uihover? 0.044 0.04))
                        UIfastimg "ui/" "no" "" $s $s
                        uirelease [ @@@@@@arg4 ; hideui $uiname ]
                    ]
                ]
            ]
        ]
    ] [
        [.UImillis_@@arg1] = $getmillis
        @arg5
    ] $arg6
]

// UIquickdialog "NAME" [ ui/bg-children ] [ children ] [ on-show ] [ on-hide ]
UIquickdialog = [
    newui $arg1 [
        @arg2
        uispace $.UI_pad_L $.UI_pad_L [
            uiclamp 1 1 1 1
            @@arg3
        ]
    ] [
        [.UImillis_@@arg1] = $getmillis
        @arg4
    ] $arg5
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #2  settip, cleartip, fastimg, bar, triangle, checkbox, radio, arrowbutton & keybox                     //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UIfastimg PATH1 PATH2 PATH3 X Y [ children ]
UIfastimg = [
    uiimage (concatword "media/interface/" $arg1 $arg2 $arg3 ".png") $arg4 $arg5 [ arg6 ]
]

// UIbar CLAMP-X CLAMP-Y
UIbar = [
    uiline $c_line
    uiclamp- $arg1 $arg1 $arg2 $arg2
]

// UItriangle 0xCOLOR X Y ROTATION
UItriangle = [
    if $arg1 [] [ arg1 = $c_fill ]
    uigroup [
        uitriangle $arg1 $arg2 $arg3 $arg4
        uitriangleoutline $arg1 $arg2 $arg3 $arg4
    ]
]

// UIcheckbox [ condition ] SIZE 0xAARRGGBB/ICON-ON 0xAARRGGBB/ICON-OFF [ children ]
UIcheckbox = [
    if $.UI_useimg [ UIfastimg "ui/" "ui_checkbox" (!= (arg1) 0) $arg2 $arg2 ] [
        if $arg4 [] [ arg4 = $c_cb0 ]
        if $arg3 [] [ arg3 = $c_cb1 ]
        uicolor (? (arg1) $arg3 $arg4) $arg2 $arg2 [
            uimodvgradient 0xFFFFFF 0xB8B8B8
            doargs $arg5
            uioutline $c_line
            uiclamp* 1 1 1 1
        ]
    ]
]

// UIradio [ condition ] SIZE 0xAARRGGBB/ICON-ON 0xAARRGGBB/ICON-OFF [ children ]
UIradio = [
    if $.UI_useimg [ UIfastimg "ui/" "ui_radio" (!= (arg1) 0) $arg2 $arg2 ] [
        if $arg4 [] [ arg4 = $c_rd0 ]
        if $arg3 [] [ arg3 = $c_rd1 ]
        uicircleoutline $c_line $arg2 [
            uicircle (? (arg1) $arg3 $arg4) $arg2 [
                //uimodvgradient 0xFFFFFF 0xB8B8B8
                doargs $arg5
                uiclamp* 1 1 1 1
            ]
        ]
    ]
]

// UIarrowbutton X Y ROTATION [ on-release ] ARROWSIZE:% [ children ]
UIarrowbutton = [
    local arrowsize
    arrowsize = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
    uitarget $arg1 $arg2 [
        style_button_default
        doargs $arg6
        uirelease [ doargs $arg4 ]
        uiclamp* 1 1 1 1
        UItriangle 0xFFFFFF $arrowsize $arrowsize $arg3
    ]
]

// UIkeybox X Y [ children ] 0xAARRGGBB
UIkeybox = [
    if $arg4 [] [ arg4 = 0x505050 ]
    uigroup [
        uicolor $arg4 (-f $arg1 0.004) (-f $arg2 0.003) [ uimodvgradient 0xFFFFFF $c_grdmod ; uiclamp- 1 1 1 1 ]
        uistretchedimage "media/interface/ui/io/frame.png" 0.025 0.025 [ uifill $arg1 $arg2 ]
        uioffset 0 -0.003 [ doargs $arg3 ]
    ]
]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #3  settip, cleartip, basicfield, basiceditor & activetable                                             //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

UIsettip   = [ uihover [ [.@[uiname]_tip] = $arg1 ] ]
UIcleartip = [ uihover [] [ [.@[uiname]_tip] = "" ] ]

// UIbasicfield VAR STRLEN [ on-change ] TEXTSIZE [ children ]
UIbasicfield = [
    uicolor $c_menu_m 0 0 [
        uifield $arg1 $arg2 $arg3 $arg4 [
            arg5
            uioutline (uifocus? $c_focus $c_line)
            uiclamp- 1 1 1 1
        ]
    ]
]

// UIbasiceditor VAR STRLEN STRLINES TEXTSIZE [ on-change ] PERSISTENCE [ children ]
UIbasiceditor = [
    uicolor $c_menu_m 0 0 [
        uitexteditor $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [
            arg7
            uioutline (uifocus? $c_focus $c_line)
            uiclamp- 1 1 1 1
        ]
    ]
]

// UIactivetable X Y TEXTSIZE [ "NAME" [ children ] [ action ] .. ] COLUMNS "tooltip"
UIactivetable = [
    uigrid $arg5 0 0 [
        UIsettip $arg6
        looplist3 n c a $arg4 [
            if (=s $n "") uifill [
                UIbutton "hold2" [
                    c ; uitext $n $arg3
                ] $arg1 $arg2 $a -1
            ]
        ]
    ]
]


//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #4  box, tooltipbox, buttonarea & button                                                                //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UIbox [ style(s) ] X Y [ children ]
UIbox = [
    uitarget $arg2 $arg3 [
        if (=s $arg1 "") [
            style_generic_default
        ] [ doargs [style_generic_@arg1] ]
        uiclamp* 1 1 1 1
        doargs $arg4
    ]
]

// UItooltipbox X Y [ children ] SCROLLSPEED [ bottom children ]
UItooltipbox = [
    if (=f $arg4 0) [ arg4 = (*f $arg2 2) ]
    local diagline ; diagline = [ uiline $c_line 0.006 0.02 ]
    uihlist 0 [
        uiline $c_line 0 $arg2 [ uialign 0 -1 ]
        uivlist 0 [
            uigroup [
                uiscroll (+f $arg1 0.03) $arg2 [
                    uifill $arg1 $arg2 $arg3
                ]
                UIbar 1 ; uialign- 0 -1 // gotta make sure it stays on top.
            ]
            UIbar 1
            uihlist 0 [
                uiclamp 1 1
                uihlist 0.006 [
                    diagline
                    if (!=s (getalias [.@[uiname]_tip]) "") [
                        UIfastimg "ui/" "ui_question" "1" 0.02 0.02
                        uiclip 0 0.02 [ uitext $[.@[uiname]_tip] 0.5 ]
                    ] [ UIfastimg "ui/" "ui_question" "0" 0.02 0.02 ]
                ]
                if (> $numargs 4) [
                    uiclip 0 0.02 [
                        uihlist 0.006 [
                            arg5
                            uifill
                        ]
                    ]
                ]
            ]
            uioffset 0.006 0 [
                uiclamp 1 1
                UIbar 1
            ]
        ]
        uivlist 0 [ // purely cosmetic lines
            uiclamp 0 0 1 1
            uiline $c_line 0.01
            uigroup [
                diagline            ; uialign- -1
                uiline $c_line 0.01 ; uialign-  0 1
            ]
        ]
        UIvscroll 0.02 (+f $arg2 0.02) $arg4
    ]
]

// UIbuttonarea [ style(s) ] X Y [ children ]
UIbuttonarea = [
    uitarget $arg2 $arg3 [
        if (=s $arg1 "") [
            style_button_default
        ] [ doargs [style_button_@arg1] ]
        uiclamp* 1 1 1 1
        doargs $arg4
    ]
]

// UIbutton [ style(s) ] [ button-children ] X Y [ on-release ] ROTATION [ children ] (USE-PADDING)
UIbutton = [
    uitarget $arg3 $arg4 [
        doargs $arg7
        if (=s $arg1 "") [
            style_button_default
        ] [ doargs [style_button_@arg1] ]
        uirelease [ doargs $arg5 ]
        uiclamp* 1 1 1 1
        uispace (? (>= $numargs 8) $arg8 $.UI_pad_L) 0 [
            uiclamp 1 1
            uihlist 0.01 [
                uialign $arg6 0
                doargs $arg2
            ]
        ]
    ]
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #5  scrollbars, sliders, colorsliders & listslider                                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UIvscroll X Y SCROLLSPEED
UIvscroll = [
    local arrowsize
    arrowsize = (*f $arg1 0.35)
    uivlist 0 [
        uiscrollarrow (-f 0 $arg3)
        UIbar 1
        uivscrollbar [
            uifill $arg1 $arg2 [ UIbar 0 1 ]
            uiscrollbutton [
                uiclamp 1 1
                style_button_hold5
                uiclamp* 1 1 1 1
                uispace 0 $arrowsize [
                    uiclamp 0 0 1 1
                    style_generic_linesh 3 0.004 (*f $arg1 0.5)
                    UItriangle $c_line $arrowsize $arrowsize   0 ; uialign- 0 -1
                    UItriangle $c_line $arrowsize $arrowsize 180 ; uialign- 0  1
                ]
            ]
        ]
        UIbar 1
        uiscrollarrow $arg3
    ]
]

// UIvscroll X Y SCROLLSPEED ARROWSIZE:%
UIhscroll = [
    local arrowsize
    arrowsize = (*f $arg2 0.35)
    uihlist 0 [
        uiscrollarrow $arg3
        UIbar 0 1
        uihscrollbar [
            uifill $arg1 $arg2 [ UIbar 1 ]
            uiscrollbutton [
                uiclamp 0 0 1 1
                style_button_hold1
                uiclamp* 1 1 1 1
                uispace $arrowsize 0 [
                    uiclamp 1 1
                    style_generic_linesv 3 0.004 (*f $arg2 0.5)
                    UItriangle $c_line $arrowsize $arrowsize  90 ; uialign- -1
                    UItriangle $c_line $arrowsize $arrowsize 270 ; uialign-  1
                ]
            ]
        ]
        UIbar 0 1
        uiscrollarrow (-f 0 $arg3)
    ]
]

// UIvslider VAR MIN MAX STEP X Y [ children ] [ on-change ] ARROWSIZE:%
UIvslider = [
    if $arg9 [] [ arg9 = 0.3 ]
    uivlist 0 [
        uisliderarrow -1 [ UIarrowbutton $arg5 $arg5 0 [] $arg9 ]
        uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
            UIbox "" (-f $arg6 (*f $arg5 2)) (*f 0.75 $arg5)
            uisliderbutton [
                uifill $arg5 0.012
                style_button_hold5
                uiclamp* 1 1 1 1
                if (< $numargs 10) [
                    uispace $.UI_pad_L 0 arg7
                ] arg7
            ]
        ]
        uisliderarrow 1 [ UIarrowbutton $arg5 $arg5 180 [] $arg9 ]
    ]
]

// UIhslider VAR MIN MAX STEP X Y [ children ] [ on-change ] ARROWSIZE:%
UIhslider = [
    if $arg9 [] [ arg9 = 0.3 ]
    uihlist 0 [
        uisliderarrow -1 [ UIarrowbutton $arg6 $arg6 90 [] $arg9 ]
        uihslider $arg1 $arg2 $arg3 $arg4 $arg8 [
            UIbox "" (-f $arg5 (*f $arg6 2)) (*f 0.75 $arg6)
            uisliderbutton [
                uifill 0.012 $arg6
                style_button_hold1
                uiclamp* 1 1 1 1
                if (< $numargs 10) [
                    uispace $.UI_pad_L 0 arg7
                ] arg7
            ]
        ]
        uisliderarrow 1 [ UIarrowbutton $arg6 $arg6 270 [] $arg9 ]
    ]
]

// UIhsliderslim VAR MIN MAX X Y [ on-change ] [ label formatting ]
UIhsliderslim = [
    uigroup [
        UIbox "" $arg4 (*f 0.75 $arg5) [ uioutline $c_line ; uiclamp- 1 1 1 1 ]
        uifont "default_outline" [
            uicolortext (if (> $numargs 6) [do (@arg7)] [result $$arg1]) @(? (|| uihover? uihold?) 0x98FFFFFF 0x60FFFFFF) 0.7
        ]
        uihslider $arg1 $arg2 $arg3 1 [ doargs $arg6 ] [
            uifill (*f 0.98 $arg4) $arg5
            uisliderbutton [ uiline 0xFFFFFF 0 $arg5 ]
        ]
    ]
]

// UIvcolorslider VAR MIN MAX 0xCOLOR1 0xCOLOR2 X Y [ on-change ]
UIvcolorslider = [
    uigroup [
        uivgradient $arg5 $arg4 (*f 0.75 $arg6) $arg7 [ uioutline $c_line ; uiclamp- 1 1 1 1 ]
        uifont "default_outline" [ uivlist -0.01 [
            loop i (strlen (abs $$arg1)) [
                uicolortext (substr $$arg1 $i 1) @@@(? (|| uihover? uihold?) 0x98FFFFFF 0x60FFFFFF) 0.7
            ]
        ] ]
        uivslider $arg1 $arg2 $arg3 1 [ doargs $arg8 ] [
            uifill $arg6 (*f 0.98 $arg7)
            uisliderbutton [ uiline 0xFFFFFF $arg6 0 ]
        ]
    ]
]

// UIhcolorslider VAR MIN MAX 0xCOLOR1 0xCOLOR2 X Y [ on-change ]
UIhcolorslider = [
    uigroup [
        uihgradient $arg4 $arg5 $arg6 (*f 0.75 $arg7) [ uioutline $c_line ; uiclamp- 1 1 1 1 ]
        uifont "default_outline" [
            uicolortext $$arg1 @(? (|| uihover? uihold?) 0x98FFFFFF 0x60FFFFFF) 0.7
        ]
        uihslider $arg1 $arg2 $arg3 1 [ doargs $arg8 ] [
            uifill (*f 0.98 $arg6) $arg7
            uisliderbutton [ uiline 0xFFFFFF 0 $arg7 ]
        ]
    ]
]

//UIlistslider VAR COMPARISON [ val1 val2 .. valn ] X Y [ on-change ] ARROWSIZE:% [ children ]
UIlistslider = [
    if $arg7 [] [ arg7 = 0.3 ]
    local ls_x ls_y
    ls_x = (-f $arg4 (*f $arg5 2))
    ls_y = (*f $arg5 0.85)
    uihlist 0 [
        UIarrowbutton $arg5 $arg5 90 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (< $n 0) [n = 0] [
                n = (- $n 2)
                if (< $n 0) [ n = (+ $n (listlen $arg3)) ]
            ]
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
        UIbox "" $ls_x $ls_y [
            uiclip $ls_x $ls_y [
                if (< $numargs 8) [
                    uitext (|| [[listassoc@arg2] $arg3 $$arg1] [ result "custom" ]) 0.6
                ] arg8
            ]
        ]
        UIarrowbutton $arg5 $arg5 270 [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
            $arg1 = (at $arg3 $n)
            arg6
        ] $arg7
    ]
]
