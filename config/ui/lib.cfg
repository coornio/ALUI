/////////////////////////////////////////////////////////////////////////////////
//                                                                             //
//                          UI ELEMENTS LEGEND                                 //
//                                                                             //
//  #1  menu, panel, stddialog, quickdialog, menutitle & title buttons         //
//  #2  fastimg, bar, triangle, checkbox, radio, arrowbutton & keybox          //
//  #3  settip, cleartip, hoversound, tabaction & blurbg                       //
//  #4  basicfield, basiceditor, tooltipbox & activetable                      //
//  #5  box, buttonarea & button                                               //
//  #6  scrollbars, (slim/bar)sliders, (color/spectrum)sliders & optionslider  //
//  #7  HUD: hudcell, hudcellslider, entcell, entcolor12/24                    //
//                                                                             //
//  ui*    hardcoded functions                                                 //
//  UI*    custom functions                                                    //
//  UI_*   variables                                                           //
//                                                                             //
/////////////////////////////////////////////////////////////////////////////////




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #1  menu, panel, stddialog, quickdialog, menutitle & title buttons                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UImenutitle [ title children ] Y [ button-area-children ]
UImenutitle = [
	if $arg2 [] [ arg2 = $.UI_pad_6XL ]
	uivgradient $c_main1 $c_main2 0 $arg2 [
		uiclamp 1 1
		uifont "default_outline" arg1
		if (= $numargs 3) [
			uispace $.UI_pad_S 0.001 [
				uialign 1 -1
				uihlist 0 arg3
			]
		]
	]
]

UItitlebutton_mini = [
	UIbuttonarea "hold2 0x404040 n n" 0.034 0.026 [
		UIfastimg "ui/" "ui_mini" "" $.UI_pad_2XL $.UI_pad_2XL
		uirelease [
			if (< (indexof $.UI_taskbar $uiname) 0) [
				.UI_taskbar (concat $.UI_taskbar (escape $uiname))
			]
			hideui $uiname
		]
	]
]

UItitlebutton_exit = [
	UIbuttonarea "hold2 0x801010 n n" 0.05 0.026 [
		UIfastimg "ui/" "ui_exit" "" $.UI_pad_2XL $.UI_pad_2XL
		uirelease [ hideui $uiname ]
	]
]

// UImenu "NAME" [ children ] [ on-show ] [ on-hide ] [ ui-children ] [ title-children ] [ title-buttons ]
// NOTE: title buttons are formatted like so: [ [titlebutton-name arg1 arg2 ..] .. ]
UImenu = [
	newui $arg1 [
		@arg5
		@(? (>= $numargs 6) [
			uivlist 0 [
				UImenutitle [@@@arg6] 0 [
					@@@(if (= $numargs 7) [
						looplistconcat i [@@arg7] [ result [UItitlebutton_@i ;] ]
					] [ result [UItitlebutton_mini ; UItitlebutton_exit] ])
				]
				uicolor $c_menu 0.01 0.01 [
					uitiledimage "media/interface/ui/ui_bg0.png" $.UI_pad_SXL $.UI_pad_SXL
					uispace $.UI_pad_L $.UI_pad_L [@@@@arg2]
					uiclamp* 1 1 1 1
				]
			]
		] [
			uicolor $c_menu 0.01 0.01 [
				uitiledimage "media/interface/ui/ui_bg0.png" $.UI_pad_SXL $.UI_pad_SXL
				uispace $.UI_pad_L $.UI_pad_L [@@@arg2]
				uiclamp* 1 1 1 1
			]
		])
		uioutline $c_line
		uiclamp* 1 1 1 1
	] [
		UIblurbg 1
		UIcoswaveset @arg1
		@arg3
	] [
		UIblurbg -1
		@arg4
	]
]

// UIpanel "NAME" [ children ] [ on-show ] [ on-hide ] [ ui-children ]
UIpanel = [
	newui $arg1 [
		@arg5
		uicolor $c_diag2 0.01 0.01 [
			uispace $.UI_pad_L $.UI_pad_L [ @@@arg2 ]
			uioutline $c_line
			uiclamp* 1 1 1 1
		]
		uiclamp* 1 1 1 1
	] [
		UIcoswaveset @arg1
		@arg3
	] $arg4
]

// UIstddialog "NAME" [ children ] [ true ] [ false ] [ on-show ] [ on-hide ]
UIstddialog = [
	newui $arg1 [
		uispace 0.025 0 [
			uiclamp 1 1 1 1
			UIbox "box d n d d -1" 0 0 [ uiclamp 1 1 1 1 ]
		]
		uispace 0 $.UI_pad_L [
			uihlist 0 [
				uifill 0 0.065 [
					uialign -2 -1
					UIbuttonarea "" 0.05 0.05 [
						uialign -2 1
						UIfastimg "ui/" "yes" "" (uihold? 0.04 (uihover? 0.044 0.04))
						uirelease [ @@@@@@arg3 ; hideui $uiname ]
					]
				]
				@@@arg2
				uifill 0 0.065 [
					uialign -2 -1
					UIbuttonarea "" 0.05 0.05 [
						uialign -2 1
						UIfastimg "ui/" "no" "" (uihold? 0.04 (uihover? 0.044 0.04))
						uirelease [ @@@@@@arg4 ; hideui $uiname ]
					]
				]
			]
		]
	] [
		UIblurbg 1
		UIcoswaveset @arg1
		@arg5
	] [
		UIblurbg -1
		@arg6
	]
]

// UIquickdialog "NAME" [ ui/bg-children ] [ children ] [ on-show ] [ on-hide ]
UIquickdialog = [
	newui $arg1 [
		@arg2
		uispace $.UI_pad_L $.UI_pad_L [
			uiclamp 1 1 1 1
			@@arg3
		]
	] [
		UIcoswaveset @arg1
		@arg4
	] $arg5
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #2  fastimg, bar, triangle, checkbox, radio, arrowbutton & keybox                                       //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UIfastimg PATH1 PATH2 PATH3 X Y [ children ]
UIfastimg = [
	if (< $numargs 5) [ arg5 = $arg4 ]
	uiimage (concatword "media/interface/" $arg1 $arg2 $arg3 ".png") $arg4 $arg5 [ arg6 ]
]

// UIbar CLAMP-X CLAMP-Y SPACE-X SPACE-Y
UIbar = [
	if (> $numargs 2) [
		uispace $arg3 $arg4 [
			uiclamp $arg1 $arg1 $arg2 $arg2
			uiline $c_line
			uiclamp- $arg1 $arg1 $arg2 $arg2
		]
	] [
		uiline $c_line
		uiclamp- $arg1 $arg1 $arg2 $arg2
	]
]

// UItriangle 0xCOLOR X Y ROTATION
UItriangle = [
	if $arg1 [] [ arg1 = $c_fill ]
	uigroup [
		uitriangle $arg1 $arg2 $arg3 $arg4
		uitriangleoutline $arg1 $arg2 $arg3 $arg4
	]
]

// UIcheckbox [ condition ] SIZE 0xAARRGGBB/ICON-ON 0xAARRGGBB/ICON-OFF [ children ]
UIcheckbox = [
	if $.UI_useimg [ UIfastimg "ui/" "ui_checkbox" (!= (arg1) 0) $arg2 $arg2 ] [
		if $arg4 [] [ arg4 = $c_cb0 ]
		if $arg3 [] [ arg3 = $c_cb1 ]
		uicolor (? (arg1) $arg3 $arg4) $arg2 $arg2 [
			uimodvgradient 0xFFFFFF 0xB8B8B8
			doargs $arg5
			uioutline $c_line
			uiclamp* 1 1 1 1
		]
	]
]

// UIradio [ condition ] SIZE 0xAARRGGBB/ICON-ON 0xAARRGGBB/ICON-OFF [ children ]
UIradio = [
	if $.UI_useimg [ UIfastimg "ui/" "ui_radio" (!= (arg1) 0) $arg2 $arg2 ] [
		if $arg4 [] [ arg4 = $c_rd0 ]
		if $arg3 [] [ arg3 = $c_rd1 ]
		uicircleoutline $c_line $arg2 [
			uicircle (? (arg1) $arg3 $arg4) $arg2 [
				//uimodvgradient 0xFFFFFF 0xB8B8B8
				doargs $arg5
				uiclamp* 1 1 1 1
			]
		]
	]
]

// UIkeybox X Y [ key-children ] 0xAARRGGBB [ children ]
UIkeybox = [
	if $arg4 [] [ arg4 = 0x505050 ]
	uigroup [
		uicolor $arg4 (-f $arg1 0.004) (-f $arg2 0.003) [ uimodvgradient 0xFFFFFF $c_grdmod ; uiclamp- 1 1 1 1 ]
		uistretchedimage "media/interface/ui/io/frame.png" 0.025 0.025 [ uifill $arg1 $arg2 $arg5 ]
		uioffset 0 -0.003 [ doargs $arg3 ]
	]
]





//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #3  settip, cleartip, hoversound, tabaction & blurbg                                                    //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

UIsettip     = [ uihover [ [.@[uiname]_tip] = $arg1 ] ]
UIcleartip   = [ uihover [] [ [.@[uiname]_tip] = "" ] ]

UIhoversound = [
	if $.UI_sounds [
		uitarget 0 0 [
			uiclamp 1 1 1 1
			if (&& $ui!hover? [! $uihold?] [! $uieschold?]) [
				playsound @@@(registersound "uihover.wav" 150)
			]
			uihover [ uitarget 0 0 ; uiclamp- 1 1 1 1 ]
		]
	]
]

// UItabaction STATE_1 STATE_2 [ action_1 ] [ action_2 ]
UItabaction = [
	if $.UI_clicktab [
		$arg1 $arg3
	] [
		$arg2 (? (< $numargs 4) $arg3 $arg4)
	]
]

// UIblurbg 1/-1
UIblurbg = [
	case $arg1 1 [
		if (! $.UI_blurbg) [
			addpostfx "sobel"
			loop i 10 [
				addpostfx "hblur5"
				addpostfx "vblur5"
			]
		]
		.UI_blurbg (+ $.UI_blurbg $arg1)
	] -1 [
		.UI_blurbg (+ $.UI_blurbg $arg1)
		if (! $.UI_blurbg) clearpostfx
	]
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #4  basicfield, basiceditor, tooltipbox & activetable                                                   //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////


// UIbasicfield VAR STRLEN [ on-change ] TEXTSIZE [ children ]
UIbasicfield = [
	uicolor $c_menu_m 0 0 [
		uihover [ uioutline (coswave.INT $c_line $c_rd0 500) ] [ uioutline $c_line ]
		uiclamp- 1 1 1 1
		uifield $arg1 $arg2 $arg3 $arg4 [
			arg5
			uifocus [ uioutline $c_focus ]
			uiclamp- 1 1 1 1
		]
	]
]

// UIbasiceditor VAR STRLEN STRLINES TEXTSIZE [ on-change ] PERSISTENCE [ children ]
UIbasiceditor = [
	uicolor $c_menu_m 0 0 [
		uihover [ uioutline (coswave.INT $c_line $c_rd0 500) ] [ uioutline $c_line ]
		uiclamp- 1 1 1 1
		uitexteditor $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [
			arg7
			uifocus [ uioutline $c_focus ]
			uiclamp- 1 1 1 1
		]
	]
]

// UItooltipbox X Y [ children ] SCROLLSPEED [ bottom children ]
UItooltipbox = [
	UIcleartip
	if (=f $arg4 0.0) [ arg4 = (*f $arg2 2) ]
	local diagline ; diagline = [ uiline $c_line $.UI_pad_S $.UI_pad_3XL ]
	uihlist 0 [
		uiline $c_line 0 $arg2 [ uialign 0 -1 ]
		uivlist 0 [
			uigroup [
				uiscroll (+f $arg1 $.UI_pad_L $.UI_pad_3XL) $arg2 [
					uifill $arg1 $arg2 $arg3
				]
				UIbar 1 ; uialign- 0 -1 // gotta make sure it stays on top.
			]
			UIbar 1
			uihlist 0 [
				uiclamp 1 1
				uihlist $.UI_pad_S [
					diagline
					if (!=s (getalias [.@[uiname]_tip]) "") [
						UIfastimg "ui/" "ui_question" "1" $.UI_pad_3XL $.UI_pad_3XL
						uiclip 0 $.UI_pad_3XL [ uitext $[.@[uiname]_tip] 0.5 ]
					] [ UIfastimg "ui/" "ui_question" "0" $.UI_pad_3XL $.UI_pad_3XL ]
				]
				if (> $numargs 4) [
					uiclip 0 $.UI_pad_3XL [
						uihlist $.UI_pad_S [
							arg5
							uifill
						]
					]
				]
			]
			uioffset $.UI_pad_S 0 [
				uiclamp 1 1
				UIbar 1
			]
		]
		uivlist 0 [ // purely cosmetic lines
			uiclamp 0 0 1 1
			uiline $c_line $.UI_pad_L
			uigroup [
				diagline                  ; uialign- -1
				uiline $c_line $.UI_pad_L ; uialign-  0 1
			]
		]
		UIvscroll 0 (+f $arg2 $.UI_pad_3XL) $arg4
	]
]

// UIactivetable X Y TEXTSIZE [ "NAME" [ children ] [ action ] .. ] COLUMNS "tooltip"
UIactivetable = [
	uigrid $arg5 0 0 [
		UIsettip $arg6
		looplist3 n c a $arg4 [
			if (=s $n "") uifill [
				UIbutton "hold2" [
					c ; uitext $n $arg3
				] $arg1 $arg2 $a -1
			]
		]
	]
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #5  box, buttonarea & button                                                                            //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UIbox [ style(s) ] X Y [ children ]
UIbox = [
	uitarget $arg2 $arg3 [
		if (=s $arg1 "") [
			style_generic_default
		] [ doargs [style_generic_@arg1] ]
		uiclamp* 1 1 1 1
		doargs $arg4
	]
]

// UIbuttonarea [ style(s) ] X Y [ children ]
UIbuttonarea = [
	uitarget $arg2 $arg3 [
		UIhoversound
		if (=s $arg1 "") [
			style_button_default
		] [ doargs [style_button_@arg1] ]
		uiclamp* 1 1 1 1
		doargs $arg4
	]
]

// UIbutton [ style(s) ] [ button-children ] X Y [ on-release ] ROTATION [ children ] (USE-PADDING)
UIbutton = [
	if (< $numargs 8) [ arg8 = $.UI_pad_L ]
	uitarget $arg3 $arg4 [
		UIhoversound
		doargs $arg7
		if (=s $arg1 "") [
			style_button_default
		] [ doargs [style_button_@arg1] ]
		uirelease [ doargs $arg5 ]
		uiclamp* 1 1 1 1
		uispace $arg8 0 [
			uiclamp 1 1
			uihlist $.UI_pad_L [
				uialign $arg6 0
				doargs $arg2
			]
		]
	]
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #6  scrollbars, (slim/bar)sliders, (color/spectrum)sliders & optionslider                               //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UIvscroll X Y SCROLLSPEED
UIvscroll = [
	if (=f $arg1 0) [ arg1 = $.UI_pad_3XL ]
	arg4 = (*f $arg1 0.35) // arrow size
	if (< $numargs 3) [ arg3 = (*f $arg2 2) ]
	uivlist 0 [
		uiscrollarrow (-f 0 $arg3)
		UIbar 1
		uivscrollbar [
			uifill $arg1 $arg2 [ UIbar 0 1 ]
			uiscrollbutton [
				UIhoversound
				uiclamp 1 1
				style_button_hold5
				uiclamp* 1 1 1 1
				uispace 0 $arg4 [
					uiclamp 0 0 1 1
					style_generic_linesh 3 0.004 (*f $arg1 0.5)
					UItriangle $c_line $arg4 $arg4   0 ; uialign- 0 -1
					UItriangle $c_line $arg4 $arg4 180 ; uialign- 0  1
				]
			]
		]
		UIbar 1
		uiscrollarrow $arg3
	]
]

// UIvscroll X Y SCROLLSPEED
UIhscroll = [
	if (=f $arg2 0) [ arg2 = $.UI_pad_3XL ]
	arg4 = (*f $arg2 0.35) // arrow size
	if (< $numargs 3) [ arg3 = (*f $arg1 2) ]
	uihlist 0 [
		uiscrollarrow $arg3
		UIbar 0 1
		uihscrollbar [
			uifill $arg1 $arg2 [ UIbar 1 ]
			uiscrollbutton [
				UIhoversound
				uiclamp 0 0 1 1
				style_button_hold1
				uiclamp* 1 1 1 1
				uispace $arg4 0 [
					uiclamp 1 1
					style_generic_linesv 3 0.004 (*f $arg2 0.5)
					UItriangle $c_line $arg4 $arg4  90 ; uialign- -1
					UItriangle $c_line $arg4 $arg4 270 ; uialign-  1
				]
			]
		]
		UIbar 0 1
		uiscrollarrow (-f 0 $arg3)
	]
]

// UIvslider VAR MIN MAX STEP X Y [ children ] [ on-change ] ARROWSIZE:%
UIvslider = [
	arg9 = (*f $arg5 0.35)
	uivlist 0 [
		uisliderarrow -1 [
			UIbuttonarea "hold6" (*f $arg5 1.5) $arg5 [
				UItriangle @(uihover? $c_fill $c_line) $arg9 $arg9 0
				UItriangle @(uihover? $c_fill $c_line) $arg9 (divf $arg9 2) 180
			]
		]
		uivslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
			UIhoversound
			UIbox "" (-f $arg6 (*f $arg5 3)) (*f 0.75 $arg5)
			uisliderbutton [
				uifill $arg5 $.UI_pad_XL
				style_button_hold5
				uiclamp* 1 1 1 1
				if (< $numargs 10) [
					uispace $.UI_pad_L 0 arg7
				] arg7
			]
		]
		uisliderarrow 1 [
			UIbuttonarea "hold6" (*f $arg5 1.5) $arg5 [
				UItriangle @(uihover? $c_fill $c_line) $arg9 (divf $arg9 2) 0
				UItriangle @(uihover? $c_fill $c_line) $arg9 $arg9 180
			]
		]
	]
]

// UIhslider VAR MIN MAX STEP X Y [ children ] [ on-change ] ARROWSIZE:%
UIhslider = [
	arg9 = (*f $arg6 0.35)
	uihlist 0 [
		uisliderarrow -1 [
			UIbuttonarea "hold2" (*f $arg6 1.5) $arg6 [
				UItriangle @(uihover? $c_fill $c_line) $arg9 $arg9 90
				UItriangle @(uihover? $c_fill $c_line) $arg9 (divf $arg9 2) 270
			]
		]
		uihslider $arg1 $arg2 $arg3 $arg4 $arg8 [
			UIhoversound
			UIbox "" (-f $arg5 (*f $arg6 3)) (*f 0.75 $arg6)
			uisliderbutton [
				uifill $.UI_pad_XL $arg6
				style_button_hold1
				uiclamp* 1 1 1 1
				if (< $numargs 10) [
					uispace $.UI_pad_L 0 arg7
				] arg7
			]
		]
		uisliderarrow 1 [
			UIbuttonarea "hold2" (*f $arg6 1.5) $arg6 [
				UItriangle @(uihover? $c_fill $c_line) $arg9 (divf $arg9 2) 90
				UItriangle @(uihover? $c_fill $c_line) $arg9 $arg9 270
			]
		]
	]
]

// UIhbarslider VAR MIN MAX STEP X Y [ on-change ]
UIhbarslider = [
	local bars barx
	bars = (abs (divf (-f $arg2 $arg3) $arg4))
	barx = (divf (-f $arg5 $.UI_pad_XL (*f (- $bars 1) $.UI_pad_XS)) $bars)
	uigroup [
		UIhoversound
		uihslider $arg1 $arg2 $arg3 $arg4 $arg7 [
			uifill (*f 0.97 $arg5) $arg6
			uisliderbutton
		]
		arg6 = (*f 0.75 $arg6)
		UIbox "" $arg5 $arg6 [
			uispace $.UI_pad_S $.UI_pad_S [
				uialign -1
				uihlist $.UI_pad_XS [
					loop n (divf $$arg1 $arg4) [
						if $.UI_useimg [
							uistretchedimage (concatword "media/interface/ui/ui_bar" (= (+ $n 1) (divf $$arg1 $arg4)) ".png") 0.005 0.005 [
								uifill $barx (-f $arg6 $.UI_pad_XL)
							]
						] [ uivgradient $c_cb1 0x0095DA $barx (-f $arg6 $.UI_pad_XL) ]
					]
				]
			]
		]
	]
]

// UIhsliderslim VAR MIN MAX STEP X Y [ on-change ] [ label formatting ]
UIhsliderslim = [
	uiclip $arg5 $arg6 [
		UIhoversound
		UIbox "" $arg5 (*f 0.75 $arg6)
		uifont "default_outline" [
			uicolortext (if (> $numargs 7) [do [@arg8]] [result $$arg1]) @(? (|| uihover? uihold?) 0x98FFFFFF 0x60FFFFFF) 0.65
		]
		uihslider $arg1 $arg2 $arg3 $arg4 [ doargs $arg7 ] [
			uifill (absf (-f $arg5 (*f $.UI_pad_XL 2))) $arg6
			uisliderbutton [ uiline $c_fill 0 $arg6 ]
		]
	]
]

// UIvcolorslider VAR MIN MAX 0xCOLOR1 0xCOLOR2 X Y [ on-change ]
UIvcolorslider = [
	uiclip $arg6 $arg7 [
		UIhoversound
		uivgradient $arg5 $arg4 (*f 0.75 $arg6) $arg7 [ uioutline $c_line ; uiclamp- 1 1 1 1 ]
		uifont "default_outline" [ uivlist (*f $.UI_pad_L -1) [
			loop i (strlen (abs $$arg1)) [
				uicolortext (substr $$arg1 $i 1) @@@(? (|| uihover? uihold?) 0x98FFFFFF 0x60FFFFFF) 0.65
			]
		] ]
		uivslider $arg1 $arg2 $arg3 1 [ doargs $arg8 ] [
			uifill $arg6 (absf (-f $arg7 (*f $.UI_pad_XL 2)))
			uisliderbutton [ uiline $c_fill $arg6 0 ]
		]
	]
]

// UIhcolorslider VAR MIN MAX 0xCOLOR1 0xCOLOR2 X Y [ on-change ]
UIhcolorslider = [
	uiclip $arg6 $arg7 [
		UIhoversound
		uihgradient $arg4 $arg5 $arg6 (*f 0.75 $arg7) [ uioutline $c_line ; uiclamp- 1 1 1 1 ]
		uifont "default_outline" [
			uicolortext $$arg1 @(? (|| uihover? uihold?) 0x98FFFFFF 0x60FFFFFF) 0.65
		]
		uihslider $arg1 $arg2 $arg3 1 [ doargs $arg8 ] [
			uifill (absf (-f $arg6 (*f $.UI_pad_XL 2))) $arg7
			uisliderbutton [ uiline $c_fill 0 $arg7 ]
		]
	]
]

// UIhspectrumslider VAR MIN MAX X Y [ on-change ]
UIhspectrumslider = [
	uiclip $arg4 $arg5 [
		UIhoversound
		uihlist 0 [
			uiclamp 1 1
			looplist2 c1 c2 [
				0xFF0000 0xFFFF00
				0xFFFF00 0x00FF00
				0x00FF00 0x00FFFF
				0x00FFFF 0x0000FF
				0x0000FF 0xFF00FF
				0xFF00FF 0xFF0000
			] [ uihgradient $c1 $c2 0 (*f 0.75 $arg5) ]
			uiclamp* 1 1
		]
		uioutline $c_line $arg4 (*f 0.75 $arg5)
		uihslider $arg1 $arg2 $arg3 1 [ doargs $arg6 ] [
			uifill (absf (-f $arg4 (*f $.UI_pad_XL 2))) $arg5
			uisliderbutton [ uiline $c_fill 0 $arg5 ]
		]
	]
]

// UIlistslider VAR COMPARISON [ val1 val2 .. valn ] X Y [ on-change ] [ children ] ARROWSIZE:%
//UIlistslider = [
//	arg8 = (*f (minf $arg4 $arg5) 0.36) // arrow size
//	uigroup [
//		uifill $arg4 $arg5 [ UIbar 1 0 (divf $arg5 2) 0 ]
//		uihlist 0 [
//			uiclamp 1 1
//			UIbuttonarea "hold2" (*f $arg5 1.5) $arg5 [
//				uirelease [
//					n = ([listfind@arg2] $arg3 $$arg1 1)
//					if (< $n 0) [n = 0] [
//						n = (- $n 2)
//						if (< $n 0) [ n = (+ $n (listlen $arg3)) ]
//					]
//					$arg1 = (at $arg3 $n)
//					arg6
//				]
//				uihlist 0 [
//					UItriangle @(uihover? $c_fill $c_line) $arg8 $arg8 90
//					UItriangle @(uihover? $c_fill $c_line) $arg8 (divf $arg8 2) 270
//				]
//			]
//			UIbox "" $arg5 (*f $arg5 0.9) [
//				uiclip (-f $arg4 (*f $arg5 3)) (*f $arg5 0.9) [
//					uispace $.UI_pad_M 0 [
//						if (< $numargs 7) [
//							uitext (|| [[listassoc@arg2] $arg3 $$arg1] [ result "custom" ]) 0.6
//						] arg7
//					]
//				]
//			]
//			UIbuttonarea "hold2" (*f $arg5 1.5) $arg5 [
//				uirelease [
//					n = ([listfind@arg2] $arg3 $$arg1 1)
//					n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
//					$arg1 = (at $arg3 $n)
//					arg6
//				]
//				uihlist 0 [
//					UItriangle @(uihover? $c_fill $c_line) $arg8 (divf $arg8 2) 90
//					UItriangle @(uihover? $c_fill $c_line) $arg8 $arg8 270
//				]
//			]
//		]
//	]
//]

// UIoptionslider VAR [ values ] [ names ] X Y (CONDITION) [ on-change ] [ children ]
UIoptionslider = [
	local curitem totalnum
	curitem  = (indexof $arg2 $$arg1)
	totalnum = (listlen $arg2)
	if (< $numargs 6) [ arg6 = 1 ]
	uihlist $.UI_pad_L [
		UIbuttonarea "hold2" $arg5 $arg5 [
			if $.UI_useimg [ UIfastimg "ui/" "arrow_l" $uihover? ; uiclamp- 1 1 1 1 ] [
				UItriangle (uihover? $c_fill $c_line) (*f $arg5 0.3) (*f $arg5 0.3) 90
			]
			uirelease [ $arg1 = (at $arg2 (max 0 (- $curitem 1))) ; arg7 ]
		]
		uivlist 0 [
			uicolortext (|| [at $arg3 (* $curitem 2)] [ result "custom" ]) (
				if $arg6 [ doargs (at $arg3 (+ (* $curitem 2) 1)) ] [ result $c_gray ]
			) 0.6
			uihlist $.UI_pad_L [
				looplist n $arg2 [
					UIbox [box (if (= $$arg1 $n) [ ? $arg6 $c_cb1 $c_gray_l ] [ result $c_cb0 ]) n n d -1] (
						divf (-f $arg4 (*f $arg5 2) (*f $.UI_pad_L (+ $totalnum 1))) $totalnum
					) $.UI_pad_XS
				]
			]
			uifill 0 0.002
		]
		UIbuttonarea "hold2" $arg5 $arg5 [
			if $.UI_useimg [ UIfastimg "ui/" "arrow_r" $uihover? ; uiclamp- 1 1 1 1 ] [
				UItriangle (uihover? $c_fill $c_line) (*f $arg5 0.3) (*f $arg5 0.3) 270
			]
			uirelease [ $arg1 = (at $arg2 (min (- $totalnum 1) (+ $curitem 1))) ; arg7 ]
		]
	]
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  #7  HUD: hudcell, hudcellslider, entcell, entcolor12/24                                                 //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// UIhudcell X Y [ children ] X-spacing Y-spacing
UIhudcell = [
	if (< $numargs 4) [ arg4 = $.UI_pad_L ]
	if (< $numargs 5) [ arg5 = $.UI_pad_L ]
	UIbox [box $c_diag1 d n d] $arg1 $arg2 [
		uispace $arg4 $arg5 [
			uiclamp 1 1 1 1
			doargs $arg3
		]
	]
]

// UIentcell KEY[S] Y-spacing [ children ] TITLE
UIentcell = [
	local x active
	x = (listlen $arg1)
	x = (+f (*f $x $.UI_pad_D3XL) (*f (- $x 1) $.UI_pad_L))
	active = (|| [||iskeyheld @arg1] $uihover+?)
	UIbox [box (? $active $c_diag2 $c_diag1) d n d $active] $x (*f $.UI_pad_5XL 4) [
		uispace $.UI_pad_L $.UI_pad_L [
			uiclamp 1 1 1 1
			uivlist $arg2 [
				uialign -2 -1
				uiclamp 1 1
				if (!=s $arg4 "") [
					uihlist 0 [
						UIbox [box $c_diag1 n n d $active] (-f $.UI_pad_D3XL $.UI_pad_5XL (*f $.UI_pad_L 2)) (-f $.UI_pad_5XL $.UI_pad_S) [ uitext $arg4 0.55 ]
						UIkeybox $.UI_pad_5XL $.UI_pad_5XL [ uitext $arg1 0.5 ] (? (iskeyheld $arg1) $c_cb1)
					]
				]
				doargs $arg3
			]
		]
	]
]

// UIhudcellslider VAR MIN MAX STEP ENTATTR#
UIhudcellslider = [
	UIhsliderslim $arg1 $arg2 $arg3 $arg4 (-f $.UI_pad_D3XL (*f $.UI_pad_XL 2)) $.UI_pad_5XL [
		entattr $arg5 $$arg1
	]
]


UIentcolor12 = [
	local c1 c2 c3 color12 color24
	color12 = (& (entattr $arg1) 0xFFF)
	c1 =    (>> $color12 0x8)
	c2 = (& (>> $color12 0x4) 0xF)
	c3 = (&     $color12      0xF)
	color24 = (| (<< $c1 0x14) (<< $c2 0xC) (<< $c3 0x4) 0x0F0F0F)
	UIhcolorslider c1 0 15 (& $color24 0x00FFFF) (| $color24 0xFF0000) 0.18 0.036 [ entattr $arg1 (| (<< $c1 0x8) (& $color12 0x0FF)) ]
	UIhcolorslider c2 0 15 (& $color24 0xFF00FF) (| $color24 0x00FF00) 0.18 0.036 [ entattr $arg1 (| (<< $c2 0x4) (& $color12 0xF0F)) ]
	UIhcolorslider c3 0 15 (& $color24 0xFFFF00) (| $color24 0x0000FF) 0.18 0.036 [ entattr $arg1 (|     $c3      (& $color12 0xFF0)) ]
]


UIentcolor24 = [
	local c1 c2 c3 color24
	loop+ i 1 3 [ [c@i] = (& $[ea@i] 0xFF) ]
	color24 = (| (<< $c1 0x10) (<< $c2 0x8) $c3)
	UIhcolorslider c1 0 255 (& $color24 0x00FFFF) (| $color24 0xFF0000) 0.60 0.036 [ entattr 1 $c1 ]
	UIhcolorslider c2 0 255 (& $color24 0xFF00FF) (| $color24 0x00FF00) 0.60 0.036 [ entattr 2 $c2 ]
	UIhcolorslider c3 0 255 (& $color24 0xFFFF00) (| $color24 0x0000FF) 0.60 0.036 [ entattr 3 $c3 ]
]



