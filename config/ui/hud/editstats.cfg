
UIquickdialog "edithud" [
	uiallowinput (iskeyheld "LALT")
	uieschide 0
	uiabovehud
	uialign -1 1
] [
	uihlist $.UI_pad_L [
		if (iskeyheld "LALT") [
			UIhudcell 0.07 0.128 [
				UIfastimg "ui/" "config" 0 0.05 0.05
			]
		] [
			UIhudcell 0.07 0.128 [
				uivlist 0 [
					UIfastimg "ui/" "speed" "" 0.04 0.034
					uitext $floatspeed 0.6
					uifill 0 0.006
					uigroup [
						UIfastimg "ui/" "grid" "" 0.04 0.04
						uitext $gridpower 0.5
						uialign- 1 1
					]
				]
			]
		]
		if $enthavesel [
			local ea0 ea1 ea2 ea3 ea4
			loop i 5 [ [ea@i] = (entattr $i) ]
			cases $enttype "particles" [
				UIentpropertycell "1" 0.01 [ // XXX needs work
					UIlistslider ea0 "=" [
						0 "Fire" 11 "Spirit Fire" 12 "Smoke"
						4 "Flare" 7 "Lightning" 9 "Smoke Vent" 10 "Water Drip" 13 "Snow"
						32 "Lens Flare 1" 33 "Lens Flare 2" 34 "Lens Flare 3" 35 "Lens Flare 4"
						5 "Capture Bar" 6 "Versus Bar"
						1 "Steam" 2 "Fountain" 3 "Explosion"
					] 0.18 0.032 [ entset "particles" $ea0 ]
				] "Type"
				cond (> (indexof "0 11 12" $ea0) -1) [
					UIentpropertycell "2" 0.01 [
						UIhudcellslider ea1 0 1000 1
					] "Width"
					UIentpropertycell "3" 0.01 [
						UIhudcellslider ea2 0 1000 2
					] "Height"
					UIentpropertycell "4" 0 [
						UIentcolor12 3
					]
				] (> (indexof "5 6" $ea0) -1) [
					UIentpropertycell "2" 0.01 [
						UIhudcellslider ea1 0 100 1
					] "Percentage"
					UIentpropertycell "3" 0 [
						UIentcolor12 2
					]
					if (!= $ea0 5) [
						UIentpropertycell "4" 0 [
							UIentcolor12 3
						]
					]
				] (> (indexof "32 33 34 35" $ea0) -1) [
					UIentpropertycell "2 3 4" 0 [
						UIentcolor24
					] "" 0.62
				] (> (indexof "4 7 9 10 13" $ea0) -1) [
					UIentpropertycell "2" 0.01 [ // XXX needs work
						// NADA HERE
					] "Shape"
					UIentpropertycell "3" 0.01 [
						UIhudcellslider ea2 0 200 2
					] "Size"
					UIentpropertycell "4" 0 [
						UIentcolor12 3
					] ""
					UIentpropertycell "5" 0.01 [
						UIhudcellslider ea4 0 10000 4
					] "Fade Time"
				] (= $ea0 1) [
					UIentpropertycell "2" 0.01 [
						UIlistslider ea1 "=" [-3 "Center" 0 "+Z" 1 "+Y" 2 "+X" 3 "-Z" 4 "-Y" 5 "-X"] 0.18 0.032 [ entattr 1 $ea1 ]
					] "Orientation"
				] (= $ea0 2) [
					UIentpropertycell "2" 0.01 [
						UIlistslider ea1 "=" [0 "+Z" 1 "+Y" 2 "+X" 3 "-Z" 4 "-Y" 5 "-X"] 0.18 0.032 [ entattr 1 $ea1 ]
					] "Orientation"
					UIentpropertycell "3" 0 [
						UIentcolor12 2
					] ""
				] (= $ea0 3) [
					UIentpropertycell "2" 0.01 [
						UIhudcellslider ea1 -4 100 1
					] "Size"
					if (> $ea0 1) [
						UIentpropertycell "3" 0 [
							UIentcolor12 2
						] ""
					]
				]
			] "light" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 -1 500 0
				] "Radious"
				UIentpropertycell "2 3 4" 0 [
					UIentcolor24
				] "" 0.62
				UIentpropertycell "5" 0 [ // XXX needs work
					uivlist 0 [
						uiclamp 1 1
						UIbutton "hold2 $c_diag1 d d" [ UIcheckbox [& $ea4 1] 0.016 ; uitext "Shadowless" 0.5 ] 0 0.024 [ entattr 4 (^ $ea4 1) ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIcheckbox [& $ea4 2] 0.016 ; uitext "Static"     0.5 ] 0 0.024 [ entattr 4 (^ $ea4 2) ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIcheckbox [& $ea4 4] 0.016 ; uitext "Volumetric" 0.5 ] 0 0.024 [ entattr 4 (^ $ea4 4) ] -1
						uiclamp* 1 1
					]
				] "Properties"
			] "spotlight" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 1 85 0.18 0.032 [ entattr 0 $ea0 ]
				] "Cone Angle"
			] "envmap" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 1 85 0
				] "Size"
			] "jumppad" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 -100 100 0
				] "Axis Z"
				UIentpropertycell "2" 0.01 [
					UIhudcellslider ea1 -100 100 1
				] "Axis Y" 
				UIentpropertycell "3" 0.01 [
					UIhudcellslider ea2 -100 100 2
				] "Axis X"
				UIentpropertycell "4" 0.01 [
					UIhudcellslider ea3 -1 (- $nummapsounds 1) 3
				] "Sound ID#"
			] "teleport" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 0 49 0
				] "Teleport ID#"
				UIentpropertycell "2" 0.01 [
					UIhudcellslider ea1 -1 (- $nummapmodels 1) 1
				] "Model ID#"
				UIentpropertycell "3" 0.01 [ // XXX needs work
					//UIhudcellslider ea2 0 0 0
				] "Unused"
				UIentpropertycell "4" 0.01 [
					UIhudcellslider ea3 -1 (- $nummapsounds 1) 3
				] "Sound ID#"
			] "teledest" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 0 360 0
				] "Orientation"
				UIentpropertycell "2" 0.01 [
					UIhudcellslider ea1 0 49 1
				] "Teleport ID#"
				UIentpropertycell "3" 0.01 [
					UIhudcellslider ea2 0 1 1
				] "Maintain Vel."
			] "mapmodel" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 -1 (- $nummapmodels 1) 0
				] "Model ID#"
				UIentpropertycell "2" 0.01 [
					UIhudcellslider ea1 0 360 1
				] "Yaw"
				UIentpropertycell "3" 0.01 [
					UIhudcellslider ea2 0 360 2
				] "Pitch"
				UIentpropertycell "4" 0.01 [
					UIhudcellslider ea3 0 360 3
				] "Roll"
				UIentpropertycell "5" 0.01 [
					UIhudcellslider ea4 0 500 4
				] "Scale"
			] "decal" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 0 49 0 /// XXX (- $numdecalslots 1) 0
				] "Decal ID#"
				UIentpropertycell "2" 0.01 [
					UIhudcellslider ea1 0 360 1
				] "Yaw"
				UIentpropertycell "3" 0.01 [
					UIhudcellslider ea2 0 360 2
				] "Pitch"
				UIentpropertycell "4" 0.01 [
					UIhudcellslider ea3 0 360 3
				] "Roll"
				UIentpropertycell "5" 0.01 [
					UIhudcellslider ea4 1 200 4
				] "Scale"
			] "sound" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 0 (- $nummapsounds 1) 0
				] "Sound ID#"
				UIentpropertycell "2" 0.01 [
					UIhudcellslider ea1 1 500 1
				] "Size"
				UIentpropertycell "3" 0.01 [
					UIhudcellslider ea2 1 500 2
				] "Size"
			] "playerstart" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 0 360 0
				] "Orientation"
				UIentpropertycell "2" 0 [ // XXX needs work
					uivlist 0 [
						uiclamp 1 1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 0] 0.016 ; uitext "No Team"      0.5 ] 0 0.024 [ entattr 1 0 ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 1] 0.016 ; uitext "Team ^f8Azul" 0.5 ] 0 0.024 [ entattr 1 1 ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 2] 0.016 ; uitext "Team ^f3Rojo" 0.5 ] 0 0.024 [ entattr 1 2 ] -1
						uiclamp* 1 1
					]
				] "Team"
			] "flag" [
				UIentpropertycell "1" 0.01 [
					UIhudcellslider ea0 0 360 0
				] "Orientation"
				UIentpropertycell "2" 0 [ // XXX needs work
					uivlist 0 [
						uiclamp 1 1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 1] 0.016 ; uitext "Team ^f8Azul" 0.5 ] 0 0.024 [ entattr 1 1 ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 2] 0.016 ; uitext "Team ^f3Rojo" 0.5 ] 0 0.024 [ entattr 1 2 ] -1
						uiclamp* 1 1
					]
				] "Team"
			]
		] [

							// uitext (concat $entget ":^f8" $enthavesel "^f7selected") 0.6 // XXX wheeeeere shall i put this...

			uivlist $.UI_pad_L [
				cond $hmapedit [
					UIhudcell 0.62 0.036 [
						uihlist $.UI_pad_L [
							uialign -1
							uiclamp 0 0 1 1
							uihlist 0.003 [
								UIkeybox 0.048 0.024 [ uitext "Ctrl" 0.5 ]
								UIkeybox 0.024 0.024 [ uitext "H" 0.5 ]
							]
							UIbar 0 1
							uitext "Toggle Heightmapping Mode" 0.6
						]
					] 0.01 0
					UIhudcell 0.62 0.036 [
						uihlist $.UI_pad_L [
							uialign -1
							uiclamp 0 0 1 1
							uihlist 0 [
								UIkeybox 0.024 0.024 [ uitext "H" 0.5 ]
								UIfastimg "ui/" "io/" "mouseM" 0.026 0.026
							]
							UIbar 0 1
							UIfastimg "ui/" "brush" "" 0.024 0.024
							uitext $hbrushname 0.6
						]
					] 0.01 0
				] $blendpaintmode [
					UIhudcell 0.62 0.036 [
						uihlist $.UI_pad_L [
							uialign -1
							uiclamp 0 0 1 1
							uihlist 0.003 [
								UIkeybox 0.048 0.024 [ uitext "Ctrl" 0.5 ]
								UIkeybox 0.024 0.024 [ uitext "B" 0.5 ]
							]
							UIbar 0 1
							uitext "Toggle Blendpainting Mode" 0.6
						]
					] 0.01 0
					uihlist $.UI_pad_L [
						UIhudcell 0.28 0.036 [
							uihlist $.UI_pad_L [
								uialign -1
								uiclamp 0 0 1 1
								uihlist 0 [
									UIkeybox 0.024 0.024 [ uitext "B" 0.5 ]
									UIfastimg "ui/" "io/" "mouseM" 0.026 0.026
								]
								UIbar 0 1
								UIfastimg "ui/" "color" "" 0.024 0.024
								uitext (at $blendpaintmodes $blendpaintmode) 0.6
							]
						] 0.01 0
						UIhudcell 0.33 0.036 [
							uihlist $.UI_pad_L [
								uialign -1
								uiclamp 0 0 1 1
								UIfastimg "ui/" "io/" "mouseM" 0.026 0.026
								UIbar 0 1
								UIfastimg "ui/" "brush" "" 0.024 0.024
								uitext (getblendbrushname $curblendbrush) 0.6
							]
						] 0.01 0
					]
				] [
					UIhudcell 0.62 0.082 [
						uivlist 0.01 [
							uialign -1 -1
							uitext (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "])) 0.5
							uigrid 2 0.02 0.01 [
								uihlist 0.01 [
									uitext "WTR" 0.5
									uitext (tabify (format "%1k" $editstatwtr) 1) 0.5
									uitext (tabify (format "(%1%%)" $editstatvtr) 2) 0.5
								]
								uihlist 0.01 [
									uitext "WVT" 0.5
									uitext (format "%1k" $editstatwvt) 0.5
									uitext (format "(%1%%)" $editstatvvt) 0.5
								]
							]
							uialign* -1
						]
					]
				]
				UIhudcell 0.62 0.036 [
					uihlist $.UI_pad_L [
						uialign -1
						uifill 0.036 0 [
							uitext (max 0 $getseltex) 0.6
							uialign- 1
						]
						UItriangle $c_fill 0.01 0.01 270
						uitext (concatword "^f8" (gettexname $getseltex)) 0.6
					]
				] 0.01 0
			]
		]
		uialign* -2 1
	]
]

// =================================================================================================================================== \\

UIentpropertycell = [
	if $arg5 [] [ arg5 = 0.2 ]
	UIbox [box (? (|| $uihover? $uihold? [||iskeyheld @arg1]) $c_diag2 $c_diag1) d n d] $arg5 0.128 [
		uispace $.UI_pad_L $.UI_pad_L [
			uiclamp 1 1 1 1
			uivlist $arg2 [
				uialign -2 -1
				uiclamp 1 1
				if (!=s $arg4 "") [
					uihlist 0 [
						UIbox "box $c_diag1 n n d" 0.148 0.026 [ uitext $arg4 0.55 ]
						UIkeybox 0.032 0.032 [ uitext $arg1 0.5 ] (? (iskeyheld $arg1) $c_cb1)
					]
				]
				doargs $arg3
			]
		]
	]
]

UIhudcell = [
	if (< $numargs 4) [ arg4 = $.UI_pad_L ]
	if (< $numargs 5) [ arg5 = $.UI_pad_L ]
	UIbox [box $c_diag1 d n d] $arg1 $arg2 [
		uispace $arg4 $arg5 [
			uiclamp 1 1 1 1
			doargs $arg3
		]
	]
]


UIhudcellslider = [
	UIhsliderslim $arg1 $arg2 $arg3 1 0.18 0.032 [ entattr $arg4 $$arg1 ]
]

UIentcolor12 = [
	local c1 c2 c3 color12 color24
	color12 = (& (entattr $arg1) 0xFFF)
	c1 =    (>> $color12 0x8)
	c2 = (& (>> $color12 0x4) 0xF)
	c3 = (&     $color12      0xF)
	color24 = (| (<< $c1 0x14) (<< $c2 0xC) (<< $c3 0x4) 0x0F0F0F)
	UIhcolorslider c1 0 15 (& $color24 0x00FFFF) (| $color24 0xFF0000) 0.18 0.036 [ entattr $arg1 (| (<< $c1 0x8) (& $color12 0x0FF)) ]
	UIhcolorslider c2 0 15 (& $color24 0xFF00FF) (| $color24 0x00FF00) 0.18 0.036 [ entattr $arg1 (| (<< $c2 0x4) (& $color12 0xF0F)) ]
	UIhcolorslider c3 0 15 (& $color24 0xFFFF00) (| $color24 0x0000FF) 0.18 0.036 [ entattr $arg1 (|     $c3      (& $color12 0xFF0)) ]
]

UIentcolor24 = [
	local c1 c2 c3 color24
	loop+ i 1 3 [ [c@i] = (& $[ea@i] 0xFF) ]
	color24 = (| (<< $c1 0x10) (<< $c2 0x8) $c3)
	UIhcolorslider c1 0 255 (& $color24 0x00FFFF) (| $color24 0xFF0000) 0.60 0.036 [ entattr 1 $c1 ]
	UIhcolorslider c2 0 255 (& $color24 0xFF00FF) (| $color24 0x00FF00) 0.60 0.036 [ entattr 2 $c2 ]
	UIhcolorslider c3 0 255 (& $color24 0xFFFF00) (| $color24 0x0000FF) 0.60 0.036 [ entattr 3 $c3 ]
]



// (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "]))
// (format "wtr:%1k(%2%%)" $editstatwtr $editstatvtr)
// (format "wvt:%1k(%2%%)" $editstatwvt $editstatvvt)
// (format "evt:%1k" $editstatevt)
// (format "eva:%1k" $editstateva)
// (concatword "ond:" $editstatocta)
// (concatword "va:" $editstatva)
// (format "gl:%1(%2)" $editstatglde $editstatgeombatch)
// (concatword "oq:" $editstatoq)
// (concatword "pvs:" $editstatpvs)
