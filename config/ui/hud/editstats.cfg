
UIquickdialog "edithud" [
	uiallowinput (iskeyheld "LALT")
	uieschide 0
	uiabovehud
	uialign -1 1
] [
	uihlist $.UI_pad_L [
		if (iskeyheld "LALT") [
			UIhudcell 0.07 0.128 [
				UIfastimg "ui/" "config" 0 0.05 0.05
			]
		] [
			UIhudcell 0.07 0.128 [
				uivlist 0 [
					UIfastimg "ui/" "speed" "" 0.04 0.034
					uitext $floatspeed 0.6
					uifill 0 $.UI_pad_S
					uigroup [
						UIfastimg "ui/" "grid" "" 0.04 0.04
						uitext $gridpower 0.5
						uialign- 1 1
					]
				]
			]
		]
		if $enthavesel [
			local ea0 ea1 ea2 ea3 ea4
			loop i 5 [ [ea@i] = (entattr $i) ]
			cases $enttype "particles" [
				UIentcell "1" $.UI_pad_L [ // XXX needs work
					UIoptionslider ea0 [
						0 11 12
						4 7 9 10 13
						32 33 34 35
						5 6
						1 2 3
					] [
						"Fire" "Spirit Fire" "Smoke"
						"Flare" "Lightning" "Smoke Vent" "Water Drip" "Snow"
						"Lens Flare 1" "Lens Flare 2" "Lens Flare 3" "Lens Flare 4"
						"Capture Bar" "Versus Bar"
						"Steam" "Fountain" "Explosion"
					] 0.18 $.UI_pad_5XL 0 [ entset "particles" $ea0 ]
				] "Type"
				cond (> (indexof "0 11 12" $ea0) -1) [
					UIentcell "2" $.UI_pad_L [
						UIhudcellslider ea1 0 1000 1 1
					] "Width"
					UIentcell "3" $.UI_pad_L [
						UIhudcellslider ea2 0 1000 1 2
					] "Height"
					UIentcell "4" 0 [
						UIentcolor12 3
					]
				] (> (indexof "5 6" $ea0) -1) [
					UIentcell "2" $.UI_pad_L [
						UIhudcellslider ea1 0 100 1
					] "Percentage"
					UIentcell "3" 0 [
						UIentcolor12 2
					]
					if (!= $ea0 5) [
						UIentcell "4" 0 [
							UIentcolor12 3
						]
					]
				] (> (indexof "32 33 34 35" $ea0) -1) [
					UIentcell "2 3 4" 0 [
						UIentcolor24
					]
				] (> (indexof "4 7 9 10 13" $ea0) -1) [
					UIentcell "2" $.UI_pad_L [ // XXX needs work
						// NADA HERE
					] "Shape"
					UIentcell "3" $.UI_pad_L [
						UIhudcellslider ea2 0 200 1 2
					] "Size"
					UIentcell "4" 0 [
						UIentcolor12 3
					]
					UIentcell "5" $.UI_pad_L [
						UIhudcellslider ea4 0 50000 200 4
					] "Fade Time"
				] (= $ea0 1) [
					UIentcell "2" $.UI_pad_L [
						UIoptionslider ea1 [-3 0 1 2 3 4 5] ["Center" "+Z" "+Y" "+X" "-Z" "-Y" "-X"] 0.18 $.UI_pad_5XL 0 [ entattr 1 $ea1 ]
					] "Orientation"
				] (= $ea0 2) [
					UIentcell "2" $.UI_pad_L [
						UIoptionslider ea1 [0 1 2 3 4 5] ["+Z" "+Y" "+X" "-Z" "-Y" "-X"] 0.18 .UI_pad_5XL 0 [ entattr 1 $ea1 ]
					] "Orientation"
					UIentcell "3" 0 [
						UIentcolor12 2
					]
				] (= $ea0 3) [
					UIentcell "2" $.UI_pad_L [
						UIhudcellslider ea1 -4 100 1 1
					] "Size"
					if (> $ea0 1) [
						UIentcell "3" 0 [
							UIentcolor12 2
						]
					]
				]
			] "light" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 -1 500 1 0
				] "Radious"
				UIentcell "2 3 4" 0 [
					UIentcolor24
				]
				UIentcell "5" 0 [ // XXX needs work
					uivlist 0 [
						uiclamp 1 1
						UIbutton "hold2 $c_diag1 d d" [ UIcheckbox [& $ea4 1] $.UI_pad_2XL ; uitext "Shadowless" 0.5 ] 0 $.UI_pad_4XL [ entattr 4 (^ $ea4 1) ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIcheckbox [& $ea4 2] $.UI_pad_2XL ; uitext "Static"     0.5 ] 0 $.UI_pad_4XL [ entattr 4 (^ $ea4 2) ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIcheckbox [& $ea4 4] $.UI_pad_2XL ; uitext "Volumetric" 0.5 ] 0 $.UI_pad_4XL [ entattr 4 (^ $ea4 4) ] -1
						uiclamp* 1 1
					]
				] "Properties"
			] "spotlight" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 1 85 1 0
				] "Cone Angle"
			] "envmap" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 1 85 1 0
				] "Size"
			] "jumppad" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 -100 100 1 0
				] "Axis Z"
				UIentcell "2" $.UI_pad_L [
					UIhudcellslider ea1 -100 100 1 1
				] "Axis Y" 
				UIentcell "3" $.UI_pad_L [
					UIhudcellslider ea2 -100 100 1 2
				] "Axis X"
				UIentcell "4" $.UI_pad_L [
					UIhudcellslider ea3 -1 (- $nummapsounds 1) 1 3
				] "Sound ID#"
			] "teleport" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 0 49 1 0
				] "Teleport ID#"
				UIentcell "2" $.UI_pad_L [
					UIhudcellslider ea1 -1 (- $nummapmodels 1) 1 1
				] "Model ID#"
				UIentcell "3" $.UI_pad_L [ // XXX needs work
					UIhudcellslider ea2 0 49 1 1
				] "Trigger ID#" // can_teleport_# = [..]
				UIentcell "4" $.UI_pad_L [
					UIhudcellslider ea3 -1 (- $nummapsounds 1) 1 3
				] "Sound ID#"
			] "teledest" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 0 360 5 0
				] "Orientation"
				UIentcell "2" $.UI_pad_L [
					UIhudcellslider ea1 0 49 1 1
				] "Teleport ID#"
				UIentcell "3" $.UI_pad_L [
					UIhudcellslider ea2 0 1 1
				] "Maintain Vel."
			] "mapmodel" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 -1 (- $nummapmodels 1) 1 0
				] "Model ID#"
				UIentcell "2" $.UI_pad_L [
					UIhudcellslider ea1 0 360 1 1
				] "Yaw"
				UIentcell "3" $.UI_pad_L [
					UIhudcellslider ea2 0 360 1 2
				] "Pitch"
				UIentcell "4" $.UI_pad_L [
					UIhudcellslider ea3 0 360 1 3
				] "Roll"
				UIentcell "5" $.UI_pad_L [
					UIhudcellslider ea4 0 500 1 4
				] "Scale"
			] "decal" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 0 49 1 0 /// XXX (- $numdecalslots 1) 0
				] "Decal ID#"
				UIentcell "2" $.UI_pad_L [
					UIhudcellslider ea1 0 360 1 1
				] "Yaw"
				UIentcell "3" $.UI_pad_L [
					UIhudcellslider ea2 0 360 1 2
				] "Pitch"
				UIentcell "4" $.UI_pad_L [
					UIhudcellslider ea3 0 360 1 3
				] "Roll"
				UIentcell "5" $.UI_pad_L [
					UIhudcellslider ea4 1 200 1 4
				] "Scale"
			] "sound" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 0 (- $nummapsounds 1) 1 0
				] "Sound ID#"
				UIentcell "2" $.UI_pad_L [
					UIhudcellslider ea1 1 500 1 1
				] "Size"
				UIentcell "3" $.UI_pad_L [
					UIhudcellslider ea2 1 500 1 2
				] "Size"
			] "playerstart" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 0 360 1 0
				] "Orientation"
				UIentcell "2" 0 [ // XXX needs work
					uivlist 0 [
						uiclamp 1 1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 0] $.UI_pad_2XL ; uitext "No Team"      0.5 ] 0 $.UI_pad_4XL [ entattr 1 0 ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 1] $.UI_pad_2XL ; uitext "Team ^f8Azul" 0.5 ] 0 $.UI_pad_4XL [ entattr 1 1 ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 2] $.UI_pad_2XL ; uitext "Team ^f3Rojo" 0.5 ] 0 $.UI_pad_4XL [ entattr 1 2 ] -1
						uiclamp* 1 1
					]
				] "Team"
			] "flag" [
				UIentcell "1" $.UI_pad_L [
					UIhudcellslider ea0 0 360 0
				] "Orientation"
				UIentcell "2" 0 [ // XXX needs work
					uivlist 0 [
						uiclamp 1 1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 1] $.UI_pad_2XL ; uitext "Team ^f8Azul" 0.5 ] 0 $.UI_pad_4XL [ entattr 1 1 ] -1
						UIbutton "hold2 $c_diag1 d d" [ UIradio [= $ea1 2] $.UI_pad_2XL ; uitext "Team ^f3Rojo" 0.5 ] 0 $.UI_pad_4XL [ entattr 1 2 ] -1
						uiclamp* 1 1
					]
				] "Team"
			]
		] [

							// uitext (concat $entget ":^f8" $enthavesel "^f7selected") 0.6 // XXX wheeeeere shall i put this...

			uivlist $.UI_pad_L [
				cond $hmapedit [
					UIhudcell 0.62 0.036 [
						uihlist $.UI_pad_L [
							uialign -1
							uiclamp 0 0 1 1
							uihlist $.UI_pad_XS [
								UIkeybox $.UI_pad_SXL $.UI_pad_4XL [ uitext "Ctrl" 0.5 ]
								UIkeybox $.UI_pad_4XL $.UI_pad_4XL [ uitext "H" 0.5 ]
							]
							UIbar 0 1
							uitext "Toggle Heightmapping Mode" 0.6
						]
					] $.UI_pad_L 0
					UIhudcell 0.62 0.036 [
						uihlist $.UI_pad_L [
							uialign -1
							uiclamp 0 0 1 1
							uihlist 0 [
								UIkeybox $.UI_pad_4XL $.UI_pad_4XL [ uitext "H" 0.5 ]
								UIfastimg "ui/" "io/" "mouseM" 0.026 0.026
							]
							UIbar 0 1
							UIfastimg "ui/" "brush" "" 0.024 0.024
							uitext $hbrushname 0.6
						]
					] $.UI_pad_L 0
				] $blendpaintmode [
					UIhudcell 0.62 0.036 [
						uihlist $.UI_pad_L [
							uialign -1
							uiclamp 0 0 1 1
							uihlist .UI_pad_XS [
								UIkeybox $.UI_pad_SXL $.UI_pad_4XL [ uitext "Ctrl" 0.5 ]
								UIkeybox $.UI_pad_4XL $.UI_pad_4XL [ uitext "B" 0.5 ]
							]
							UIbar 0 1
							uitext "Toggle Blendpainting Mode" 0.6
						]
					] $.UI_pad_L 0
					uihlist $.UI_pad_L [
						UIhudcell 0.28 0.036 [
							uihlist $.UI_pad_L [
								uialign -1
								uiclamp 0 0 1 1
								uihlist 0 [
									UIkeybox $.UI_pad_4XL $.UI_pad_4XL [ uitext "B" 0.5 ]
									UIfastimg "ui/" "io/" "mouseM" 0.026 0.026
								]
								UIbar 0 1
								UIfastimg "ui/" "color" "" 0.024 0.024
								uitext (at $blendpaintmodes $blendpaintmode) 0.6
							]
						] $.UI_pad_L 0
						UIhudcell 0.33 0.036 [
							uihlist $.UI_pad_L [
								uialign -1
								uiclamp 0 0 1 1
								UIfastimg "ui/" "io/" "mouseM" 0.026 0.026
								UIbar 0 1
								UIfastimg "ui/" "brush" "" 0.024 0.024
								uitext (getblendbrushname $curblendbrush) 0.6
							]
						] $.UI_pad_L 0
					]
				] [
					UIhudcell 0.62 0.082 [
						uivlist $.UI_pad_L [
							uialign -1 -1
							uitext (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "])) 0.5
							uigrid 2 $.UI_pad_3XL $.UI_pad_L [
								uihlist $.UI_pad_XL [
									uitext "WTR" 0.5
									uitext (tabify (format "%1k" $editstatwtr) 1) 0.5
									uitext (tabify (format "(%1%%)" $editstatvtr) 2) 0.5
								]
								uihlist $.UI_pad_XL [
									uitext "WVT" 0.5
									uitext (format "%1k" $editstatwvt) 0.5
									uitext (format "(%1%%)" $editstatvvt) 0.5
								]
							]
							uialign* -1
						]
					]
				]
				UIhudcell 0.62 0.036 [
					uihlist $.UI_pad_L [
						uialign -1
						uifill 0.036 0 [
							uitext (max 0 $getseltex) 0.6
							uialign- 1
						]
						UItriangle $c_fill $.UI_pad_L $.UI_pad_L 270
						uitext (concatword "^f8" (gettexname $getseltex)) 0.6
					]
				] $.UI_pad_L 0
			]
			UIhudcell 0.128 0.128 [
				uivslotview (max 0 $getseltex)
				uiclamp- 1 1 1 1
			]
		]
		uialign* -2 1
	]
]


// (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "]))
// (format "wtr:%1k(%2%%)" $editstatwtr $editstatvtr)
// (format "wvt:%1k(%2%%)" $editstatwvt $editstatvvt)
// (format "evt:%1k" $editstatevt)
// (format "eva:%1k" $editstateva)
// (concatword "ond:" $editstatocta)
// (concatword "va:" $editstatva)
// (format "gl:%1(%2)" $editstatglde $editstatgeombatch)
// (concatword "oq:" $editstatoq)
// (concatword "pvs:" $editstatpvs)

veditdirection = [
	dir = (modf (+f $getcamyaw 45) 360)
	cond (< $dir 90) [

	] (< $dir 180) [

	] (< $dir 270) [

	] [

	]
]
