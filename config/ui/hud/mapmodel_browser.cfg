
UIquickdialog "mapmodel_browser" [
	uialign 1
	if (> $.mm_totalpg 1) [
		if (iskeyheld "LCTRL") [
			uiscrolldown [ UIscrollsound ; .mm_curpage = (- $.mm_totalpg 1) ]
			uiscrollup   [ UIscrollsound ; .mm_curpage = 0 ]
		] [
			uiscrolldown [ UIscrollsound ; ++ .mm_curpage 1 (- $.mm_totalpg 1) ]
			uiscrollup   [ UIscrollsound ; -- .mm_curpage 1 0 ]
		]
	]
] [
	UIhudcell 0 0 [
		uivlist $.UI_pad_L [
			uihlist 0 [
				UIbutton "hold2 $c_diag2" [
					UIcheckbox [= $.mm_doaction 1] 0 0 0 [
						if $arg1 [ UItriangle $c_line1 $.UI_pad_ML 0 180 ]
					]
					uitext "Create" 0.6
				] $.z 0 [ .mm_doaction = 1 ] -1
				uigroup [
					uifill $.z $.UI_pad_6XL
					uihlist (-f 0 $.UI_pad_MS) [
						uiclamp.y
						uifill $.UI_pad_DXS 0 [
							uifont "mono" [ uitext (+ $.mm_curpage 1) 0.7 ]
							uialign- 1 -1
						]
						uioffset 0 (-f 0 $.UI_pad_L) [
							uiclip 0 $.UI_pad_6XL [ uicolortext "/" $c_gray 1.2 ]
						] // text takes up a lot of vertical space, have to "cut" it
						uifill $.UI_pad_DXS 0 [
							uifont "mono" [ uitext (max $.mm_totalpg 1) 0.7 ]
							uialign- -1 1
						]
						uiclamp*y
					]
				]
				UIbutton "hold2 $c_diag2" [
					uitext "Replace" 0.6
					UIcheckbox [= $.mm_doaction 0] 0 0 0 [
						if $arg1 [ UItriangle $c_line1 $.UI_pad_ML 0 180 ]
					]
				] $.z 0 [ .mm_doaction = 0 ] 1
				uiclamp*y
			]
			uigroup [
				uifill $.x $.y
				uihover [] [ uihold [] [ .mm_selidx = -1 ] ]
				uigrid 3 0 0 [
					uialign -1 -1
					loopwhile+ i (* $.mm_curpage 15) 15 [< $i $nummapmodels] [
						n = (mod $i 15) // page normalized index
						uitarget $.z $.z [
							if (= $n $.mm_selidx) [
								uimodcolor $c_grdmod
								uimodelpreview (mapmodelname $i 1) "mapmodel"
								uiclamp*e
								uispace $.UI_pad_S $.UI_pad_MS [
									uifont "default_outline" [ uitext $i 0.55 ]
								] ; uialign- -1 -1
								uirelease [
									if $.mm_doaction [ newent mapmodel $i ] [
										if $enthavesel [
											entloop [ if (=s $enttype "mapmodel") [
												entset "mapmodel" $i (entattr 1) (entattr 2) (entattr 3) (entattr 4)
											] ]
										] [ newent mapmodel $i ]
									]
								]
							] [
								uimodcolor $c_boxmod
								uimodelpreview (mapmodelname $i 1) "mapmodel"
								uiclamp*e
								uispace $.UI_pad_S $.UI_pad_MS [
									uifont "default_outline" [ uicolortext $i (|A 0xA0 $c_white) 0.55 ]
								] ; uialign- -1 -1
								uihover [ .mm_selidx = $n ; UIsetmillis ]
								uioutline $c_line1 $.z $.z
							]
							if (mapmodelloaded $i) [
								uispace $.UI_pad_S $.UI_pad_MS [
									uifont "webdings_outline" [ uitext "a" 0.55 ]
								] ; uialign- 1 -1
							]
							UIhoversound
						]
					]
				]
				if (> $.mm_selidx -1) [ // drawing on top to avoid overlap from other cells
					uioffset (*f $.z (mod $.mm_selidx 3)) (*f $.z (div $.mm_selidx 3)) [
						uioutline (coswave.INT $c_cyan_t $c_line1 500) $.z $.z
					] ; uialign- -1 -1
				]
			]
			uihlist 0 [
				uifont "webdings" [
					uifill $.z
					if (> $.mm_curpage 0) [
						UIbuttonarea "hold2 $c_diag2" 0 0 [ uitext "7" 0.9 ] [
							uirelease [-- .mm_curpage 1 ]
						] ; uiclamp-e
					] [ uicolortext "7" $c_gray 0.9 ]
				]
				uifill $.z 0 [ UIfastimg "ui/" "io/" "mouseM" $.UI_pad_6XL ]
				uifont "webdings" [
					uifill $.z
					if (< (+ $.mm_curpage 1) (max $.mm_totalpg 1)) [
						UIbuttonarea "hold2 $c_diag2" 0 0 [ uitext "8" 0.9 ] [
							uirelease [ ++ .mm_curpage 1 ]
						] ; uiclamp-e
					] [ uicolortext "8" $c_gray 0.9 ]
				]
				uiclamp*y
			]
		]
	]
] [
	.y = (-f 1 $.UI_pad_DM $.UI_pad_DS)
	.z = (divf $.y 5)
	.x = (*f $.z 3)
	.mm_selidx   = -1
	.mm_totalpg  = (ceil (divf $nummapmodels 15))
	.mm_curpage  = 0
	hideui "texture_browser"
	hideui "map_browser"
	hideui "geo_prefab"
	hideui "editvars" 
] [ if $editing [ showui "editvars" ] ]
