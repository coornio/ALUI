
UIquickdialog "texture_browser" [
	uialign 1
] [
	UIhudcell 0 0 [
		uihlist $.UI_pad_L [
			uiscroll (*f $.UI_pad_DXL $.UI_texcol) (-f 1 (*f $.UI_pad_L 4)) [
				uifill (*f $.UI_pad_DXL $.UI_texcol) (-f 1 (*f $.UI_pad_L 4))
				uihover [] [ .UI_texsel = -1 ]
				uigrid $.UI_texcol 0 0 [
					uialign -1 -1
					loop i $numslots [ // or looptexmru
						uitarget $.UI_pad_DXL $.UI_pad_DXL [
							UIhoversound
							uispace 0.0006 0.0006 [
								uiclamp 1 1 1 1
								if (!= $i $.UI_texsel) [
									uimodcolor $c_boxmod
									uislotview $i
									uioutline $c_line
								] [
									uimodcolor $c_grdmod
									uislotview $i
									uioutline (coswave.INT $c_line $c_cyan_t (uihold? 250 500))
								]
								uiclamp* 1 1 1 1
							]
							uihover [
								if (!= $.UI_texsel $i) [
									.UI_texsel = $i
									UIcoswaveset
								]
								uispace $.UI_pad_S $.UI_pad_XS [
									uialign -1 -1
									uifont "default_outline" [ uicolortext $i 0x98FFFFFF 0.55 ]
								]
							]
							uipress [ UIcoswaveset ]
							uirelease [
								settex (getslottex $i)
								hideui $uiname
							]
						]
					]
				]
			]
			UIvscroll 0 (-f 1 (*f $.UI_pad_L 4)) 4.8
		]
	]
] [
	.UI_texsel = -1
	.UI_texcol = (+ (>f (divf $screenw $screenh) 1.3) 3)
	hideui "mapmodel_browser"
	hideui "geo_prefab"
	hideui "editvars"
] [ showui "editvars" ]
