
newui "texture_browser" [
    uialign 1
    uifill 0 1 [
        uihlist 0.01 [
            uialign 0 1
            uiscroll 0.42 0.78 [
                uihover [] [ _tex_sel = -1 ]
                uifill 0.42 0.78
                uigrid 4 0 0 [
                    uialign -1 -1
                    loop i $numslots [ // or looptexmru
                        UIfastimg "ui/" "layer" "3" 0.105 0.105 [
                            if (!=s (gettexname $i) "") [
                                uirelease [
                                    hideui "texture"
                                    settex (getslottex $i)
                                ]
                                uihold [
                                    uispace 0.003 0.003 [
                                        uiclamp 1 1 1 1
                                        uicolor $c_fill 0.01 0.01 [ uialign -1 -1 ; uioutline $c_fill 0.01 0.01 ]
                                        uicolor $c_fill 0.01 0.01 [ uialign  1  1 ; uioutline $c_fill 0.01 0.01 ]
                                    ]
                                ] [
                                    uihover [
                                        uispace 0.003 0.003 [
                                            uiclamp 1 1 1 1
                                            uicolor $c_fill 0.01 0.01 [ uialign  1 -1 ; uioutline $c_fill 0.01 0.01 ]
                                            uicolor $c_fill 0.01 0.01 [ uialign -1  1 ; uioutline $c_fill 0.01 0.01 ]
                                        ]
                                        _tex_sel = $i
                                    ]
                                ]
                            ]
                            uislotview $i 0.095 0.095
                        ]
                    ]
                ]
            ]
            UIvscroll 0.02 0.76 4.2
        ]
    ]
    UIbox "box d n bg0" 0.46 0.22 [
        uialign -2 -1
        uihlist 0.01 [
            UIvscroll 0.02 0.2 0.44
            uigroup [
                uiscroll 0.2 0.2 [
                    uivslotview $getseltex 0.2 0.2 [
                        uimodcolor 0x808080 0 0 [
                            uialign -1 -1
                            uispace 0.004 0 [ uitext $getseltex 0.6 ]
                            uioutline $c_line
                            uiclamp- 1 1 1 1
                        ]
                    ]
                ]
                uioutline $c_line
                uiclamp- 1 1 1 1
            ]
            uifill 0.2 0.2 [
                if (>= $_tex_sel 0) [
                    uislotview $_tex_sel 0 0 [
                        uimodcolor 0x808080 0 0 [
                            uialign -1 -1
                            uispace 0.004 0 [ uitext $_tex_sel 0.6 ]
                            uioutline $c_line
                            uiclamp- 1 1 1 1
                        ]
                    ]
                ] [
                    UIbox "box d d bg2 n" 0 0 [
                        uivlist 0 [
                            uitext "PREVIEW" 0.6
                            uitext "UNAVAILABLE" 0.6
                        ]
                    ]
                ]
                uioutline $c_line
                uiclamp* 1 1 1 1
            ]
        ]
    ]
] [
    _tex_sel = -1
    hideui "mapmodel_browser"
    hideui "editvars"
] [ showui "editvars" ]
