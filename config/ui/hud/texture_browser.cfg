
UIquickdialog "texture_browser" [
	uialign 1
] [
	UIhudcell 0 0 [
		uihlist $.UI_pad_L [
			uiscroll $.x $.y [
				uifill $.x $.y
				uihover [] [ .UI_texsel = -1 ]
				uigrid $.UI_texcol 0 0 [
					uialign -1 -1
					loop i $numslots [ // or looptexmru
						UIbuttonarea "none" $.UI_pad_DXL $.UI_pad_DXL [
							uispace 0.0006 0.0006 [
								uiclamp.e
								if (!= $i $.UI_texsel) [
									uimodcolor $c_boxmod
									uislotview $i
									uioutline $c_line
								] [
									uimodcolor $c_grdmod
									uislotview $i
									uioutline (coswave.INT $c_cyan_t $c_line (uihold? 250 500))
								]
								uiclamp*e
							]
							uihover [
								if (!= $.UI_texsel $i) [
									.UI_texsel = $i
									UIcoswaveset
								]
								uispace $.UI_pad_S $.UI_pad_MS [
									uialign -1 -1
									uifont "default_outline" [ uicolortext $i 0x98FFFFFF 0.55 ]
								]
							]
							uipress [ UIcoswaveset ]
							uirelease [
								settex (getslottex $i)
								hideui $uiname
							]
						]
					]
				]
			]
			UIvscroll 0 $.y 4.8
		]
	]
] [
	.UI_texsel = -1
	.UI_texcol = (+ (>f (divf $screenw $screenh) 1.3) 3)
	.x = (*f $.UI_pad_DXL $.UI_texcol)
	.y = (-f 1 (*f $.UI_pad_L 4))
	hideui "mapmodel_browser"
	hideui "map_browser"
	hideui "geo_prefab"
	hideui "editvars"
] [ showui "editvars" ]
