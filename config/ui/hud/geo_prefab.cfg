
UIquickdialog "geo_prefab" [
	uialign 1
] [
	UIhudcell 0 0 [
		uihlist $.UI_pad_L [
			uiscroll $.x $.y [
				uifill $.x $.y
				uihover [] [ .UI_obrsel = -1 ]
				uigrid 2 0 0 [
					uialign -1 -1
					local i ; i = 0
					loopfiles f "media/prefab" obr [
						UIbuttonarea "none" $.UI_pad_D4XL $.UI_pad_D4XL [
							uispace 0.0006 0.0006 [
								if (!= $i $.UI_obrsel) [
									uimodcolor $c_boxmod
									uioutline $c_line
								] [
									uimodcolor $c_grdmod
									uioutline (coswave.INT $c_cyan_t $c_line (uihold? 250 500))
								]
								uiclamp*e
							]
							uiprefabpreview $f $c_gray_l
							uiclamp*e
							uispace $.UI_pad_S $.UI_pad_MS [
								uialign -1 -1
								uifont "default_outline" [ uicolortext $i 0x98FFFFFF 0.55 ]
							]
							uihover [
								if (!= $i $.UI_obrsel) [
									.UI_obrsel = $i
									UIcoswaveset
								]
							]
							uipress [ UIcoswaveset ]
							uirelease [ pasteprefab $f ]
						]
						i = (+ $i 1)
					]
				]
			]
			UIvscroll 0 $.y 6.4
		]
	]
] [
	.UI_obrsel = -1
	.x = (*f $.UI_pad_D4XL 2)
	.y = (-f 1 (*f $.UI_pad_L 4))
	hideui "texture_browser"
	hideui "mapmodel_browser"
	hideui "map_browser"
	hideui "editvars" 
] [ showui "editvars" ]
