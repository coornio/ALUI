
UImenu "cci" [
    local numspec numplay numazul numrojo
    if $mainmenu [ hideui "cci" ] [
        cp_updateselected (listclients 1)
        UIcleartip
    ]
    //looplist cn (listclients 1) [
    //    if (isspectator $cn) [
    //        numspec = (+ $numspec 1)
    //    ] [
    //        numplay = (+ $numplay 1)
    //        case (getclientteam $cn) 1 [
    //            numazul = (+ $numazul 1)
    //        ] 2 [ numrojo = (+ $numrojo 1) ]
    //    ]
    //]

    uivlist 0.01 [
        uihlist 0.01 [
            uialign -1 -2
            // XXX incomplete buttons, placeholders
            UIbutton "" [
                uitext "Kick" 0.6
            ] 0.2 0.04 [
                selected = (listfilter i $selected [&& [! (isai $i)] [!= $i $getclientnum]])
                selected_kick = $selected
                looplist cn $selected [ kick $cn ]
            ] 0 [ UIsettip "Kick players except for (!bot & !self)" ]
            UIbutton "" [
                uitext "Spectate" 0.6
            ] 0.2 0.04 [
                selected = (listfilter i $selected [! (isai $i)])
                looplist cn $selected [ spectator 1 $cn ]
            ] 0 [ UIsettip "Put players into spectator mode except (!bot)" ]
            UIbutton "" [
                uitext "Unspectate" 0.6
            ] 0.2 0.04 [
                selected = (listfilter i $selected [! (isai $i)])
                looplist cn $selected [ spectator 0 $cn ]
            ] 0 [ UIsettip "Put players out of spectator mode (!bot)" ]
            UIbutton "" [
                uitext "Swap Team" 0.6
            ] 0.2 0.04 [
                looplist cn $selected [ case (getclientteam $cn) 1 [ setteam 2 $cn ] 2 [ setteam 1 $cn ] ]
            ] 0 [ UIsettip "Swap teams for selected players" ]
        ]
        UItooltipbox 0.8 0.364 [
            uihlist 0.01 [
                uialign -2 -1
                uiclamp 0 0 1 1
                uivlist 0 [
                    if (m_teammode $getmode) [
                        cp_clientpanel 1 0
                        cp_clientpanel 1 1
                    ] [ cp_clientpanel 0 0 ]
                ]
                uivlist 0 [
                    if (m_teammode $getmode) [
                        cp_clientpanel 2 0
                        cp_clientpanel 2 1
                    ] [ cp_clientpanel 0 1 ]
                ]
                uialign* -2 -1
            ]
        ] 0.64 [
            uitext $selected 0.5
        ]
        uihlist 0.01 [
            UIbutton "hold2" [
                UIcheckbox $cp_showspec 0.016
                uitext "Show Spec" 0.6
            ] 0.16 0.032 [
                cp_showspec = (! $cp_showspec)
                selected = (listfilter i $selected [! (isspectator $i)])
                cp_specfill = (? $cp_showspec 0.576 0.384)
            ] -1
            UIbutton "hold2" [
                UIcheckbox $cp_showbots 0.016
                uitext "Show Bots" 0.6
            ] 0.16 0.032 [
                cp_showbots = (! $cp_showbots)
                selected = (listfilter i $selected [! (isai $i)])
            ] -1
            UIbutton "hold2" [
                UIcheckbox $cp_showself 0.016
                uitext "Show Self" 0.6
            ] 0.16 0.032 [
                cp_showself = (! $cp_showself)
                selected = (listdel $selected $getclientnum)
            ] -1
        ]
    ]
] [] [] [] [ uitext "Client Control Interface" 0.8 ]

////////////////////////////////////////////////////////////////////////////////////////////////////

cp_showbots  = 1
cp_showself  = 1
cp_showspec  = 1

cp_updateselected = [ selected = (listintersect $selected $arg1) ]

cp_client = [
    UIbuttonarea (? (< (indexof $selected $arg1) 0) "hold2" "hold1") 0.395 0.032 [
        uirelease [
            if (< (indexof $selected $arg1) 0) [
                selected = (concat $selected $arg1)
            ] [ selected = (listdel $selected $arg1) ]
        ]
        uihlist 0 [
            uiclamp 1 1
            uicolortext (getclientcolorname $arg1) (scoreboardstatus $arg1) 0.55
            if $showclientnum [
                if (isai $arg1) [
                    uitext "BOT" 0.55
                ] [
                    uitext $arg1 0.55
                ]
            ]
        ]
    ]

]

cp_clientpanel = [
    uifill 0.395
    looplist cn (
        listfilter i (listclients $cp_showself) [
            && [
                if $arg2 [isspectator $i] [! (isspectator $i)]
            ] [
                case $arg1 1 [= (getclientteam $i) 1] 2 [= (getclientteam $i) 2] () 1
            ] [
                if $cp_showbots [result 1] [! (isai $i)]
            ]
        ]
    ) [ cp_client $cn ]
]
