
UImenu "cci" [
	local numspec numplay numazul numrojo
	if $mainmenu [ hideui "cci" ] [
		cp_updateselected (listclients 1)
	]
	//looplist cn (listclients 1) [
	//    if (isspectator $cn) [
	//        numspec = (+ $numspec 1)
	//    ] [
	//        numplay = (+ $numplay 1)
	//        case (getclientteam $cn) 1 [
	//            numazul = (+ $numazul 1)
	//        ] 2 [ numrojo = (+ $numrojo 1) ]
	//    ]
	//]

	uivlist $.UI_pad_L [
		uihlist $.UI_pad_L [
			UIbutton "hold2" [
				UIcheckbox $.cci_showspec 0.016
				uitext "Show Spec" 0.6
			] 0.16 0.032 [
				.cci_showspec (! $.cci_showspec)
				.cci_clients (listfilter i $.cci_clients [! (isspectator $i)])
			] -1
			UIbutton "hold2" [
				UIcheckbox $.cci_showbots 0.016
				uitext "Show Bots" 0.6
			] 0.16 0.032 [
				.cci_showbots (! $.cci_showbots)
				.cci_clients (listfilter i $.cci_clients [! (isai $i)])
			] -1
			UIbutton "hold2" [
				UIcheckbox $.cci_showself 0.016
				uitext "Show Self" 0.6
			] 0.16 0.032 [
				.cci_showself (! $.cci_showself)
				.cci_clients (listdel $.cci_clients $getclientnum)
			] -1
		]
		UItooltipbox 0.8 0.352 [
			uihlist $.UI_pad_L [
				uiclamp 0 0 1 1
				uialign -2 -1
				uivlist 0 [
					if (m_teammode $getmode) [
						cp_clientpanel 1 0
						cp_clientpanel 1 1
					] [ cp_clientpanel 0 0 ]
				]
				uivlist 0 [
					if (m_teammode $getmode) [
						cp_clientpanel 2 0
						cp_clientpanel 2 1
					] [ cp_clientpanel 0 1 ]
				]
				uialign* -2 -1
			]
		] 0.64 [
			uitext $.cci_clients 0.5
		]
		uihlist $.UI_pad_L [
			uialign -1 -2
			// XXX incomplete buttons, placeholders
			UIbutton "" [
				uitext "Kick" 0.6
			] 0.2 0.04 [
				.cci_clients (listfilter i $.cci_clients [&& [! (isai $i)] [!= $i $getclientnum]])
				selected_kick = $.cci_clients
				looplist cn $.cci_clients [ kick $cn ]
			] 0 [ UIsettip "Kick players (!bot & !self)" ]
			UIbutton "" [
				uitext "Spectate" 0.6
			] 0.2 0.04 [
				.cci_clients (listfilter i $.cci_clients [! (isai $i)])
				looplist cn $.cci_clients [ spectator 1 $cn ]
			] 0 [ UIsettip "Put players into spectator mode (!bot)" ]
			UIbutton "" [
				uitext "Unspectate" 0.6
			] 0.2 0.04 [
				.cci_clients (listfilter i $.cci_clients [! (isai $i)])
				looplist cn $.cci_clients [ spectator 0 $cn ]
			] 0 [ UIsettip "Put players out of spectator mode (!bot)" ]
			UIbutton "" [
				uitext "Swap Team" 0.6
			] 0.2 0.04 [
				looplist cn $.cci_clients [ case (getclientteam $cn) 1 [ setteam $cn 2 ] 2 [ setteam $cn 1 ] ]
			] 0 [ UIsettip "Swap teams for .cci_clients players" ]
		]
	]
] [] [] [] [ uitext "Client Control Interface" 0.8 ]

////////////////////////////////////////////////////////////////////////////////////////////////////


cp_updateselected = [ .cci_clients (listintersect $.cci_clients $arg1) ]

cp_client = [
	local selected ; selected = (>= (indexof $.cci_clients $arg1) 0)
	UIbuttonarea (
			? $selected "hold1" "hold2"
		) 0.39 0.032 [
		uirelease [
			if $selected [
				.cci_clients (listdel $.cci_clients $arg1)
			] [ .cci_clients (concat $.cci_clients $arg1) ]
		]
		uispace 0.006 0 [
			uiclamp 1 1
			uihlist 0 [
				uiclamp 1 1
				uicolortext (getclientcolorname $arg1) (scoreboardstatus $arg1) 0.55
				if $showclientnum [
					if (isai $arg1) [
						uitext (concatword (? $selected "^f8" "^f4") "BOT") 0.55
					] [ uitext (concatword (? $selected "^f8" "^f7") $arg1) 0.6 ]
					uialign- 1
				]
			]
		]
	]

]

cp_clientpanel = [
	uifill 0.39
	looplist cn (
		listfilter i (listclients $.cci_showself) [
			&& [
				if $arg2 [isspectator $i] [! (isspectator $i)]
			] [
				case $arg1 1 [= (getclientteam $i) 1] 2 [= (getclientteam $i) 2] () 1
			] [
				if $.cci_showbots [result 1] [! (isai $i)]
			]
		]
	) [ cp_client $cn $arg1 $arg2 ]
]
