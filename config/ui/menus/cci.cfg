
UImenu "cci" [
    local numspec numplay numazul numrojo
    if $mainmenu [ hideui "cci" ] [
        cp_updateselected (listclients 1)
        UIcleartip
    ]
    //looplist cn (listclients 1) [
    //    if (isspectator $cn) [
    //        numspec = (+ $numspec 1)
    //    ] [
    //        numplay = (+ $numplay 1)
    //        case (getclientteam $cn) 1 [
    //            numazul = (+ $numazul 1)
    //        ] 2 [ numrojo = (+ $numrojo 1) ]
    //    ]
    //]

    uivlist 0.01 [
        uihlist 0.01 [
            UIbutton "hold2" [
                UIcheckbox $cp_showspec 0.016
                uitext "Show Spec" 0.6
            ] 0.16 0.032 [
                cp_showspec = (! $cp_showspec)
                cnlist = (listfilter i $cnlist [! (isspectator $i)])
                cp_specfill = (? $cp_showspec 0.576 0.384)
            ] -1
            UIbutton "hold2" [
                UIcheckbox $cp_showbots 0.016
                uitext "Show Bots" 0.6
            ] 0.16 0.032 [
                cp_showbots = (! $cp_showbots)
                cnlist = (listfilter i $cnlist [! (isai $i)])
            ] -1
            UIbutton "hold2" [
                UIcheckbox $cp_showself 0.016
                uitext "Show Self" 0.6
            ] 0.16 0.032 [
                cp_showself = (! $cp_showself)
                cnlist = (listdel $cnlist $getclientnum)
            ] -1
        ]
        UItooltipbox 0.8 0.352 [
            uihlist 0.01 [
                uiclamp 0 0 1 1
                uialign -2 -1
                uivlist 0 [
                    if (m_teammode $getmode) [
                        cp_clientpanel 1 0
                        cp_clientpanel 1 1
                    ] [ cp_clientpanel 0 0 ]
                ]
                uivlist 0 [
                    if (m_teammode $getmode) [
                        cp_clientpanel 2 0
                        cp_clientpanel 2 1
                    ] [ cp_clientpanel 0 1 ]
                ]
                uialign* -2 -1
            ]
        ] 0.64 [
            uitext $cnlist 0.5
        ]
        uihlist 0.01 [
            uialign -1 -2
            // XXX incomplete buttons, placeholders
            UIbutton "" [
                uitext "Kick" 0.6
            ] 0.2 0.04 [
                cnlist = (listfilter i $cnlist [&& [! (isai $i)] [!= $i $getclientnum]])
                selected_kick = $cnlist
                looplist cn $cnlist [ kick $cn ]
            ] 0 [ UIsettip "Kick players except for (!bot & !self)" ]
            UIbutton "" [
                uitext "Spectate" 0.6
            ] 0.2 0.04 [
                cnlist = (listfilter i $cnlist [! (isai $i)])
                looplist cn $cnlist [ spectator 1 $cn ]
            ] 0 [ UIsettip "Put players into spectator mode except (!bot)" ]
            UIbutton "" [
                uitext "Unspectate" 0.6
            ] 0.2 0.04 [
                cnlist = (listfilter i $cnlist [! (isai $i)])
                looplist cn $cnlist [ spectator 0 $cn ]
            ] 0 [ UIsettip "Put players out of spectator mode (!bot)" ]
            UIbutton "" [
                uitext "Swap Team" 0.6
            ] 0.2 0.04 [
                looplist cn $cnlist [ case (getclientteam $cn) 1 [ setteam 2 $cn ] 2 [ setteam 1 $cn ] ]
            ] 0 [ UIsettip "Swap teams for cnlist players" ]
        ]
    ]
] [] [] [] [ uitext "Client Control Interface" 0.8 ]

////////////////////////////////////////////////////////////////////////////////////////////////////

cp_showbots  = 1
cp_showself  = 1
cp_showspec  = 1

cp_updateselected = [ cnlist = (listintersect $cnlist $arg1) ]

cp_client = [
    local selected ; selected = (>= (indexof $cnlist $arg1) 0)
    UIbuttonarea (
            ? $selected "hold1" "hold2"
        ) 0.39 0.032 [
        uirelease [
            if $selected [
                cnlist = (listdel $cnlist $arg1)
            ] [ cnlist = (concat $cnlist $arg1) ]
        ]
        uispace 0.006 0 [
            uiclamp 1 1
            uihlist 0 [
                uiclamp 1 1
                uicolortext (getclientcolorname $arg1) (scoreboardstatus $arg1) 0.55
                if $showclientnum [
                    if (isai $arg1) [
                        uitext (concatword (? $selected "^f8" "^f4") "BOT") 0.55
                    ] [ uitext (concatword (? $selected "^f8" "^f7") $arg1) 0.6 ]
                    uialign- 1
                ]
            ]
        ]
    ]

]

cp_clientpanel = [
    uifill 0.39
    looplist cn (
        listfilter i (listclients $cp_showself) [
            && [
                if $arg2 [isspectator $i] [! (isspectator $i)]
            ] [
                case $arg1 1 [= (getclientteam $i) 1] 2 [= (getclientteam $i) 2] () 1
            ] [
                if $cp_showbots [result 1] [! (isai $i)]
            ]
        ]
    ) [ cp_client $cn $arg1 $arg2 ]
]
