
UImenu "main" [
	uieschide (! $mainmenu)
	uihlist 0 [
		uivlist 0 [
			uifill $.UI_pad_D3XL
			UIbutton "hold2" [ uitext "Server Browser" 0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "servers" ]
			UIbutton "hold2" [ uitext "Map Browser"    0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "map_browser" ]
			UIbar 1 0 0 $.UI_pad_L
			//UIbutton "hold2" [ uitext "Player Setup"   0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "player_setup" ]
			UIbutton "hold2" [ uitext "Options"        0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "options" ]
			UIbutton "hold2" [ uitext "Controls"       0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "keybinds" ]
			if $mainmenu [
				UIbutton "hold2" [ uitext "Credits"    0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "credits" ]
			]
			UIbar 1 0 0 $.UI_pad_L
			UIbutton "hold2" [ uitext "Quit"           0.6 ] 0 $.UI_pad_6XL [
				if $confirmquit [ hideui $uiname ; showui "dialog_quit" ] quit
			]
			uiclamp* 1 1
		]
		UIbar 0 1 $.UI_pad_L
		if $mainmenu [
			uifill 0.392 0.392 [
				uiplayerpreview 0 (case $.ps_team 0 $playercolor 1 $playercolorazul 2 $playercolorrojo) $.ps_team $getweapon 0 0.36
				uialign- -2 -1
				uiclamp- 1 1
				uihlist 0 [
					uiclamp 1 1
					uivlist $.UI_pad_M [
						UIfastimg "solo" "" "" 0.032 0.032
						loop n 9 [
							uicolor (getplayercolor $n 0) 0.032 0.032 [
								uioutline (if (= $playercolor $n) [ result $c_fill ] [ - (getplayercolor $n 0) 0x161616 ])
								uiclamp- 1 1 1 1
								uipress [ playercolor $n ]
								uihover [ .ps_team 0 ]
							]
						]
					]
					uivlist $.UI_pad_M [
						UIfastimg "azul" "" "" 0.032 0.032
						loop n 4 [
							uicolor (getplayercolor $n 1) 0.032 0.032 [
								uioutline (if (= $playercolorazul $n) [ result $c_fill ] [ - (getplayercolor $n 1) 0x161616 ])
								uiclamp- 1 1 1 1
								uipress [ playercolorazul $n ]
								uihover [ .ps_team 1 ]
							]
						]
						UIfastimg "rojo" "" "" 0.032 0.032
						loop n 4 [
							uicolor (getplayercolor $n 2) 0.032 0.032 [
								uioutline (if (= $playercolorrojo $n) [ result $c_fill ] [ - (getplayercolor $n 2) 0x161616 ])
								uiclamp- 1 1 1 1
								uipress [ playercolorrojo $n ]
								uihover [ .ps_team 2 ]
							]
						]
					]
				]
				uivlist 0.002 [
					uialign 0 1
					
					UIbasicfield name 15 [] 0.7
					uifill
				]
			]
		] [
			uivlist 0 [
				uifill $.UI_pad_D3XL
				if (isspectator $getclientnum) [
					local canplay
					canplay = (|| [isconnected 0 0] [< $getmastermode 2] [ismaster $getclientnum])
					UIbutton "hold2" [
						uitext (concatword (? $canplay "" "^f4") "Join Game") 0.6
					] 0 $.UI_pad_6XL [ if $canplay [ hideui $uiname ; spectator 0 ] ]
				] [ UIbutton "hold2" [ uitext "Spectate"     0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; spectator 1 ] ]
				case $getteam 0 [ uifill 0 $.UI_pad_6XL ] 1 [
					UIbutton "hold2" [ uitext "Join ^f3Rojo" 0.6 ] 0 $.UI_pad_6XL [ team "rojo" ]
				] 2 [
					UIbutton "hold2" [ uitext "Join ^f1Azul" 0.6 ] 0 $.UI_pad_6XL [ team "azul" ]
				]
				//uifill 0 $.UI_pad_6XL
				uispace 0 $.UI_pad_L [ UIbar 1 ]
				UIbutton "hold2" [
					uivlist $.UI_pad_L [
						uitext "Client Control" 0.6
						uitext "Interface" 0.6
					]
				] 0 (*f $.UI_pad_6XL 2) [ hideui $uiname ; showui "cci" ]
				uispace 0 $.UI_pad_L [ UIbar 1 ]
				UIbutton "hold2" [ uitext "Disconnect" 0.6 ] 0 $.UI_pad_6XL [ disconnect ]
				uiclamp* 1 1
			]
		]
	]
]
