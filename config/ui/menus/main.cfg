
UImenu "main" [
	uieschide (! $mainmenu)
	uihlist 0 [
		uivlist 0 [
			uifill $.UI_pad_D3XL
			UIbutton "hold2" [ uitext "Server Browser" 0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "servers" ]
			UIbutton "hold2" [ uitext "Map Browser"    0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "map_browser" ]
			UIbar 1 0 0 $.UI_pad_L
			UIbutton "hold2" [ uitext "Player Profile" 0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "profile" ]
			UIbutton "hold2" [ uitext "Options"        0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "options" ]
			UIbutton "hold2" [ uitext "Controls"       0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "keybinds" ]
			UIbutton "hold2" [ uitext "Recorder"       0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "recorder" ]
			if $mainmenu [
				UIbutton "hold2" [ uitext "Credits"    0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; showui "credits" ]
			]
			UIbar 1 0 0 $.UI_pad_L
			UIbutton "hold2" [ uitext "Quit"           0.6 ] 0 $.UI_pad_6XL [
				if $confirmquit [ hideui $uiname ; showui "dialog_quit" ] quit
			]
			uiclamp*x
		]
		if $mainmenu [
			// blank
			UIbar 0 0 $.UI_pad_L
			UIclock 0.36 0 3
		] [
			UIbar 0 1 $.UI_pad_L
			uivlist 0 [
				uifill $.UI_pad_D3XL
				if (isspectator $getclientnum) [
					local canplay
					canplay = (|| [isconnected 0 0] [< $getmastermode 2] [ismaster $getclientnum])
					UIbutton "hold2" [
						uitext (concatword (? $canplay "" "^f4") "Join Game") 0.6
					] 0 $.UI_pad_6XL [ if $canplay [ hideui $uiname ; spectator 0 ] ]
				] [ UIbutton "hold2" [ uitext "Spectate"     0.6 ] 0 $.UI_pad_6XL [ hideui $uiname ; spectator 1 ] ]
				case $getteam 0 [ uifill 0 $.UI_pad_6XL ] 1 [
					UIbutton "hold2" [ uitext "Join ^f3Rojo" 0.6 ] 0 $.UI_pad_6XL [ team "rojo" ]
				] 2 [
					UIbutton "hold2" [ uitext "Join ^f1Azul" 0.6 ] 0 $.UI_pad_6XL [ team "azul" ]
				]
				//uifill 0 $.UI_pad_6XL
				UIbar 1 0 0 $.UI_pad_L
				UIbutton "hold2" [
					uivlist $.UI_pad_L [
						uitext "Client Control" 0.6
						uitext "Interface" 0.6
					]
				] 0 (*f $.UI_pad_6XL 2) [ hideui $uiname ; showui "cci" ]
				UIbar 1 0 0 $.UI_pad_L
				UIbutton "hold2" [ uitext "Disconnect" 0.6 ] 0 $.UI_pad_6XL [ disconnect ]
				uiclamp*x
			]
		]
	]
]

// helper UI to allow escaping advanced fields when
// clicking outside of the UI that they reside within
newui "KBhook" [
	uieschide 0
	if (getalias .UI_KBisoob) [
		uiallowinput 1
		uiclamp.e
		uipress    [ .UI_KBclean ]
		uiescpress [ .UI_KBclean ]
		uitarget
		uiclamp-e
	] [ uiallowinput 0 ]
	if $debugruncodestack [
		uiclamp.e
		uispace 0.01 0.1 [
			uialign 1 1
			uitext $runcodestack 1
		]
	]
] [] [ sleep 1 [ showui "KBhook" ] ] ; showui "KBhook"
