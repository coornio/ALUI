
UImenu "main" [
    uieschide (! $mainmenu)
    uihlist 0 [
        uivlist 0 [
            uifill 0.2
            UIbutton "hold2" [ uitext "Server Browser" 0.6 ] 0 0.04 [ hideui "main" ; showui "servers" ]
            UIbutton "hold2" [ uitext "Map Browser"    0.6 ] 0 0.04 [ hideui "main" ; showui "map_browser" ]
            uispace 0 0.01 [ UIbar 1 ]
            UIbutton "hold2" [ uitext "Player Setup"   0.6 ] 0 0.04 [ hideui "main" ; showui "player_setup" ]
            UIbutton "hold2" [ uitext "Options"        0.6 ] 0 0.04 [ hideui "main" ; showui "options" ]
            UIbutton "hold2" [ uitext "Controls"       0.6 ] 0 0.04 [ hideui "main" ; showui "controls" ]
            if $mainmenu [
                UIbutton "hold2" [ uitext "Credits"    0.6 ] 0 0.04 [ hideui "main" ; showui "credits" ]
            ]
            uispace 0 0.01 [ UIbar 1 ]
            UIbutton "hold2" [ uitext "Quit"           0.6 ] 0 0.04 [
                if $confirmquit [ hideui "main" ; showui "dialog_quit" ] quit
            ]
            uiclamp* 1 1
        ]
        if (! $mainmenu) [
            uispace 0.01 0 [ UIbar 0 1 ; uiclamp 1 1 1 1 ]
            uivlist 0 [
                uifill 0.2
                if (isspectator $getclientnum) [
                    local canplay
                    canplay = (|| [isconnected 0 0] [< $getmastermode 2] [ismaster $getclientnum])
                    UIbutton "hold2" [
                        uitext (concatword (? $canplay "" "^f4") "Join Game") 0.6
                    ] 0 0.04 [ if $canplay [ hideui "main" ; spectator 0 ] ]
                ] [ UIbutton "hold2" [ uitext "Spectate"     0.6 ] 0 0.04 [ hideui "main" ; spectator 1 ] ]
                case $getteam 0 [ uifill 0 0.04 ] 1 [
                    UIbutton "hold2" [ uitext "Join ^f3Rojo" 0.6 ] 0 0.04 [ team "rojo" ]
                ] 2 [
                    UIbutton "hold2" [ uitext "Join ^f1Azul" 0.6 ] 0 0.04 [ team "azul" ]
                ]
                uifill 0 0.04
                uispace 0 0.01 [ UIbar 1 ]
                UIbutton "hold2" [
                    uivlist 0.01 [
                        uitext "Client Control" 0.6
                        uitext "Interface" 0.6
                    ]
                ] 0 0.08 [ hideui main ; showui "cci" ]
                uispace 0 0.01 [ UIbar 1 ]
                UIbutton "hold2" [ uitext "Disconnect" 0.6 ] 0 0.04 [ disconnect ]
                uiclamp* 1 1
            ]
        ]
    ]
]
