
UImenu "servers" [
	refreshservers
	local selectedserv
	uivlist $.UI_pad_L [
		uihlist 0 [
			uiclamp 1 1
			UIbutton "hold2" [
				UIfastimg "ui/" "refresh" "" 0.030 0.030
				uitext "Update Server List" 0.6
			] 0 $.UI_pad_6XL [ updatefrommaster ] 0 [
				UIsettip "retrieve registered servers from the masterserver"
			]
			uihlist 0 [
				uispace $.UI_pad_L $.UI_pad_S [
					uiclamp 0 0 1 1
					UIbar 0 1
				]
				UIbuttonarea "hold2" 0.06 $.UI_pad_6XL [
					UIsettip "scan for servers on your local area network"
					UIfastimg "ui/" "lan" $searchlan 0.028 0.028
					uirelease [ searchlan (! $searchlan) ]
				]
				UIbuttonarea "hold2" 0.06 $.UI_pad_6XL [
					UIsettip "automatically update server information on display"
					UIfastimg "ui/" "web" $autoupdateservers 0.030 0.030
					uirelease [ autoupdateservers (! $autoupdateservers) ]
				]
				UIbuttonarea "hold2" 0.06 $.UI_pad_6XL [
					UIsettip "automatically sort servers by player amount & latency"
					UIfastimg "ui/" "sort" $autosortservers 0.030 0.030
					uirelease [
						.autosortservers (! $.autosortservers)
						autosortservers $.autosortservers
					]
				]
				uispace $.UI_pad_L $.UI_pad_S [
					uiclamp 0 0 1 1
					UIbar 0 1
				]
			]
			UIbutton "hold2" [
				uifill $.UI_pad_L 0 [ UItriangle 0 $.UI_pad_L $.UI_pad_S (? $.sb_showfilters 180 270) ]
				uitext (concatword (? $.sb_showfilters "^f9") "Server Filtering") 0.6
				UIfastimg "ui/" "piechart" "" 0.03 0.03
			] 0 $.UI_pad_6XL [ .sb_showfilters (! $.sb_showfilters) ] 0 [
				UIsettip "select filters with which to hide servers"
			]
		]
		if $.sb_showfilters [
			UIbox [box d n d d -1] 0 0.148 [
				uispace $.UI_pad_L $.UI_pad_L [
					uihlist $.UI_pad_L [
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"rDM"      [ UIcheckbox [& $.sb_mode  2] $.UI_pad_2XL ] [ .sb_mode (^ $.sb_mode  2) ]
							"pDM"      [ UIcheckbox [& $.sb_mode  4] $.UI_pad_2XL ] [ .sb_mode (^ $.sb_mode  4) ]
							"rTDM"     [ UIcheckbox [& $.sb_mode  8] $.UI_pad_2XL ] [ .sb_mode (^ $.sb_mode  8) ]
							"pTDM"     [ UIcheckbox [& $.sb_mode 16] $.UI_pad_2XL ] [ .sb_mode (^ $.sb_mode 16) ]
							"rCTF"     [ UIcheckbox [& $.sb_mode 32] $.UI_pad_2XL ] [ .sb_mode (^ $.sb_mode 32) ]
							"pCTF"     [ UIcheckbox [& $.sb_mode 64] $.UI_pad_2XL ] [ .sb_mode (^ $.sb_mode 64) ]
							"Edit"     [ UIcheckbox [& $.sb_mode  1] $.UI_pad_2XL ] [ .sb_mode (^ $.sb_mode  1) ]
							"Disabled" [ UIradio    [= $.sb_mode  0] $.UI_pad_2XL ] [ .sb_mode 0 ]
						] 2 "hide servers according to game mode"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"Auth"     [ UIcheckbox [& $.sb_mm  1] $.UI_pad_2XL ] [ .sb_mm (^ $.sb_mm  1) ]
							"Open"     [ UIcheckbox [& $.sb_mm  2] $.UI_pad_2XL ] [ .sb_mm (^ $.sb_mm  2) ]
							"Veto"     [ UIcheckbox [& $.sb_mm  4] $.UI_pad_2XL ] [ .sb_mm (^ $.sb_mm  4) ]
							"Locked"   [ UIcheckbox [& $.sb_mm  8] $.UI_pad_2XL ] [ .sb_mm (^ $.sb_mm  8) ]
							"Private"  [ UIcheckbox [& $.sb_mm 16] $.UI_pad_2XL ] [ .sb_mm (^ $.sb_mm 16) ]
							"Password" [ UIcheckbox [& $.sb_mm 32] $.UI_pad_2XL ] [ .sb_mm (^ $.sb_mm 32) ]
							"" [] []
							"Disabled" [ UIradio    [= $.sb_mm  0] 0.016 ] [ .sb_mm 0 ]
						] 2 "hide servers according to master mode"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"50+"      [ UIradio [= $.sb_ping   55] $.UI_pad_2XL ] [ .sb_ping   55 ]
							"100+"     [ UIradio [= $.sb_ping  110] $.UI_pad_2XL ] [ .sb_ping  110 ]
							"150+"     [ UIradio [= $.sb_ping  165] $.UI_pad_2XL ] [ .sb_ping  165 ]
							"250+"     [ UIradio [= $.sb_ping  270] $.UI_pad_2XL ] [ .sb_ping  270 ]
							"500+"     [ UIradio [= $.sb_ping  525] $.UI_pad_2XL ] [ .sb_ping  525 ]
							"Disabled" [ UIradio [= $.sb_ping 9999] $.UI_pad_2XL ] [ .sb_ping 9999 ]
						] 2 "hide servers according to latency"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"Empty"    [ UIcheckbox [= $.sb_players_e 1] $.UI_pad_2XL ] [ .sb_players_e (! $.sb_players_e) ]
							"Full"     [ UIcheckbox [= $.sb_players_f 1] $.UI_pad_2XL ] [ .sb_players_f (! $.sb_players_f) ]
							"Disabled" [ UIradio [&& [! $.sb_players_e] [! $.sb_players_f]] $.UI_pad_2XL ] [ .sb_players_e 0 ; .sb_players_f 0 ]
						] 1 "hide servers according to player amount"
						uialign* -2 -1
					]
				]
			]
		]
		UItooltipbox 0.96 (? $.sb_showfilters 0.482 0.64) [
			local numfiltered
			uihover [ autosortservers 0 ] [
				autosortservers $.autosortservers
				selectedserv = -1
			]
			UIsettip "join server"
			uivlist 0 [
				uialign -2 -1
				uiclamp 1 1
				loop i $numservers [
					if (servinfovalid $i) [
						if (&& [ // latency
							<= (servinfoping $i) $.sb_ping
						] [      // mastermode
							if $.sb_mm [! (& (<< 1 (+ (servinfomastermode $i) 1)) $.sb_mm)] 1
						] [      // gamemode
							if $.sb_mode [! (& (<< 1 (servinfomode $i)) $.sb_mode)] 1
						] [      // empty server
							if $.sb_players_e [servinfonumplayers $i] 1
						] [      // full server
							if $.sb_players_f [< (servinfonumplayers $i) (servinfomaxplayers $i)] 1
						]) [
							uihover+ [ selectedserv = $i ]
							UIserver $i
							uiclamp* 1 1
						]
					] [ numfiltered = (+ $numfiltered 1) ]
				]
				uiclamp* 1 1
			]
		] 0 [
			if (>= $selectedserv 0) [
				uitext (concatword "^f4" (servinfoname $selectedserv)) 0.5
				uitext "^f4::" 0.5
				uitextfill 2.39 0.5 [
					uitext (concatword "^f4" (servinfoport $selectedserv)) 0.5
					uialign- -1
				]
			]
			diagline
			uitextfill 8.25 0.5 [
				uitext (concatword "^f8" (- $numservers $numfiltered) " ^f7servers online") 0.5
				uialign- 1
			]
		]
	]
] [ initservers ] [] [
	uipress [ if (uivisible "dialog_serverconnect") [ hideui "dialog_serverconnect" ] ]
] [ uitext "Server Browser" 0.8 ]





UIserver = [
	UIbuttonarea "" 0 (*f $.UI_pad_5XL 2) [
		uirelease [
			if (!= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)) [
				if (> (servinfomastermode $arg1) 2) [
					.sb_connectid $arg1
					showui "dialog_serverconnect"
				] [ connectservinfo $arg1 ]
			]
		]
		uihlist $.UI_pad_L [
			uialign -1
			if (&& [=s $connectedip (servinfoname $arg1)] [= $connectedport (servinfoport $arg1)]) [
				uiline
				UItriangle 0 $.UI_pad_3XL $.UI_pad_L 270
			] [ uifill $.UI_pad_3XL ]
			uicroppedimage "media/interface/ui/mm.png" $.UI_pad_6XL $.UI_pad_6XL [@(*f (+ (servinfomastermode $arg1) 1) 48)p] 0 48p 1
			uivlist $.UI_pad_S [
				uihlist $.UI_pad_L [
					uitext "^f4 .:" 0.6
					uicolortext (servinfodesc $arg1) 0xBFBFBF 0.62
				]
				uihlist $.UI_pad_L [
					uitext (servinfomodename $arg1) 0.55
					if (m_timed (servinfomode $arg1)) [
						uitext "^f4~" 0.6
						uitext (concatword "^f8" (servinfotime $arg1)) 0.55
					]
					if (!=s (servinfomap $arg1) "") [
						uitext "^f4~" 0.6
						uitext (concatword "^f9" (servinfomap $arg1)) 0.55
					]
				]
				uialign* -1
			]
		]
		uihlist $.UI_pad_L [
			uialign 1
			uifill $.UI_pad_SXL 0 [
				uivlist $.UI_pad_XS [
					local n ; n = 0
					loopwhile i 5 [<= (servinfoping $arg1) (at "525 270 165 110 55" $i)] [ n = (+ $i 1) ]
					uicroppedimage "media/interface/ui/conn.png" 0.025 0.025 [@(*f $n 64)p] 0 64p 1
					if (> (servinfoping $arg1) 2000) [ uitext "^f2LAG" 0.55 ] [
						uitext (servinfoping $arg1) 0.55
					]
				]
			]
			uifill $.UI_pad_XL 0 [
				uihlist $.UI_pad_S [
					uitext (servinfonumplayers $arg1) 0.75
					uicolortext "/" 0xBFBFBF 0.6
					uicolortext (servinfomaxplayers $arg1) 0xBFBFBF 0.55
				]
			]
			uiline
		]
	]
]

////////////////////////////////////////////////////////////////////////////////////////////////////


// search desc
//    if $.sb_searchdesc [>= (strstr (servinfodesc $i) $.sb_searchdesc) 0] 1
//] [      // search map
//    if $.sb_searchmap [>= (strstr (servinfomap $i) $.sb_searchmap) 0] 1
//] [

//cond (|| [= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)] [> (servinfomastermode $arg1) 2]) [
//    uihgradient 0xFFFFFF 0x80D04040
//] (>= (*f 100 (divf (servinfonumplayers $arg1) (servinfomaxplayers $arg1))) 60) [
//    uihgradient 0xFFFFFF 0x80D0B040
//]

//UIfastimg "" "key" (servinfohaspassword $arg1) 0.016 0.016
//uialign- -1  1
//UIfastimg "" "fav" (servinfokeep $arg1) 0.016 0.016
//uialign-  1  1