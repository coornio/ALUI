
UImenu "server_browser" [
    refreshservers
    uivlist 0.01 [
        uihover [] [ _SERVERBROWSER_tooltip = "" ]
        uihlist 0 [
            uiclamp 1 1
            uihlist 0 [
                UIbutton "hold2" [
                    UIfastimg "ui/" "refresh" "" 0.03 0.03
                    uitext "Update Server List" 0.6
                ] 0 0.04 [ updatefrommaster ] 0 [ uihover [
                    _SERVERBROWSER_tooltip = "retrieve registered servers from the masterserver"
                ] ]
                uispace 0.01 0.006 [
                    uiclamp 0 0 1 1
                    UIbar 0 1
                ]
            ]
            uihlist 0 [
                _SERVERBROWSER_optionsbutton $searchlan "lan" 0.028 [] [
                    searchlan (! $searchlan)
                ] "scan for servers on your local area network"
                _SERVERBROWSER_optionsbutton $autoupdateservers "web" 0.030 [] [
                    autoupdateservers (! $autoupdateservers)
                ] "automatically update server information on display"
                _SERVERBROWSER_optionsbutton $autosortservers "sort" 0.030 [] [
                    .autosortservers (! $.autosortservers)
                    autosortservers $.autosortservers
                ] "automatically sort servers by player amount & latency"
            ]
            uihlist 0 [
                uispace 0.01 0.006 [
                    uiclamp 0 0 1 1
                    UIbar 0 1
                ]
                UIbutton "hold2" [
                    uitext (concatword (? $_SERVERBROWSER_showfilters "^f9") "Server Filtering") 0.6
                    UIfastimg "ui/" "piechart" "" 0.03 0.03
                ] 0 0.04 [ _SERVERBROWSER_showfilters = (! $_SERVERBROWSER_showfilters) ] 0 [ uihover [
                    _SERVERBROWSER_tooltip = "select filters with which to hide servers"
                ] ]
            ]
        ]
        uihlist 0 [
            uialign 1 -2
            local h ; h = (? $_SERVERBROWSER_showfilters 0.462 0.66)
            uiline $c_line 0 $h [ uialign -2 -1 ]
            uivlist 0 [
                if $_SERVERBROWSER_showfilters [
                    UIbox "" 1.07 0.198 [
                        uispace 0.01 0.01 [
                            uiclamp 1 1 1 1
                            uivlist 0 [
                                uiclamp 1 1 1 1
                                uihlist 0.01 [
                                    uialign -1 -1
                                    _SERVERBROWSER_quickfilterlist [
                                        "rDM"      [ UIcheckbox [& $_SERVERBROWSER_mode  2] 0.016 ] [ _SERVERBROWSER_mode = (^ $_SERVERBROWSER_mode  2) ]
                                        "pDM"      [ UIcheckbox [& $_SERVERBROWSER_mode  4] 0.016 ] [ _SERVERBROWSER_mode = (^ $_SERVERBROWSER_mode  4) ]
                                        "rTDM"     [ UIcheckbox [& $_SERVERBROWSER_mode  8] 0.016 ] [ _SERVERBROWSER_mode = (^ $_SERVERBROWSER_mode  8) ]
                                        "pTDM"     [ UIcheckbox [& $_SERVERBROWSER_mode 16] 0.016 ] [ _SERVERBROWSER_mode = (^ $_SERVERBROWSER_mode 16) ]
                                        "rCTF"     [ UIcheckbox [& $_SERVERBROWSER_mode 32] 0.016 ] [ _SERVERBROWSER_mode = (^ $_SERVERBROWSER_mode 32) ]
                                        "pCTF"     [ UIcheckbox [& $_SERVERBROWSER_mode 64] 0.016 ] [ _SERVERBROWSER_mode = (^ $_SERVERBROWSER_mode 64) ]
                                        "Edit"     [ UIcheckbox [& $_SERVERBROWSER_mode  1] 0.016 ] [ _SERVERBROWSER_mode = (^ $_SERVERBROWSER_mode  1) ]
                                        "Disabled" [ UIradio    [= $_SERVERBROWSER_mode  0] 0.016 ] [ _SERVERBROWSER_mode = 0 ]
                                    ] 2 0.13 0.032 "hide servers according to game mode"
                                    UIbar 0 1
                                    _SERVERBROWSER_quickfilterlist [
                                        "Auth"     [ UIcheckbox [& $_SERVERBROWSER_mm  1] 0.016 ] [ _SERVERBROWSER_mm = (^ $_SERVERBROWSER_mm  1) ]
                                        "Open"     [ UIcheckbox [& $_SERVERBROWSER_mm  2] 0.016 ] [ _SERVERBROWSER_mm = (^ $_SERVERBROWSER_mm  2) ]
                                        "Veto"     [ UIcheckbox [& $_SERVERBROWSER_mm  4] 0.016 ] [ _SERVERBROWSER_mm = (^ $_SERVERBROWSER_mm  4) ]
                                        "Locked"   [ UIcheckbox [& $_SERVERBROWSER_mm  8] 0.016 ] [ _SERVERBROWSER_mm = (^ $_SERVERBROWSER_mm  8) ]
                                        "Private"  [ UIcheckbox [& $_SERVERBROWSER_mm 16] 0.016 ] [ _SERVERBROWSER_mm = (^ $_SERVERBROWSER_mm 16) ]
                                        "Password" [ UIcheckbox [& $_SERVERBROWSER_mm 32] 0.016 ] [ _SERVERBROWSER_mm = (^ $_SERVERBROWSER_mm 32) ]
                                        "" [] []
                                        "Disabled" [ UIradio    [= $_SERVERBROWSER_mm  0] 0.016 ] [ _SERVERBROWSER_mm = 0 ]
                                    ] 2 0.13 0.032 "hide servers according to master mode"
                                    UIbar 0 1
                                    _SERVERBROWSER_quickfilterlist [
                                        "50+"      [ UIradio [= $_SERVERBROWSER_ping   55] 0.016 ] [ _SERVERBROWSER_ping =   55 ]
                                        "100+"     [ UIradio [= $_SERVERBROWSER_ping  110] 0.016 ] [ _SERVERBROWSER_ping =  110 ]
                                        "150+"     [ UIradio [= $_SERVERBROWSER_ping  165] 0.016 ] [ _SERVERBROWSER_ping =  165 ]
                                        "250+"     [ UIradio [= $_SERVERBROWSER_ping  270] 0.016 ] [ _SERVERBROWSER_ping =  270 ]
                                        "500+"     [ UIradio [= $_SERVERBROWSER_ping  525] 0.016 ] [ _SERVERBROWSER_ping =  525 ]
                                        "Disabled" [ UIradio [= $_SERVERBROWSER_ping 9999] 0.016 ] [ _SERVERBROWSER_ping = 9999 ]
                                    ] 2 0.13 0.032 "hide servers according to latency"
                                    UIbar 0 1
                                    _SERVERBROWSER_quickfilterlist [
                                        "Empty"    [ UIcheckbox [= $_SERVERBROWSER_players_e 1] 0.016 ] [ _SERVERBROWSER_players_e = (! $_SERVERBROWSER_players_e) ]
                                        "Full"     [ UIcheckbox [= $_SERVERBROWSER_players_f 1] 0.016 ] [ _SERVERBROWSER_players_f = (! $_SERVERBROWSER_players_f) ]
                                        "Disabled" [ UIradio [&& [! $_SERVERBROWSER_players_e] [! $_SERVERBROWSER_players_f]] 0.016 ] [ _SERVERBROWSER_players_e = 0 ; _SERVERBROWSER_players_f = 0 ]
                                    ] 1 0.13 0.032 "hide servers according to player amount"
                                    uialign* -2 -1
                                ]
                            ]
                        ]
                    ]
                ]
                uigroup [
                    uiscroll 1.1 $h [
                        uifill 1.07 $h
                        uihover [ autosortservers 0 ] [
                            autosortservers $.autosortservers
                            _SERVERBROWSER_selected = -1
                        ]
                        uivlist 0 [
                            uialign -2 -1
                            uiclamp 1 1
                            _SERVERBROWSER_waiting = 0
                            uihover [ _SERVERBROWSER_tooltip = "join server" ]
                            loop i $numservers [
                                if (servinfovalid $i) [
                                    if (&& [ // latency
                                        <= (servinfoping $i) $_SERVERBROWSER_ping
                                    ] [      // search desc
                                        if $_SERVERBROWSER_searchdesc [>= (strstr (servinfodesc $i) $_SERVERBROWSER_searchdesc) 0] 1
                                    ] [      // search map
                                        if $_SERVERBROWSER_searchmap [>= (strstr (servinfomap $i) $_SERVERBROWSER_searchmap) 0] 1
                                    ] [      // mastermode
                                        if $_SERVERBROWSER_mm [! (& (<< 1 (+ (servinfomastermode $i) 1)) $_SERVERBROWSER_mm)] 1
                                    ] [      // gamemode
                                        if $_SERVERBROWSER_mode [! (& (<< 1 (servinfomode $i)) $_SERVERBROWSER_mode)] 1
                                    ] [      // empty server
                                        if $_SERVERBROWSER_players_e [servinfonumplayers $i] 1
                                    ] [      // full server
                                        if $_SERVERBROWSER_players_f [< (servinfonumplayers $i) (servinfomaxplayers $i)] 1
                                    ]) [
                                        local current_server
                                        current_server = (&& [=s $connectedip (servinfoname $i)] [= $connectedport (servinfoport $i)])
                                        uihover+ [ _SERVERBROWSER_selected = $i ]
                                        //uihover+ [ _SERVERBROWSER_tooltip = (concatword "connect to ^f4" (servinfoname $i) ":" (servinfoport $i)) ]
                                        _SERVERBROWSER_server $i
                                    ]
                                ] [ _SERVERBROWSER_waiting = (+ $_SERVERBROWSER_waiting 1) ]
                            ]
                            uiclamp* 1 1
                        ]
                    ]
                    UIbar 1 ; uialign- -2 -1 // it's here so it appears over mid-scrolled servers
                ]
                UIbar 1
                uihlist 0 [
                    uiclamp 1 1
                    uihlist 0.006 [
                        uiline $c_line 0.006 0.02
                        if (!=s $_SERVERBROWSER_tooltip "") [
                            UIfastimg "ui/" "ui_question" "1" 0.02 0.02
                            uiclip 0 0.02 [ uitext $_SERVERBROWSER_tooltip 0.5 ]
                        ] [ UIfastimg "ui/" "ui_question" "0" 0.02 0.02 ]
                    ]
                    uiclip 0 0.02 [
                        uihlist 0.006 [
                            if (>= $_SERVERBROWSER_selected 0) [
                                uitext (concatword "^f4" (servinfoname $_SERVERBROWSER_selected)) 0.5
                                uitext "^f4::" 0.5
                                uitextfill 2.39 0.5 [
                                    uitext (concatword "^f4" (servinfoport $_SERVERBROWSER_selected)) 0.5
                                    uialign- -1
                                ]
                            ]
                            uiline $c_line 0.006 0.02
                            uitextfill 8.25 0.5 [
                                uitext (concatword "^f8" (- $numservers $_SERVERBROWSER_waiting) " ^f7servers online") 0.5
                                uialign- 1
                            ]
                            uifill // spacing dud
                        ]
                    ]
                ]
                uioffset 0.006 0 [
                    uiclamp 1 1
                    UIbar 1
                ]
            ]
            uivlist 0 [ // the next segment is purely cosmetic line work
                uiclamp 0 0 1 1
                uiline $c_line 0.01
                uigroup [
                    uiline $c_line 0.006 0.02
                    uialign- -1
                    uiline $c_line 0.01
                    uialign- -2 1
                ]
            ]
            UIvscroll 0.022 0.68 2.64
        ]
    ]
] initservers [] [
    uipress [ if (uivisible "dialog_serverconnect") [ hideui "dialog_serverconnect" ] ]
] [ uitext "Server Browser" 0.8 ]

//                                                                      //
// SERVER BUTTON BELOW                                                  //
//                                                                      //

_SERVERBROWSER_server = [
    UIbuttonarea "" 0 0.066 [
        uirelease [
            if (!= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)) [
                if (> (servinfomastermode $arg1) 2) [
                    _SERVERBROWSER_connectname = (servinfoname $arg1)
                    _SERVERBROWSER_connectport = (servinfoport $arg1)
                    showui "dialog_serverconnect"
                ] [ connectservinfo $arg1 ]
            ]
        ]
        uiclamp 1 1
        uihlist 0.01 [
            uialign -1
            if $current_server [
                uifill // spacing dud
                UItriangle 0 0.032 0.01 270
            ] [ uifill 0.02 ]
            uifill 0.036 0.056 [ // --------------------------------- // mastermode, key to server, favorite
                uicroppedimage "media/interface/ui/mm.png" 0.04 0.04 (*f (+ (servinfomastermode $arg1) 1) 0.125) 0 0.125 1
                //uialign- -2 -1
                //uiimage (concatword $UI_path_icons "key" (servinfohaspassword $arg1) ".png") 0.016 0.016
                //uialign- -1  1
                //uiimage (concatword $UI_path_icons "favorite" (servinfokeep $arg1) ".png") 0.016 0.016
                //uialign-  1  1
            ]
            //uifill // spacing dud
            uivlist 0.006 [ // --------------------------------- // desc, gamemode, time, map
                uihlist 0.006 [
                    uitext "^f4::" 0.6 ; uialign- -2 -1
                    uicolortext (servinfodesc $arg1) 0xBFBFBF 0.62
                ]
                uihlist 0.012 [
                    uitext (servinfomodename $arg1) 0.55
                    if (m_timed (servinfomode $arg1)) [
                        uitext "^f4~" 0.6
                        uitext (concatword "^f8" (servinfotime $arg1)) 0.55
                    ]
                    if (!=s (servinfomap $arg1) "") [
                        uitext "^f4~" 0.6
                        uitext (concatword "^f9" (servinfomap $arg1)) 0.55
                    ]
                ]
                uialign* -1
            ]
        ]
        uihlist 0.01 [
            uialign 1 -2
            uigroup [ // --------------------------------- // map image
                UIfastimg "mapshot/" (servinfomap $arg1) "_w" 0.198 0.066
                UIbar 0 1 ; uialign- 1
            ]
            uifill 0.05 0 [ // --------------------------------- // ping
                uivlist 0.002 [
                    local n ; n = 0
                    loopwhile i 5 [<= (servinfoping $arg1) (at "525 270 165 110 55" $i)] [ n = (+ $i 1) ]
                    uicroppedimage "media/interface/ui/conn.png" 0.025 0.025 (*f $n 0.125) 0 0.125 1
                    uitext (servinfoping $arg1) 0.55
                ]
            ]
            uifill 0.12 0.066 [ // --------------------------------- // clients
                cond (|| [= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)] [> (servinfomastermode $arg1) 2]) [
                    uihgradient 0xFFFFFF 0x80D04040
                ] (>= (*f 100 (divf (servinfonumplayers $arg1) (servinfomaxplayers $arg1))) 60) [
                    uihgradient 0xFFFFFF 0x80D0B040
                ]
                uiclamp- 1 1 1 1
                uihlist 0.006 [
                    uitext (servinfonumplayers $arg1) 0.75
                    uicolortext "/" 0xBFBFBF 0.6
                    uicolortext (servinfomaxplayers $arg1) 0xBFBFBF 0.55
                ]
            ]
        ]
        uioutline $c_line
        uiclamp- 1 1 1 1
    ]
]

////////////////////////////////////////////////////////////////////////////////////////////////////

_SERVERBROWSER_mm        = 0
_SERVERBROWSER_mode      = 0
_SERVERBROWSER_ping      = 9999
_SERVERBROWSER_players_e = 0
_SERVERBROWSER_players_f = 0

_SERVERBROWSER_searchdesc = ""
_SERVERBROWSER_searchmap  = ""

_SERVERBROWSER_selected    = -1
_SERVERBROWSER_showfilters = 0

_SERVERBROWSER_connectname = ""
_SERVERBROWSER_connectport = ""

// _SERVERBROWSER_optionsbutton [ cond ] "icon name" SIZE [ children ] [ action ] [ button children ]
_SERVERBROWSER_optionsbutton = [
    UIbutton "hold2" [
        uigroup [
            UIfastimg "ui/" $arg2 (arg1) $arg3 $arg3
            arg4
        ]
    ] 0.06 0.04 arg5 0 [ uihover [ _SERVERBROWSER_tooltip = $arg6 ] ] 0
]

// _SERVERBROWSER_quickfilterlist [ "NAME" [ children ] [ action ] .. ] <COLUMNS> <BUTTON WIDTH> <BUTTON HEIGHT>
_SERVERBROWSER_quickfilterlist = [
    uigrid $arg2 0 0 [
        uihover [ _SERVERBROWSER_tooltip = $arg5 ]
        looplist3 n c a $arg1 [
            if (=s $n "") uifill [
                UIbutton "hold2" [
                    c ; uitext $n 0.55
                ] $arg3 $arg4 $a -1
            ]
        ]
    ]
]
