
UImenu "servers" [
	refreshservers
	uivlist $.UI_pad_L [
		uihlist 0 [
			uiclamp.x
			UIbutton "hold2" [
				UIfastimg "ui/" "refresh" "" 0.030
				uitext "Update Server List" 0.6
			] 0 $.UI_pad_6XL [ updatefrommaster ] 0 [
				UIsettip "retrieve registered servers from the masterserver"
			]
			uihlist 0 [
				UIbar 0 1 $.UI_pad_L $.UI_pad_S
				UIbuttonarea "hold2" $.UI_pad_DS $.UI_pad_6XL [
					UIsettip "scan for servers on your local area network"
					UIfastimg "ui/" "lan" $searchlan 0.028
					uirelease [ searchlan (! $searchlan) ]
				]
				UIbuttonarea "hold2" $.UI_pad_DS $.UI_pad_6XL [
					UIsettip "automatically update server information on display"
					UIfastimg "ui/" "web" $autoupdateservers 0.030
					uirelease [ autoupdateservers (! $autoupdateservers) ]
				]
				UIbuttonarea "hold2" $.UI_pad_DS $.UI_pad_6XL [
					UIsettip "automatically sort servers by player amount & latency"
					UIfastimg "ui/" "sort" $autosortservers 0.030
					uirelease [
						.autosortservers (! $.autosortservers)
						autosortservers $.autosortservers
					]
				]
				UIbar 0 1 $.UI_pad_L $.UI_pad_S
			]
			UIbutton "hold2" [
				UIcheckbox $.sb_showfilters 0 0 0 [
					if $arg1 [ UItriangle $c_line1 $.UI_pad_ML 0 180 ]
				]
				uitext "Server Filtering" 0.6
				UIfastimg "ui/" "piechart" "" 0.03
			] 0 $.UI_pad_6XL [ .sb_showfilters (! $.sb_showfilters) ] 0 [
				UIsettip "select filters with which to hide servers"
			]
		]
		if $.sb_showfilters [
			UIbox [box d n n d n] 0 0.148 [
				uispace $.UI_pad_L $.UI_pad_L [
					uihlist $.UI_pad_L [
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"rDM"      [ UIcheckbox [& $.sb_mode  2] ] [ .sb_mode (^ $.sb_mode  2) ]
							"pDM"      [ UIcheckbox [& $.sb_mode  4] ] [ .sb_mode (^ $.sb_mode  4) ]
							"rTDM"     [ UIcheckbox [& $.sb_mode  8] ] [ .sb_mode (^ $.sb_mode  8) ]
							"pTDM"     [ UIcheckbox [& $.sb_mode 16] ] [ .sb_mode (^ $.sb_mode 16) ]
							"rCTF"     [ UIcheckbox [& $.sb_mode 32] ] [ .sb_mode (^ $.sb_mode 32) ]
							"pCTF"     [ UIcheckbox [& $.sb_mode 64] ] [ .sb_mode (^ $.sb_mode 64) ]
							"Edit"     [ UIcheckbox [& $.sb_mode  1] ] [ .sb_mode (^ $.sb_mode  1) ]
							"Disabled" [ UIradio    [= $.sb_mode  0] ] [ .sb_mode 0 ]
						] 2 "hide servers according to game mode"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"Auth"     [ UIcheckbox [& $.sb_mm  1] ] [ .sb_mm (^ $.sb_mm  1) ]
							"Open"     [ UIcheckbox [& $.sb_mm  2] ] [ .sb_mm (^ $.sb_mm  2) ]
							"Veto"     [ UIcheckbox [& $.sb_mm  4] ] [ .sb_mm (^ $.sb_mm  4) ]
							"Locked"   [ UIcheckbox [& $.sb_mm  8] ] [ .sb_mm (^ $.sb_mm  8) ]
							"Private"  [ UIcheckbox [& $.sb_mm 16] ] [ .sb_mm (^ $.sb_mm 16) ]
							"Passcode" [ UIcheckbox [& $.sb_mm 32] ] [ .sb_mm (^ $.sb_mm 32) ]
							"" [] []
							"Disabled" [ UIradio    [= $.sb_mm  0] ] [ .sb_mm 0 ]
						] 2 "hide servers according to master mode"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"50+"      [ UIradio [= $.sb_ping   55] ] [ .sb_ping   55 ]
							"100+"     [ UIradio [= $.sb_ping  110] ] [ .sb_ping  110 ]
							"150+"     [ UIradio [= $.sb_ping  165] ] [ .sb_ping  165 ]
							"250+"     [ UIradio [= $.sb_ping  270] ] [ .sb_ping  270 ]
							"500+"     [ UIradio [= $.sb_ping  525] ] [ .sb_ping  525 ]
							"Disabled" [ UIradio [= $.sb_ping 9999] ] [ .sb_ping 9999 ]
						] 2 "hide servers according to latency"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"Empty"    [ UIcheckbox [= $.sb_players_e 1] ] [ .sb_players_e (! $.sb_players_e) ]
							"Full"     [ UIcheckbox [= $.sb_players_f 1] ] [ .sb_players_f (! $.sb_players_f) ]
							"Disabled" [ UIradio [&& [! $.sb_players_e] [! $.sb_players_f]] ] [ .sb_players_e 0 ; .sb_players_f 0 ]
						] 1 "hide servers according to player amount"
						uialign* -2 -1
					]
				]
			]
		]
		UItooltipbox 0.96 (? $.sb_showfilters 0.482 0.64) [
			local numfiltered
			uihover [ autosortservers 0 ] [
				autosortservers $.autosortservers
				.serverhover = -1
			]
			UIsettip "join server"
			uivlist 0 [
				uialign -2 -1
				uiclamp.x
				loop i $numservers [
					if (servinfovalid $i) [
						if (&& [ // latency
							<= (servinfoping $i) $.sb_ping
						] [      // mastermode
							if $.sb_mm [! (& (<< 1 (+ (servinfomastermode $i) 1)) $.sb_mm)] 1
						] [      // gamemode
							if $.sb_mode [! (& (<< 1 (servinfomode $i)) $.sb_mode)] 1
						] [      // empty server
							if $.sb_players_e [servinfonumplayers $i] 1
						] [      // full server
							if $.sb_players_f [< (servinfonumplayers $i) (servinfomaxplayers $i)] 1
						]) [
							uihover+ [ .serverhover = $i ]
							UIserver $i
						]
					] [ numfiltered = (+ $numfiltered 1) ]
				]
				uiclamp*x
			]
		] 0 [
			if (> $.serverhover -1) [
				uicolortext (servinfoname $.serverhover) $c_gray 0.5
				uifont "wide" [ uicolortext ":" $c_gray 0.5 ]
				uitextfill 2.5 0.5 [
					uicolortext (servinfoport $.serverhover) $c_gray 0.5
					uialign- -1
				]
			]
			uiline $c_line1 $.UI_pad_S
			uiline $c_line1 $.UI_pad_S
			uiclamp*y
			uitextfill 8.25 0.5 [
				uicolortext (concat (- $numservers $numfiltered) "^f7servers online") $c_cyan_t 0.5
				uialign- 1
			]
		]
	]
] [ initservers ] [] [
	uiallowinput (! (uivisible "dialog_serverconnect"))
] [ uitext "Server Browser" 0.8 ]





UIserver = [
	arg2 = (&& [=s $connectedip (servinfoname $arg1)] [= $connectedport (servinfoport $arg1)])
	uitarget 0 (*f $.UI_pad_5XL 2) [
		style_button_hold1 d (? $arg2 d n) (? $uihover? d n)
		uiclamp*e
		uirelease [
			if (!= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)) [
				if (> (servinfomastermode $arg1) 2) [
					.sb_connectid $arg1
					showui "dialog_serverconnect"
				] [ connectservinfo $arg1 ]
			]
		]
		uihlist $.UI_pad_L [
			uialign -1
			if $arg2 [
				uispace 0.001 0 [ UItriangle $c_line2 $.UI_pad_4XL $.UI_pad_S 270 ]
			] [ uifill $.UI_pad_M ]
			uigroup [
				uifill $.UI_pad_DS
				uicroppedimage "media/interface/ui/mm.png" $.UI_pad_DSS $.UI_pad_DSS [@(*f (+ (servinfomastermode $arg1) 1) 48)p] 0 48p 1
				uispace 0 $.UI_pad_L [ uifont "wide_outline" [ uitext (servinfomodename $arg1) 0.5 ] ]
				uialign- 0 1
			]
			uivlist $.UI_pad_S [
				uispace $.UI_pad_MM 0 [
					uihlist $.UI_pad_L [
						uifont "wide" [ uicolortext "::" $c_gray 0.6 ]
						uicolortext (servinfodesc $arg1) (? $arg2 $c_olive $c_white) 0.6
					]
				]
				uihlist $.UI_pad_L [
					if (m_timed (servinfomode $arg1)) [
						uifont "webdings" [ uicolortext "i" $c_gray 0.65 ]
						uifont "mono" [ uicolortext (servinfotime $arg1) $c_cyan_t 0.55 ]
					]
					if (servinfomap $arg1) [
						uifont "wide" [ uicolortext ">" $c_gray 0.6 ]
						uicolortext (servinfomap $arg1) $c_gray_l 0.55
					]
					uialign* -2 -1
				]
				uialign* -1
			]
		]
		uihlist $.UI_pad_L [
			uialign 1
			uifill $.UI_pad_SXL 0 [
				uivlist $.UI_pad_MS [
					loopwhile i 5 [<= (servinfoping $arg1) (at "525 270 165 110 55" $i)] [ arg3 = (+ $i 1) ]
					uicroppedimage "media/interface/ui/conn.png" 0.025 0.025 [@(*f $arg3 64)p] 0 64p 1
					if (> (servinfoping $arg1) 999) [ uicolortext "999+" $c_gray 0.55 ] [
						uitext (servinfoping $arg1) 0.55
					]
				]
			]
			uifill $.UI_pad_XL $.UI_pad_6XL [
				uihlist (-f 0 $.UI_pad_ML) [
					uiclamp.y
					uifill (*f $.UI_pad_L 3.4) 0 [
						uifont "mono" [ uitext (servinfonumplayers $arg1) 0.6 ]
						uialign- 1 -1
					]
					uioffset 0 (-f 0 $.UI_pad_L) [
						uiclip 0 $.UI_pad_SXL [ uicolortext "/" $c_gray 1.3 ]
					] // text takes up a lot of vertical space, have to "cut" it
					uifill $.UI_pad_SXL 0 [
						uifont "mono" [ uicolortext (servinfomaxplayers $arg1) $c_gray_l 0.55 ]
						uialign- -1 1
					]
					uiclamp*y
				]
			]
		]
	]
]

////////////////////////////////////////////////////////////////////////////////////////////////////


// search desc
//    if $.sb_searchdesc [>= (strstr (servinfodesc $i) $.sb_searchdesc) 0] 1
//] [      // search map
//    if $.sb_searchmap [>= (strstr (servinfomap $i) $.sb_searchmap) 0] 1
//] [

//cond (|| [= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)] [> (servinfomastermode $arg1) 2]) [
//    uihgradient 0xFFFFFF 0x80D04040
//] (>= (*f 100 (divf (servinfonumplayers $arg1) (servinfomaxplayers $arg1))) 60) [
//    uihgradient 0xFFFFFF 0x80D0B040
//]

//UIfastimg "" "key" (servinfohaspassword $arg1) $.UI_pad_2XL
//uialign- -1  1
//UIfastimg "" "fav" (servinfokeep $arg1) $.UI_pad_2XL
//uialign-  1  1