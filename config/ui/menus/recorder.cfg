
UImenu "recorder" [
	uihlist $.UI_pad_L [
		uivlist 0 [
			UIhsliderslim moviefps 10 (min 200 $maxfps) 2 $.UI_pad_D6XL $.UI_pad_5XL [] [ concat $$arg1 "fps" ]
			UIhsliderslim .mr_resmul 10 100 5 $.UI_pad_D6XL $.UI_pad_5XL [
				moview = (*f (? $fullscreen $desktopw $screenw) (divf $.mr_resmul 100))
				movieh = (*f (? $fullscreen $desktoph $screenh) (divf $.mr_resmul 100))
			] [ concatword $$arg1 "%" ] // "
			uispace 0 $.UI_pad_M [ uitext (format "^f2%1 ^f4x ^f2%2" $moview $movieh) 0.6 ]
			uihlist 0 [
				uitext "Save location:" 0.6
				UIbasicfield  moviedir     16 [] 0.7 [] [
					uifill 0 0 [
						uicolortext "no subfolder set" $c_gray 0.5
					]
				]
			]
			uihlist 0 [
				uitext "Video name:" 0.6
				UIbasicfield .mr_title 16 [] 0.7 [] [
					uifill 0 0 [
						uicolortext (concatword "movie_" (
							if (! $mainmenu) [
								concatword (getmodename $getmode) "_" $mapname
							] [ result "menu" ]
						) "_" $getmillis) $c_gray 0.5
					]
				]
			]
			UIbar 0 0 0 $.UI_pad_M
			UIbutton "hold2" [
				uitext (? $movierecording "Stop Recording" "Start Recording") 0.6
			] 0 $.UI_pad_5XL [ togglerecording ] 0
			uiclamp*x
		]
		UIbar 0 1
		uivlist 0 [
			uifill $.UI_pad_D3XL 0
			UIbutton "hold2" [
				UIcheckbox $moviesound
				uitext "Record Sound" 0.6
			] 0 $.UI_pad_5XL [ moviesound (! $moviesound) ] -1
			UIbutton "hold2" [
				UIcheckbox (! $moviesync)
				uitext "Frameskipping" 0.6
			] 0 $.UI_pad_5XL [ moviesync (! $moviesync) ] -1
			UIbar 1 0 $.UI_pad_L $.UI_pad_L
			UIbutton "hold2" [
				UIcheckbox $movieaccel
				uitext "HW Accel" 0.6
			] 0 $.UI_pad_5XL [ movieaccel (! $movieaccel) ] -1
			UIbutton "hold2" [
				UIcheckbox $movieaccelyuv
				uitext "YUV Encoding" 0.6
			] 0 $.UI_pad_5XL [ movieaccelyuv (! $movieaccelyuv) ] -1
			UIbutton "hold2" [
				UIcheckbox $movieaccelblit
				uitext "Blitting" 0.6
			] 0 $.UI_pad_5XL [ movieaccelblit (! $movieaccelblit) ] -1
			UIbutton "hold2" [
				UIcheckbox $dbgmovie
				uitext "Debugging" 0.6
			] 0 $.UI_pad_5XL [ dbgmovie (! $dbgmovie) ] -1
			uiclamp*x
		]
	]
] [
	local windowratio
	windowratio = (gcd $screenw $screenh)

	.UI_aspectX = (div $screenw $windowratio)
	.UI_aspectY = (div $screenh $windowratio)

	moviefps = (min $moviefps 200 $maxfps)

	if (&& [! $movierecording] [!=f $windowratio (gcd $moview $movieh)]) [
		moview $screenw
		movieh $screenh
		.mr_resmul 100
	]
] [] [] [ uitext "Movie Recorder" 0.8 ]

togglerecording = [	
	if $movierecording [ movie ] [
		if (=s $.mr_title "") [
			movie (concatword "movie_" (
				if (! $mainmenu) [
					concatword (getmodename $getmode) "_" $mapname
				] [ result "menu" ]
			) "_" $getmillis)
		] [ movie $.mr_title ]
	]
]
