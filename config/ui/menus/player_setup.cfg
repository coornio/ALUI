
UImenu "player_setup" [
    uihlist 0.01 [
        uivlist 0 [
            uiplayerpreview $ps_model (case $ps_team 0 $playercolor 1 $playercolorazul 2 $playercolorrojo) $ps_team $ps_weapon 0.4 0.5 [
                style_generic_default
                uiclamp* 1 1 1 1
            ]
            uihlist 0 [
                uiclamp 1 1
                UIbutton "" [
                    UIradio [= $ps_weapon 0] 0.016
                    uitext "Railgun" 0.6
                ] 0 0.032 [ ps_weapon = 0 ] -1
                UIbutton "" [
                    UIradio [= $ps_weapon 1] 0.016
                    uitext "Pulse Rifle" 0.6
                ] 0 0.032 [ ps_weapon = 1 ] -1
                uiclamp* 1 1
            ]
        ]
        uivlist 0.01 [
            uialign -2 -1
            uivlist 0.0037 [
                uitext "Name" 0.7
                UIbasicfield name 15 [] 0.7
            ]
            looplist3 v t l [
                "playercolor"     0 9
                "playercolorazul" 1 4
                "playercolorrojo" 2 4
            ] [
                uivlist 0.0037 [
                    case $t 1 [
                        uitext "^f8Azul" 0.7
                    ] 2 [
                        uitext "^f3Rojo" 0.7
                    ] () [ uitext "Solo" 0.7 ]
                    uigrid (? (< $t 1) 3 2) 0.005 0.005 [
                        local color
                        uihover [ ps_team = $t ]
                        loop i $l [
                            color = (getplayercolor $i $t)
                            uicolor $color 0.0458 0.0458 [
                                uioutline (? (= $$v $i) $c_fill $color)
                                uiclamp- 1 1 1 1
                                uipress [ $v $i ]
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
] [] [] [] [ uitext "Player Setup" 0.8 ]

////////////////////////////////////////////////////////////////////////////////////////////////////

ps_model  = 0
ps_team   = 0
ps_weapon = 1
