
UI_ratio4:3 = [
    "640 480"
    "800 600"
    "1024 768"
    "1152 864"
    "1280 960"
    "1400 1050"
    "1600 1200"
    "1920 1440"
    "2560 1920"
]
UI_ratio5:4 = [
    "800 640"
    "960 768"
    "1280 1024"
    "1440 1152"
    "1600 1280"
    "1920 1536"
    "2560 2048"
]
UI_ratio16:9 = [
    "800 450"
    "1024 600"
    "1280 720"
    "1366 768"
    "1600 900"
    "1920 1080"
    "2048 1152"
    "2560 1440"
]
UI_ratio16:10 = [
    "768 480"
    "1024 640"
    "1280 800"
    "1440 900"
    "1680 1050"
    "1920 1200"
    "2560 1600"
]
opt_ratios = [4:3 5:4 16:9 16:10]
opt_ratios_assoc = (concat "custom" "custom" (looplistconcat ratio $opt_ratios [concat $ratio $ratio]))
UI_findratio = [
    looplist ratio $opt_ratios [
        looplist res $[UI_ratio@ratio] [
            if (=s $res $UI_resolution) [ UI_aspectratio = $ratio ]
        ]
    ]
]

UImenu "options" [
    uivlist 0 [
        uihlist 0 [
            uiclamp 1 1
            uifill
            looplist3 v n i [
                0 "Game"    "config"
                1 "Sound"   "sound"
                2 "Display" "display"
                3 "Misc"    "misc"
            ] [
                UIbuttonarea (? (= $UI_opttabs $v) "hold1" "hold2") 0.15 0.05 [
                    uihlist 0.01 [
                        UIfastimg "ui/" $i (= $UI_opttabs $v) 0.04 0.04
                        uitext $n 0.7
                    ]
                    uihover [ UI_opttabs = $v ]
                ]
            ]
            uifill
            uiclamp* 1 1
            //uifill
        ]
        UItooltipbox 0.8 0.512 [
            uispace 0.01 0.01 [
                uiclamp 1 1 1 1
                uivlist 0 [
                    uiclamp 1 1
                    uialign -1 -1
                    case $UI_opttabs 0 [
                        uihlist 0 [
                            uitext "FOV" 0.6
                            UIhslider fov 10 150 5 0.3 $UI_optrowh [ uitext $fov 0.6 ]
                            UIbasicfield fov 3 [] 0.65
                        ]
                        uihlist 0 [
                            uitext "Sensitivity" 0.6
                            UIhslider sensitivity 0.1 40.0 0.1 0.3 $UI_optrowh [ uitext $sensitivity 0.6 ]
                            UIbasicfield sensitivity 3 [] 0.65
                        ]
                        uihlist 0 [
                            uitext "Zoom FOV" 0.6
                            UIhslider zoomfov 10 90 1 0.3 $UI_optrowh [ uitext $zoomfov 0.6 ]
                            UIbasicfield zoomfov 3 [] 0.65
                        ]
                        uihlist 0 [
                            uitext "Zoom Sensitivity" 0.6
                            UIhslider zoomsens 0.1 20.0 0.1 0.3 $UI_optrowh [ uitext $zoomsens 0.6 ]
                            UIbasicfield zoomsens 3 [] 0.65
                        ]
                        UIbutton "hold2" [
                            UIcheckbox $zoomtoggle 0.016
                            uitext "Zoom Toggle" 0.6
                        ] $UI_optc1wB $UI_optrowh [ zoomtoggle (! $zoomtoggle) ] -1
                        UIbutton "hold2" [
                            UIcheckbox $invmouse 0.016
                            uitext "Invert Mouse" 0.6
                        ] $UI_optc1wB $UI_optrowh [ invmouse (! $invmouse) ] -1
                        uihlist 0 [
                            uitext "UI Sensitivity" 0.6
                            UIhslider uisensitivity 0.1 4.0 0.1 0.3 0.032 [ uitext $uisensitivity 0.6 ]
                            UIbasicfield uisensitivity 3 [] 0.65
                        ]
                        UIbutton "hold2" [
                            UIcheckbox $showfps 0.016
                            uitext "Show FPS" 0.6
                        ] $UI_optc1wB $UI_optrowh [ showfps (! $showfps) ] -1
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $wallclock 0.016
                                uitext "Show Clock" 0.6
                            ] $UI_optc1wB $UI_optrowh [ wallclock (! $wallclock) ] -1
                            UIbutton "hold2" [
                                UIcheckbox $wallclock24 0.016
                                uitext (concatword (? (! $wallclock) "^f4") "24 Hour") 0.6
                            ] 0.178 $UI_optrowh [ wallclock24 (! $wallclock24) ] -1
                            UIbutton "hold2" [
                                UIcheckbox $wallclocksecs 0.016
                                uitext (concatword (? (! $wallclock) "^f4") "Seconds") 0.6
                            ] 0.178 $UI_optrowh [ wallclocksecs (! $wallclocksecs) ] -1
                        ]
                        UIbutton "hold2" [
                            UIcheckbox $hudgun 0.016
                            uitext "Show Weapon" 0.6
                        ] $UI_optc1wB $UI_optrowh [ hudgun (! $hudgun) ] -1
                        UIbutton "hold2" [
                            UIcheckbox $damagescreen 0.016
                            uitext "Bloody Screen" 0.6
                        ] $UI_optc1wB $UI_optrowh [ damagescreen (! $damagescreen) ] -1
                        UIbutton "hold2" [
                            UIcheckbox $blood 0.016
                            uitext "Blood" 0.6
                        ] $UI_optc1wB $UI_optrowh [ blood (! $blood) ] -1
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox (! $hidedead) 0.016
                                uitext "Show Dead Players" 0.6
                            ] $UI_optc1wB $UI_optrowh [ hidedead (! $hidedead) ] -1
                            UIhslider ragdollmillis 0 300000 5000 0.3 $UI_optrowh [ uitext (concatword (? $hidedead "^f4") (div $ragdollmillis 1000)) 0.6 ]
                        ]
                        uihlist 0 [
                            uitext (concatword (? $hidedead "^f4") "Ragdoll Velocity") 0.6
                            UIhslider deadpush 1 20 1 0.3 $UI_optrowh [ uitext (concatword (? $hidedead "^f4") $deadpush) 0.6 ]
                        ]
                        uihlist 0 [
                            uitext "Fullbright Models" 0.6
                            UIlistslider fullbrightmodels = [0 "^f4off" 60 "^f1subtle" 100 "^f0bright" 150 "^f2overbright" 200 "^f3max"] 0.3 $UI_optrowh
                            UIbasicfield fullbrightmodels 3 [] 0.65
                        ]
                        uihlist 0 [
                            uitext "Radar" 0.6
                            UIbutton "hold2" [
                                UIcheckbox $radarteammates 0.016
                                uitext "Teammates" 0.6
                            ] 0.178 $UI_optrowh [ radarteammates (! $radarteammates) ] -1
                            UIbutton "hold2" [
                                UIcheckbox $showminimap 0.016
                                uitext "Mini-Map" 0.6
                            ] 0.178 $UI_optrowh [ showminimap (! $showminimap) ] -1
                        ]
                        uihlist 0 [
                            uitext "Scoreboard" 0.6
                            uigrid 2 0 0 [
                                UIbutton "hold2" [
                                    UIcheckbox $showkills 0.016
                                    uitext "Kills" 0.6
                                ] 0.178 $UI_optrowh [ showkills (! $showkills) ] -1
                                UIbutton "hold2" [
                                    UIcheckbox $showdeaths 0.016
                                    uitext "Deaths" 0.6
                                ] 0.178 $UI_optrowh [ showdeaths (! $showdeaths) ] -1
                                UIbutton "hold2" [
                                    UIcheckbox $showscore 0.016
                                    uitext "Score" 0.6
                                ] 0.178 $UI_optrowh [ showscore (! $showscore) ] -1
                                uifill 0.178
                                UIbutton "hold2" [
                                    UIcheckbox $showping 0.016
                                    uitext "Ping" 0.6
                                ] 0.178 $UI_optrowh [ showping (! $showping) ] -1
                                UIbutton "hold2" [
                                    UIcheckbox $showclientnum 0.016
                                    uitext "Client#" 0.6
                                ] 0.178 $UI_optrowh [ showclientnum (! $showclientnum) ] -1
                                UIbutton "hold2" [
                                    UIcheckbox $showip 0.016
                                    uitext "IP:Port" 0.6
                                ] 0.178 $UI_optrowh [ showip (! $showip) ] -1
                                UIbutton "hold2" [
                                    UIcheckbox $showspectators 0.016
                                    uitext "Spectators" 0.6
                                ] 0.178 $UI_optrowh [ showspectators (! $showspectators) ] -1
                                UIbutton "hold2" [
                                    UIcheckbox $sbopaque 0.016
                                    uitext "Opaque" 0.6
                                ] 0.178 $UI_optrowh [ sbopaque (! $sbopaque) ] -1
                                uifill 0.178
                            ]
                        ]
                    ] 1 [
                        UIbutton "hold2" [
                            UIcheckbox $sound 0.016
                            uitext "Enable Audio" 0.6
                        ] $UI_optc1wB $UI_optrowh [ sound (! $sound) ] -1
                        uihlist 0 [
                            uitext (concatword (? (! $sound) "^f4") "Sound Volume") 0.6
                            UIhsliderslim soundvol 0 255 0.3 $UI_optrowh [ .soundvol = $soundvol ]
                        ]
                        uihlist 0 [
                            uitext (concatword (? (! $sound) "^f4") "Music Volume") 0.6
                            UIhsliderslim musicvol 0 255 0.3 $UI_optrowh [ .musicvol = $musicvol ]// [concatword (+ (round (*f (divf $$arg1 $arg3) 100)) 0) "%"]
                        ]
                        UIbutton "hold2" [
                            UIcheckbox $hitsound 0.016
                            uitext (concatword (? (! $sound) "^f4") "Hit Confirmation") 0.6
                        ] $UI_optc1wB $UI_optrowh [ hitsound (! $hitsound) ] -1
                        UIbutton "hold2" [
                            UIcheckbox $mumble 0.016
                            uitext "Mumble Positional Audio" 0.6
                        ] $UI_optc1wB $UI_optrowh [ mumble (! $mumble) ] -1
                    ] 2 [
                        uihlist 0 [
                            uitext "Aspect Ratio" 0.6
                            UIlistslider UI_aspectratio =s $opt_ratios_assoc 0.3 $UI_optrowh [
                                if (=s $UI_aspectratio "custom") [] [
                                    UI_resolution = (at $[UI_ratio@UI_aspectratio] 1)
                                    screenw (at $UI_resolution 0)
                                    screenh (at $UI_resolution 1)
                                ]
                            ]
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox (!=s $UI_aspectratio "custom") 0.016
                                uitext "Preset Resolution" 0.6
                            ] $UI_optc1wB $UI_optrowh [
                                if (=s $UI_aspectratio "custom") [
                                    UI_findratio
                                    if (=s $UI_aspectratio "custom") [
                                        UI_aspectratio = "16:10"
                                        UI_resolution = (at $[UI_ratio@UI_aspectratio] 1)
                                        screenw (at $UI_resolution 0)
                                        screenh (at $UI_resolution 1)
                                    ]
                                ] [
                                    UI_aspectratio = "custom"
                                ]
                            ] -1
                            if (=s $UI_aspectratio "custom") [
                                uihlist 0.009 [
                                UIbasicfield screenw 5 [] 0.65
                                    uitext "x" 0.6
                                    UIbasicfield screenh 5 [] 0.65
                                    UIbutton "" [ uitext "Native" 0.6 ] 0.1 $UI_optrowh [
                                        UI_resolution = (concat $desktopw $desktoph)
                                        UI_findratio
                                        screenw (at $UI_resolution 0)
                                        screenh (at $UI_resolution 1)
                                    ]
                                ]
                            ] [
                                UIlistslider UI_resolution = (
                                    looplistconcat i $[UI_ratio@UI_aspectratio] [
                                        concat (escape $i) (strreplace $i " " "x")
                                    ]
                                ) 0.3 $UI_optrowh [
                                     screenw (at $UI_resolution 0)
                                     screenh (at $UI_resolution 1)
                                ]
                            ]
                        ]
                        uihlist 0 [
                            uitext "Anisotropic Filtering" 0.6
                            UIlistslider aniso = [0 "^f4off" 2 "^f12x" 4 "^f14x" 8 "^f18x" 16 "^f116x"] 0.3 $UI_optrowh
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $smaa 0.016
                                uitext "Morphological AA" 0.6
                            ] $UI_optc1wB $UI_optrowh [ smaa (! $smaa) ] -1
                            UIlistslider smaaquality = [
                                0 @(concatword (? $smaa "^f1" "^f4") "low")
                                1 @(concatword (? $smaa "^f0" "^f4") "medium")
                                2 @(concatword (? $smaa "^f2" "^f4") "high")
                                3 @(concatword (? $smaa "^f3" "^f4") "ultra")
                            ] 0.3 $UI_optrowh [ smaacoloredge (>= $smaaquality 3) ]
                        ]
                        UIbutton "hold2" [
                            UIcheckbox $tqaa 0.016
                            uitext "Temporal AA" 0.6
                        ] $UI_optc1wB $UI_optrowh [ tqaa (! $tqaa) ] -1
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $fxaa 0.016
                                uitext "FXAA" 0.6
                            ] $UI_optc1wB $UI_optrowh [ fxaa (! $fxaa) ] -1
                            UIlistslider fxaaquality = [
                                0 @(concatword (? $fxaa "^f1" "^f4") "low")
                                1 @(concatword (? $fxaa "^f0" "^f4") "medium")
                                2 @(concatword (? $fxaa "^f2" "^f4") "high")
                                3 @(concatword (? $fxaa "^f3" "^f4") "ultra")
                            ] 0.3 $UI_optrowh
                        ]
                        uihlist 0 [
                            uitext "Multisample AA" 0.6
                            UIlistslider msaa = [0 "^f4off" 2 "^f12x" 4 "^f04x" 8 "^f28x" 16 "^f316x"] 0.3 $UI_optrowh
                        ]
                        uihlist 0 [
                            uitext "Gamma" 0.6
                            UIhslider gamma 30 300 5 0.3 $UI_optrowh [ uitext $gamma 0.6 ]
                            UIbasicfield gamma 3 [] 0.65
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $vsync 0.016
                                uitext "V-sync" 0.6
                            ] $UI_optc1wB $UI_optrowh [ vsync (! $vsync) ] -1
                            UIbutton "hold2" [
                                UIcheckbox $vsynctear 0.016
                                uitext (concatword (? (! $vsync) "^f4") "Tear")  0.6
                            ] 0.15 $UI_optrowh [ vsynctear (! $vsynctear) ] -1
                        ]
                    ] 3 [
                        uihlist 0 [
                            uitext "Shadow Resolution" 0.6
                            UIlistslider smsize = [10 "^f4lowest" 11 "^f1low" 12 "^f0medium" 13 "^f2high" 14 "^f3ultra"] 0.3 $UI_optrowh
                        ]
                        uihlist 0 [
                            uitext "Shadow Filtering" 0.6
                            UIlistslider smfilter = [0 "^f1low" 1 "^f0medium" 2 "^f2high" 3 "^f3ultra"] 0.3 $UI_optrowh
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $gi 0.016
                                uitext "Global Illumination" 0.6
                            ] $UI_optc1wB $UI_optrowh [ gi (! $gi) ] -1
                            UIlistslider rhtaps = [
                                12 @(concatword (? $gi "^f1" "^f4") "low")
                                20 @(concatword (? $gi "^f0" "^f4") "medium")
                                32 @(concatword (? $gi "^f2" "^f4") "high")
                            ] 0.3 $UI_optrowh
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $ao 0.016
                                uitext "SSAO" 0.6
                            ] $UI_optc1wB $UI_optrowh [ ao (! $ao) ] -1
                            UIlistslider aotaps = [
                                5  @(concatword (? $ao "^f1" "^f4") "low")
                                8  @(concatword (? $ao "^f0" "^f4") "medium")
                                12 @(concatword (? $ao "^f2" "^f4") "high")
                            ] 0.3 $UI_optrowh [ aobilateral (? (>= $aotaps 8) 4 3) ]
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $volumetric 0.016
                                uitext "Volumetric Lighting" 0.6
                            ] $UI_optc1wB $UI_optrowh [ volumetric (! $volumetric) ] -1
                            UIlistslider volsteps = [
                                12 @(concatword (? $volumetric "^f1" "^f4") "low")
                                16 @(concatword (? $volumetric "^f0" "^f4") "medium")
                                24 @(concatword (? $volumetric "^f2" "^f4") "high")
                            ] 0.3 $UI_optrowh [ volbilateral (? (>= $volsteps 16) 2 1) ]
                        ]
                        uihlist 0 [
                            uitext "Water" 0.6
                            uivlist 0 [
                                uihlist 0 [
                                    UIbutton "hold2" [
                                        UIcheckbox $waterreflect 0.016
                                        uitext "reflection" 0.6
                                    ] 0.178 $UI_optrowh [ waterreflect (! $waterreflect) ] -1
                                    UIbutton "hold2" [
                                        UIcheckbox $caustics 0.016
                                        uitext "caustics" 0.6
                                    ] 0.178 $UI_optrowh [ caustics (! $caustics) ] -1
                                ]
                                UIbutton "hold2" [
                                    UIcheckbox $vertwater 0.016
                                    uitext "animation" 0.6
                                ] 0.178 $UI_optrowh [ vertwater (! $vertwater) ] -1
                            ]
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $softparticles 0.016
                                uitext "Soft Particles" 0.6
                            ] $UI_optc1wB $UI_optrowh [ softparticles (! $softparticles) ] -1
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $stains 0.016
                                uitext "Stains" 0.6
                            ] $UI_optc1wB $UI_optrowh [ stains (! $stains) ] -1
                            uihlist 0 [
                                UIlistslider stainfade = [
                                    2  @(concatword (? $stains "^f1" "^f4") "instant-fade")
                                    15 @(concatword (? $stains "^f0" "^f4") "quick-fade")
                                    30 @(concatword (? $stains "^f2" "^f4") "moderate-fade")
                                    60 @(concatword (? $stains "^f3" "^f4") "slow-fade")
                                ] 0.3 $UI_optrowh [ maxstaintris (? (> $stainfade 15) 8192 2048) ]
                                UIbasicfield stainfade 4 [ maxstaintris (? (> $stainfade 15) 8192 2048) ] 0.65
                            ]
                        ]
                        uihlist 0 [
                            UIbutton "hold2" [
                                UIcheckbox $grass 0.016
                                uitext "3D Grass" 0.6
                            ] $UI_optc1wB $UI_optrowh [ grass (! $grass) ] -1
                            uihlist 0 [
                                UIlistslider grassdist = [
                                    128  @(concatword (? $grass "^f1" "^f4") "quick-fade")
                                    256  @(concatword (? $grass "^f0" "^f4") "moderate-fade")
                                    512  @(concatword (? $grass "^f2" "^f4") "slow-fade")
                                    1024 @(concatword (? $grass "^f3" "^f4") "slower-fade")
                                ] 0.3 $UI_optrowh
                                UIbasicfield grassdist 4 [] 0.65
                            ]
                        ]
                    ]
                ]
            ]
        ] 0 [
            if $pendingchanges [
                diagline
                uitextfill 8.5 0.5 [
                    uitext (format "^f8%1 ^f7changes pending" $pendingchanges) 0.5
                    uialign- 1
                ]
            ]
        ]
    ]
] [
    UI_opttabs = 0
    UI_aspectratio = "custom"
    UI_resolution  = (concat $screenw $screenh)
    UI_findratio
] [ if $pendingchanges [ showui "dialog_changes" ] ] [] [ uitext "Options" 0.8 ]



