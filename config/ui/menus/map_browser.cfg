
UImenu "map_browser" [
	uivlist 0.01 [
		uihlist 0.01 [
			uiclamp 1 1
			UIbutton "hold1" [
				UIcheckbox [= $.showmaplist 1] 0.016
				uitext "Community Maps" 0.6
			] 0.24 0.04 [ .showmaplist 1 ] -1 [
				UIsettip "browse only maps made by the community"
			]
			UIbutton "hold1" [
				UIcheckbox [= $.showmaplist 0] 0.016
				uitext "Integrated Maps" 0.6
			] 0.24 0.04 [ .showmaplist 0 ] -1 [
				UIsettip "browse only maps in the default rotation"
			]
		]
		uigroup [
			uifill 0.64 0.64 [
				uialign -1 -1
				UIfastimg "cube"
				uiimage (concatword "media/map/" $.mb_selectedmap ".png")
				style_generic_box "n" "d" "d" "d"
				uiclamp* 1 1 1 1
			]
			UItooltipbox 0.64 0.64 [
				uigrid 2 0 0 [
					uiclamp 1 1
					uialign 0 -1
					looplist m (? $.showmaplist $usermaps $defdmmaps) [
						UIbuttonarea [hold1 (? (=s $.mb_selectedmap $m) $c_diag1 n) d n] 0.32 0.08 [
							uirelease [ showui "dialog_loadmap" ]
							uihover [
								if (uivisible "dialog_loadmap") [] [
									.mb_selectedmap $m
								]
							]
							uifont "default_outline" [ uitext $m 0.6 ]
						]
					]
				]
			]
		]
	]
] [
	.mb_selectedmap ""
	genusermaps
] [] [] [ uitext "Map Browser" 0.8 ]



UIquickdialog "map_browser" [
	//uialign 1
] [
	UIhudcell 0 0 [
		uivlist $.UI_pad_L [
			uihlist 0 [
				uiclamp 1 1
				UIbutton "hold2" [
					uitext "Community Maps" 0.6
					uifill $.UI_pad_L 0 [ if (= $.showmaplist 1) [
						uioffset 0 (coswave.bounce $.UI_pad_XL 300) [ UItriangle 0 $.UI_pad_L $.UI_pad_S 180 ]
					] ]
				] 0 $.UI_pad_5XL [ .showmaplist 1 ] -1
				UIbutton "hold2" [
					uifill $.UI_pad_L 0 [ if (= $.showmaplist 0) [
						uioffset 0 (coswave.bounce $.UI_pad_XL 300) [ UItriangle 0 $.UI_pad_L $.UI_pad_S 180 ]
					] ]
					uitext "Integrated Maps" 0.6
				] 0 $.UI_pad_5XL [ .showmaplist 0 ] 1
				uiclamp* 1 1
			]
			uihlist $.UI_pad_L [
				uiscroll $.x $.y [
					uifill $.x $.y
					uihover [] [ .mb_selectedmap "" ]
					uigrid 3 0 0 [
						uialign -1 -1
						looplist m (? $.showmaplist $usermaps $defdmmaps) [
							UIbuttonarea "none" $.UI_pad_D2XL $.UI_pad_D2XL [
								uispace 0.0006 0.0006 [
									uiclamp 1 1 1 1
									if (!=s $m $.mb_selectedmap) [
										uimodcolor $c_boxmod
										UIfastimg "cube"
										uiimage (concatword "media/map/" $m ".png")
										uioutline $c_line
									] [
										uimodcolor $c_grdmod
										UIfastimg "cube"
										uiimage (concatword "media/map/" $m ".png")
										uioutline (coswave.INT $c_line $c_cyan_t (uihold? 250 500))
									]
									uiclamp* 1 1 1 1
									uispace 0.0003 $.UI_pad_S [
										uiclamp 1 1
										uialign 0 1
										uimodcolor $c_grdmod 0 0 [ uiclamp 1 1 1 1 ]
										uifont "default_outline" [ uicolortext $m 0x98FFFFFF 0.5 ]
									]
								]
								uihover [
									if (|| [=s $m $.mb_selectedmap] [uivisible "dialog_loadmap"]) [] [
										.mb_selectedmap $m
										UIcoswaveset
									]
								]
								uipress [ UIcoswaveset ]
								uirelease [ showui "dialog_loadmap" ]
							]
						]
					]
				]
				UIvscroll 0 $.y 6.4
			]
		]
	]
] [
	.x = (*f $.UI_pad_D2XL 3)
	.y = (-f 1 (*f $.UI_pad_L 6) $.UI_pad_5XL)
	.mb_selectedmap ""
	hideui "texture_browser"
	hideui "geo_prefab"
	hideui "editvars" 
] [
	if $editing [ showui "editvars"]
]
