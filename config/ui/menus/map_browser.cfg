
UIquickdialog "map_browser" [ uialign 1 ] [
	UIhudcell 0 0 [
		uivlist $.UI_pad_L [
			uihlist 0 [
				uiclamp 1 1
				UIbutton "hold2" [
					uitext "Community Maps" 0.6
					uifill $.UI_pad_L 0 [ if (= $.showmaplist 1) [
						uioffset 0 (coswave.bounce $.UI_pad_XL 300) [ UItriangle 0 $.UI_pad_L $.UI_pad_S 180 ]
					] ]
				] 0 $.UI_pad_5XL [ .showmaplist 1 ] -1
				UIbutton "hold2" [
					uifill $.UI_pad_L 0 [ if (= $.showmaplist 0) [
						uioffset 0 (coswave.bounce $.UI_pad_XL 300) [ UItriangle 0 $.UI_pad_L $.UI_pad_S 180 ]
					] ]
					uitext "Integrated Maps" 0.6
				] 0 $.UI_pad_5XL [ .showmaplist 0 ] 1
				uiclamp* 1 1
			]
			uihlist $.UI_pad_L [
				uiscroll $.x $.y [
					uifill $.x $.y
					if (|| uihover? [uivisible "dialog_loadmap"]) [] [ .mb_selectedmap "" ]
					uigrid 3 0 0 [
						uialign -1 -1
						looplist m (? $.showmaplist $usermaps $defdmmaps) [
							UIbuttonarea "none" $.UI_pad_D2XL $.UI_pad_D2XL [
								uispace 0.0006 0.0006 [
									uiclamp 1 1 1 1
									if (!=s $m $.mb_selectedmap) [
										uimodcolor $c_boxmod
										UIfastimg "cube"
										uiimage (concatword "media/map/" $m ".png")
										uioutline $c_line
									] [
										uimodcolor $c_grdmod
										UIfastimg "cube"
										uiimage (concatword "media/map/" $m ".png")
										uioutline (coswave.INT $c_cyan_t $c_line (uihold? 250 500))
									]
									uiclamp* 1 1 1 1
									uispace 0.0003 $.UI_pad_S [
										uiclamp 1 1
										uialign 0 1
										uimodcolor $c_grdmod 0 0 [ uiclamp 1 1 1 1 ]
										uifont "default_outline" [ uicolortext $m 0x98FFFFFF 0.5 ]
									]
								]
								if (uivisible "dialog_loadmap") [] [
									if (!=s $m $.mb_selectedmap) [
										uihover [
											.mb_selectedmap $m
											UIcoswaveset
										]
									]
									uipress [ UIcoswaveset ]
									uirelease [ showui "dialog_loadmap" ]
								]
							]
						]
					]
				]
				UIvscroll 0 $.y 6.4
			]
		]
	]
] [
	.x = (*f $.UI_pad_D2XL 3)
	.y = (-f 1 (*f $.UI_pad_L 6) $.UI_pad_5XL)
	.mb_selectedmap ""
	hideui "texture_browser"
	hideui "geo_prefab"
	hideui "editvars" 
] [
	if $editing [ showui "editvars" ]
]
