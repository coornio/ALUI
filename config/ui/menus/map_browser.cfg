
UIquickdialog "map_browser" [ uialign 1 ] [
	uiallowinput (! (uivisible "dialog_loadmap"))
	UIhudcell 0 0 [
		uivlist $.UI_pad_L [
			uihlist 0 [
				uiclamp.x
				UIbutton "hold2" [
					uitext "Community Maps" 0.6
					uifill $.UI_pad_L 0 [ if (= $.showmaplist 1) [
						uioffset 0 (cosbounce $.UI_pad_S 300) [ UItriangle 0 $.UI_pad_L $.UI_pad_S 180 ]
					] ]
				] 0 0 [ .showmaplist 1 ] -1
				UIbutton "hold2" [
					uifill $.UI_pad_L 0 [ if (= $.showmaplist 0) [
						uioffset 0 (cosbounce $.UI_pad_S 300) [ UItriangle 0 $.UI_pad_L $.UI_pad_S 180 ]
					] ]
					uitext "Integrated Maps" 0.6
				] 0 0 [ .showmaplist 0 ] 1
				uiclamp*x
			]
			uihlist $.UI_pad_L [
				uiscroll $.x $.y [
					uifill $.x $.y
					if (uivisible "dialog_loadmap") [] [ uihover [] [ .mb_selectedmap "" ] ]
					uigrid 3 0 0 [
						uialign -1 -1
						looplist m (? $.showmaplist $usermaps $defdmmaps) [
							UIbuttonarea "none" $.UI_pad_D2XL $.UI_pad_D2XL [
								uispace 0.0006 0.0006 [
									uiclamp.e
									if (!=s $m $.mb_selectedmap) [
										uimodcolor $c_boxmod
										UIfastimg "cube"
										uiimage (concatword "media/map/" $m ".png")
										uioutline $c_line
									] [
										uimodcolor $c_grdmod
										UIfastimg "cube"
										uiimage (concatword "media/map/" $m ".png")
										uioutline (coswave.INT $c_cyan_t $c_line (uihold? 250 500))
									]
									uiclamp*e
									uispace 0.0003 $.UI_pad_S [
										uiclamp.x
										uialign 0 1
										uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
										uifont "default_outline" [
											uiclamp.e
											uicolortext $m 0x98FFFFFF 0.5
										]
									]
								]
								if (!=s $m $.mb_selectedmap) [
									uihover [
										.mb_selectedmap $m
										UIsetmillis
									]
								]
								uipress [ UIsetmillis ]
								uirelease [ showui "dialog_loadmap" ]
							]
						]
					]
				]
				UIvscroll 0 $.y 6.4
			]
		]
	]
] [
	.x = $.UI_pad_DSXL
	.y = (-f 1 (*f $.UI_pad_L 5) $.UI_pad_5XL)
	.mb_selectedmap ""
	hideui "texture_browser"
	hideui "geo_prefab"
	hideui "editvars" 
] [
	if $editing [ showui "editvars" ]
]
