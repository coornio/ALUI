
UImenu "map_browser" [
    uihlist 0.01 [
        uivlist 0 [
            uialign -2 -1
            uifill 0.3 0.04 [ uitext $_MAPBROWSER_selmap 0.7 ]
            UIbox "" 0 0 [
                uiimage "media/interface/cube.png" 0.3 0.3 [
                    uiimage (concatword "media/map/" $_MAPBROWSER_selmap ".png") 0.3 0.3
                ]
            ]
            uifill 0.0 0.015
            uitext "List" 0.7
            UIbutton "" [
                UIradio [= $_MAPBROWSER_maplist 0] 0.016
                uitext "Tesseract" 0.6
            ] 0.3 0.03 [
                _MAPBROWSER_maplist = 0
                _MAPBROWSER_selmap = ""
            ] -1
            UIbutton "" [
                UIradio [= $_MAPBROWSER_maplist 1] 0.016
                uitext "Custom" 0.6
            ] 0.3 0.03 [
                _MAPBROWSER_maplist = 1
                _MAPBROWSER_selmap = ""
            ] -1
            uifill 0.0 0.015
            uitext "Gamemode" 0.7
            uigrid 3 0 0 [
                nextmode = (getnextmode)
                looplist2 n i [
                    "Edit"  0 "rDM"  1 "pDM"  2
                    ""     -1 "rTDM" 3 "pTDM" 4
                    ""     -1 "rCTF" 5 "pCTF" 6
                ] [
                    if (< $i 0) uifill [
                        UIbutton "hold2" [
                            UIradio [= $nextmode $i] 0.016
                            uitext $n 0.6
                        ] 0.1 0.03 [ mode $i ] -1
                    ]
                ]
            ]
            uifill 0.0 0.015
            UIbutton "" [
                uitext (concatword (if $isconnected [? (! $editing) "^f4"]) "New Map") 0.7
            ] 0.3 0.05 [
                hideui $uiname
                showui "map_new"
            ]
        ]
        uiscroll 0.4 0.64 [
            uifill 0.37 0.64
            uivlist 0 [
                uialign 0 -1
                uiclamp 1 1
                if $_MAPBROWSER_maplist [
                    if (=s $_MAPBROWSER_custommaps "") [
                        _MAPBROWSER_custommaps = ""
                        loopfiles m "media/map" "ogz" [
                            if (< (indexof $editmaps $m) 0) [
                                _MAPBROWSER_custommaps = (concat $_MAPBROWSER_custommaps $m)
                            ]
                        ]
                    ]
                    looplist m $_MAPBROWSER_custommaps [
                        UIbutton (? (!=s $_MAPBROWSER_selmap $m) "hold2") [
                            uitext $m 0.6
                        ] 0 0.04 [
                            hideui "loadmap"
                            map $m
                        ] -1 [ uihover [ _MAPBROWSER_selmap = $m ] ]
                    ]
                ] [
                    looplist m $(
                        cond [m_ctf $nextmode] [
                            result "ctfmaps"
                        ] [m_edit $nextmode] [
                            result "editmaps"
                        ] [m_teammode $nextmode] [
                            result "allmaps"
                        ] [ result "dmmaps" ]
                    ) [
                        UIbutton (? (!=s $_MAPBROWSER_selmap $m) "hold2") [
                            uitext $m 0.6
                        ] 0 0.04 [
                            hideui "loadmap"
                            map $m
                        ] -1 [ uihover [ _MAPBROWSER_selmap = $m ] ]
                    ]
                ]
                uiclamp* 1 1
            ]
        ]
        UIvscroll 0.02 0.64 1.6
    ]
] [
    _MAPBROWSER_selmap     = ""
    _MAPBROWSER_custommaps = ""
    hideui "map_new"
] [] [] [ uitext "Map Browser" 0.8 ]

UImenu "map_new" [
    uispace 0.008 0.015 [
        uivlist 0 [
            uifill 0.5 0.032 [
                uitext (concatword "^f8/newmap " (? (!= $_MAPBROWSER_mapscale 10) $_MAPBROWSER_mapscale)) 0.7
                uialign* -1 0
                uioutline $c_line ; uiclamp- 1 1 1 1
            ]
            uifill 0 0.025
            uihlist 0.01 [
                uitext "MapScale" 0.6
                UIhslider _MAPBROWSER_mapscale 10 16 1 0.5 0.032 [uitext $_MAPBROWSER_mapscale 0.6]
                uialign* -1 0
            ]
            uifill 0 0.025
            UIbutton "" [uitext (concatword (? (= $isconnected 1) (? (! $editing) "^f4")) "New") 0.6] 0.12 0.06 [hideui "map_new" ; newmap $_MAPBROWSER_mapscale]
        ]
    ]
]

////////////////////////////////////////////////////////////////////////////////////////////////////

_MAPBROWSER_mapname  = "untitled"
_MAPBROWSER_mapscale = 10
_MAPBROWSER_maplist  = 0
