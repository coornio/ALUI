
UIstddialog "dialog_changes" [
    // 0.015 offset on side buttons
    uivlist 0.01 [
        uiclamp 1 1
        uitext "Apply changes now?" 0.7
        UIbar 1
        uispace 0.01 0 [
            uifill 0.24
            uivlist 0.01 [
                loop i $pendingchanges [
                    uifill 0 0.032 [
                        uihlist 0.01 [
                            uialign -1
                            UIradio 1 0.012
                            uitext (pendingchanges $i) 0.55
                        ]
                    ]
                ]
                uiclamp* 1 1
            ]
        ]
    ]
] [ applychanges ] [] [ if (! $pendingchanges) [ hideui $uiname ] ]




UIstddialog "dialog_quit" [
    uivlist 0.01 [
       uiclamp 1 1
       uitext "Quit to desktop?" 0.7
       UIbar 1
       uispace 0.01 0 [
           UIbutton "hold2" [
               UIcheckbox (! $confirmquit) 0.016
               uitext "Don't ask again" 0.55
           ] 0.2 0.032 [ confirmquit (! $confirmquit) ] -1
       ]
    ]
] [ quit ]




UIstddialog "dialog_loadmap" [
    // 0.015 offset on side buttons
    uivlist 0.01 [
        uiclamp 1 1
        uitext "Change game mode?" 0.7
        UIbar 1
        uispace 0.01 0 [
            uigrid 3 0 0 [
                looplist2 n i [
                    "Edit"  0 "rDM"  1 "pDM"  2
                    ""     -1 "rTDM" 3 "pTDM" 4
                    ""     -1 "rCTF" 5 "pCTF" 6
                ] [
                    if (< $i 0) uifill [
                        UIbutton "hold2" [
                            UIradio [= $getnextmode $i] 0.016
                            uitext $n 0.6
                        ] 0.1 0.03 [ mode $i ] -1
                    ]
                ]
            ]
        ]
    ]
] [
    map $.mb_selectedmap
    hideui "map_browser"  
] [] [ mode $getmode ]




UIstddialog "dialog_savemap" [
    uivlist 0.01 [
        uiclamp 1 1
        uitext "Please enter your new map name:" 0.7
        UIbar 1
        uispace 0.01 0 [
            UIbasicfield .savemapname 32 [] 0.7
        ]
    ]
] [ savemap $.savemapname ] [] [ .savemapname = "" ]




UIstddialog "dialog_serverconnect" [
    uivlist 0.01 [
        uiclamp 1 1
        uitext "Please enter your server password:" 0.7
        UIbar 1
        uispace 0.01 0 [
            UIbasicfield .serverpassw 32 [] 0.7
        ]
    ]
] [ connect (servinfoname $.sb_connectid) (servinfoport $.sb_connectid) $.serverpassw ] [] [ .serverpassw = "" ]




UIstddialog "dialog_copycfg" [
    uivlist 0.01 [
        uiclamp 1 1
        uitext "Copy map config?" 0.7
        UIbar 1
        uispace 0.01 0 [
            uifill 0.21
            uivlist 0.006 [
                uitext (concatword "^f8" $.cmc_from) 0.6
                UItriangle 0 0.012 0.006 180
                uitext (concatword "^f8" $.cmc_to) 0.6
            ]
        ]
    ]
] [
    textfocus "#copymapcfg"
    textload (concatword "media/map/" $.cmc_from ".cfg")
    textsave (concatword "media/map/" $.cmc_to   ".cfg")
] [] [
    if (|| [=s $.cmc_from ""] [=s $.cmc_to ""]) [ hideui $uiname ]
] [ .cmc_from "" ; .cmc_to "" ]
