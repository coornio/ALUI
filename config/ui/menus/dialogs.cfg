
UIstddialog "dialog_changes" [
	uivlist $.UI_pad_L [
		uiclampx
		uitext "Apply changes now?" 0.7
		UIbar 1
		uispace $.UI_pad_L 0 [
			uifill $.UI_pad_D4XL
			uigrid 2 $.UI_pad_L $.UI_pad_L [
				uialign -1
				loop i $pendingchanges [
					UIradio 1 $.UI_pad_XL ; uialign- 0 -2 
					uifill 0 $.UI_pad_5XL [ uitext (pendingchanges $i) 0.6 ]
				]
				
			]
		]
	]
] [ applychanges ] [] [ if (! $pendingchanges) [ hideui $uiname ] ]




UIstddialog "dialog_quit" [
	uivlist $.UI_pad_L [
	   uiclampx
	   uitext "Quit to desktop?" 0.7
	   UIbar 1
	   uispace $.UI_pad_L 0 [
		   UIbutton "hold2" [
			   UIcheckbox (! $confirmquit)
			   uitext "Don't ask again" 0.6
		   ] $.UI_pad_D3XL 0 [ confirmquit (! $confirmquit) ] -1
	   ]
	]
] [ quit ]




UIstddialog "dialog_loadmap" [
	uivlist $.UI_pad_L [
		uiclampx
		uitext "Change game mode?" 0.7
		UIbar 1
		uispace $.UI_pad_L 0 [
			uigrid 3 0 0 [
				looplist2 n i [
					"Edit"  0 "rDM"  1 "pDM"  2
					""     -1 "rTDM" 3 "pTDM" 4
					""     -1 "rCTF" 5 "pCTF" 6
				] [
					if (< $i 0) uifill [
						UIbutton "hold2" [
							UIradio [= $getnextmode $i]
							uitext $n 0.6
						] $.UI_pad_DL 0 [ mode $i ] -1
					]
				]
			]
		]
	]
] [
	map $.mb_selectedmap
	hideui "map_browser"  
] [] [ mode $getmode ]




UIstddialog "dialog_savemap" [
	uivlist $.UI_pad_L [
		uiclampx
		uitext "Please enter your new map name:" 0.7
		UIbar 1
		uispace $.UI_pad_L 0 [
			UIbasicfield .savemapname 32 [] 0.7
		]
	]
] [ savemap $.savemapname ] [] [ .savemapname "" ]




UIstddialog "dialog_serverconnect" [
	uivlist $.UI_pad_L [
		uiclampx
		uitext "Please enter your server password:" 0.7
		UIbar 1
		uispace $.UI_pad_L 0 [
			UIbasicfield .serverpassw 32 [] 0.7
		]
	]
] [ connect (servinfoname $.sb_connectid) (servinfoport $.sb_connectid) $.serverpassw ] [] [ .serverpassw = "" ]




UIstddialog "dialog_copycfg" [
	uivlist $.UI_pad_L [
		uiclampx
		uitext "Copy map config?" 0.7
		UIbar 1
		uispace $.UI_pad_L 0 [
			uifill (+f $.UI_pad_D3XL $.UI_pad_DL)
			uivlist $.UI_pad_S [
				uitext (concatword "^f8" $.cmc_from) 0.6
				UItriangle 0 $.UI_pad_XL $.UI_pad_S 180
				uitext (concatword "^f8" $.cmc_to) 0.6
			]
		]
	]
] [
	textfocus "#copymapcfg"
	textload (concatword "media/map/" $.cmc_from ".cfg")
	textsave (concatword "media/map/" $.cmc_to   ".cfg")
] [] [
	if (|| [=s $.cmc_from ""] [=s $.cmc_to ""]) [ hideui $uiname ]
] [ .cmc_from "" ; .cmc_to "" ]
