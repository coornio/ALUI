///////////////////////////////////////////////////////////////////////////////
//  Refresh                                                                  //
///////////////////////////////////////////////////////////////////////////////

reui = [
    exec "config/ui/style.cfg"         // UI Stylesheet
    exec "config/ui/lib.cfg"           // UI Library
    exec "config/ui/common.cfg"        // UI Commons
    exec "config/ui/helpers.cfg"       // Helper Functions Library
    exec "config/ui/colorconvert.cfg"  // UI Color Conversion Library
    loopfiles f "config/ui/hud" cfg [
        exec (concatword "config/ui/hud/" $f ".cfg")   // HUD interface
    ]
    loopfiles f "config/ui/menus" cfg [
        exec (concatword "config/ui/menus/" $f ".cfg") // UI Menus
    ]
]




///////////////////////////////////////////////////////////////////////////////
//  Standard Function Library                                                //
///////////////////////////////////////////////////////////////////////////////

getmaster     = [listfilter i $arg1 [ismaster    $i]]
getadmin      = [listfilter i $arg1 [isadmin     $i]]
getspectator  = [listfilter i $arg1 [isspectator $i]]
getignored    = [listfilter i $arg1 [isignored   $i]]
getbots       = [listfilter i $arg1 [isai        $i]]

get!master    = [listfilter i $arg1 [! (ismaster    $i)]]
get!admin     = [listfilter i $arg1 [! (isadmin     $i)]]
get!spectator = [listfilter i $arg1 [! (isspectator $i)]]
get!ignored   = [listfilter i $arg1 [! (isignored   $i)]]
get!bots      = [listfilter i $arg1 [! (isai        $i)]]

cmd = [
    arg5 = 0 // saving us the trouble of declaring a local var
    loopwhile i (listlen $arg1) [>= $arg5 0] [
        if ((at $arg1 $i)) [
            doargs (at $arg2 $i)
            arg5 = (+ $arg5 1)
        ] [
            doargs (at $arg3 $i)
            arg5 = -1
        ]
    ]
    if (= $arg5 (listlen $arg1)) [ doargs $arg4 ]
]

clamp  = [ max  $arg2 (min  $arg3 $arg1) ]
clampf = [ maxf $arg2 (minf $arg3 $arg1) ]

sayteamcmd = [ inputcommand $arg1 [sayteam $commandbuf] "[team]" ]
demo = [ stopdemo ; if (mode -1) [ map $arg1 ] ]

followplayer = [
    if $editing [
        .followlist = (get!spectator (listclients))
        if (< (indexof $.followlist $.followcn) 0) [ .followcn (at $.followlist 0) ]
        if (= $.crosshairsize 0) [
            .crosshairsize $crosshairsize
            crosshairsize 0
            .followplayer 1
        ]
        sleep 1 [ goto $.followcn ; followplayer ]
    ] [
        crosshairsize $.crosshairsize
        .crosshairsize 0
        .followplayer 0
    ]
]

nextspecmode = [
    if $specmode [
        specmode (mod (+ $specmode 1) 3)
    ] [
        specmode (+ (> $getfollow -1) 1)
    ]
]

dozoom = [
    if $zoomtoggle [
        zoom (? $zoom -1 1)
    ] [
        zoom 1
        onrelease [ zoom -1 ]
    ]
]

do [
    local n ; n = 0
    looplist m $modelist [
        $m = [ if (mode @n) [ map $arg1 ] ]
        complete $m "media/map" ogz
        n = (+ $n 1)
    ]
    coop = [ if (mode 0) [ map $arg1 ] ]

]

complete demo .           dmo
complete map  "media/map" ogz // global handle
complete coop "media/map" ogz // legacy handle

dmmaps = [
    complex    alphacorp
    ot         turbine
    reflection
]
ctfmaps = [
    alphacorp  steelribs
    test_ctf   reflection
    waterworks
]
testmaps = [
    test_anim  test_model
]
tdmmaps = (listunion $dmmaps $ctfmaps)
allmaps = (listunion $tdmmaps $testmaps)


togglemainmenu = [ || hidetopui [ toggleui "main" ] ]
togglefullcon = [
    if (uivisible "fullconsole") [
       if (> $fullconsize 50) [ fullconsize 35 ] [ hideui "fullconsole" ]
    ] [ fullconsize 75 ; showui "fullconsole" ]
]
//toggletaskbar  = [ UItaskbarvisible = (! $UItaskbarvisible) ]

stats = [ showui "player_stats" ]




///////////////////////////////////////////////////////////////////////////////
//  Startup Sanitization Routines                                            //
///////////////////////////////////////////////////////////////////////////////


if $.followplayer [
    crosshairsize $.crosshairsize
    .crosshairsize 0
    .followplayer 0
]

if $.playingmusic [
    .playingmusic 0
]




///////////////////////////////////////////////////////////////////////////////
//  Hook Definitions                                                         //
///////////////////////////////////////////////////////////////////////////////

edittoggled = [
    if $editing [
        showui "edithud"
        if (! (uivisible "fullconsole")) [ showui "editvars" ]
    ] [
        hideui "edithud"
        hideui "editvars"
        hideui "texture_browser"
        hideui "mapmodel_browser"
    ]
]

mainmenutoggled = [
    if $mainmenu [
        if (! $.playingmusic) [
            .playingmusic 1
            music "sound/music/mainmenu.ogg"
        ]
    ] [
        loop n 20 [
            sleep (* $n 50) [
                musicvol (-f $musicvol @(divf @@musicvol 20))
            ]
        ]
        sleep 1000 [
            .playingmusic 0
            music ""
            musicvol @musicvol
        ]
    ]
]



