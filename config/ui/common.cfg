
reui = [
    exec "config/ui/style.cfg"         // UI Stylesheet
    exec "config/ui/lib.cfg"           // UI Library
    exec "config/ui/common.cfg"        // UI Commons
    exec "config/ui/helpers.cfg"       // Helper Functions Library
    exec "config/ui/colorconvert.cfg"  // UI Color Conversion Library
    loopfiles f "config/ui/hud" cfg [
        exec (concatword "config/ui/hud/" $f ".cfg")   // HUD interface
    ]
    loopfiles f "config/ui/menus" cfg [
        exec (concatword "config/ui/menus/" $f ".cfg") // UI Menus
    ]
]

nodebug [
    // scoreboard variables
    defvarp showscore   0 1 1
    defvarp showkills   0 1 1
    defvarp showdeaths  0 0 1
    defvarp showip      0 0 1
    defvarp sbopaque    0 1 1

    // value placeholders
    defvarp .autosortservers  0   1     1
    defvarp .hidehud          0   0     1
    defvarp .crosshairsize    0  15    50
    defvarp .showmat          0   1     1
    defvarp .entediting       0   1     1

    // other variables
    defvarp confirmquit      0 1 1
    defvarp confirmoverwrite 0 1 1
    
    // UI variables
    defvarp .UI_useimg  0 1 1
    defvarp .UI_taskbar 0 0 1
]

dmmaps = [
    complex    alphacorp
    ot         turbine
    reflection
]
ctfmaps = [
    alphacorp  steelribs
    test_ctf   reflection
    waterworks
]
testmaps = [
    test_anim  test_model
]
tdmmaps = (listunion $dmmaps $ctfmaps)
allmaps = (listunion $tdmmaps $testmaps)

edittoggled = [
    if $editing [
        showui "edithud"
        if (! (uivisible "fullconsole")) [ showui "editvars" ]
    ] [
        hideui "edithud"
        hideui "editvars"
        hideui "texture_browser"
        hideui "mapmodel_browser"
    ]
]

togglemainmenu = [ || hidetopui [ toggleui "main" ] ]
togglefullcon = [
    if (uivisible "fullconsole") [
       if (> $fullconsize 50) [ fullconsize 35 ] [ hideui "fullconsole" ]
    ] [ fullconsize 75 ; showui "fullconsole" ]
]
//toggletaskbar  = [ UItaskbarvisible = (! $UItaskbarvisible) ]

stats = [ showui "player_stats" ]
