
// Sinusoidal Transition Function
// arg1 : TIME POINT  | The current point in time along the confines of the wavelength
// arg2 : WAVELENGTH  | Measured in millis by which a full cycle will have been completed
// arg3 : 0-INVERTION | When set to true, the wave will flip around upon reaching 0, going to the negative but continuing the same pattern.
//                    > This is designed to give equal fade times both to 0 and non-zero colors. You can have a triple gradient this way too.


coswave = [
	local x y 
	x = (modf (*f 180 (divf $arg1 $arg2)) 720)
	y = (+f 0.5 (*f 0.5 (cos $x)))
	if (&& $arg3 [>= $x 180] [< $x 540]) [ y = (*f $y -1) ]
	result $y
]

// coswave.INT  INT:1  INT:2  rep-millis  custom-X  0-INVERTION
// NOT giving more than 3 parameters will have the script assume it's working within a UImenu.
coswave.INT = [
	if (>= $numargs 4) [
		arg4 = (coswave (- $getmillis $arg4) $arg3 $arg5)
	] [ arg4 = (UIcoswave $arg3) ]

	local r1 g1 b1 r2 g2 b2
	INT_to_RGB32 $arg1 [ r1 = $r ; g1 = $g ; b1 = $b ]
	INT_to_RGB32 $arg2 [ r2 = $r ; g2 = $g ; b2 = $b ]

	RGB32_to_INT 255 (
		+f $r2 (*f (-f $r2 $r1) -1 $arg4)
	) (
		+f $g2 (*f (-f $g2 $g1) -1 $arg4)
	) (
		+f $b2 (*f (-f $b2 $b1) -1 $arg4)
	)
]

// coswave.RGB24  R:1 B:1 G:1  R:2 G:2 B:2  rep-millis  custom-X  0-INVERTION
// NOT giving more than 7 parameters will have the script assume it's working within a UImenu.
coswave.RGB24 = [
	if (>= $numargs 8) [
		arg8 = (coswave (- $getmillis $arg8) $arg7 $arg9)
	] [ arg8 = (UIcoswave $arg7) ]

	RGB32_to_INT 255 (
		+f $arg4 (*f (-f $arg4 $arg1) -1 $arg8)
	) (
		+f $arg5 (*f (-f $arg5 $arg2) -1 $arg8)
	) (
		+f $arg6 (*f (-f $arg6 $arg3) -1 $arg8)
	)
]

// UIcoswave  rep-millis
// meant to make Y value acquisition simpler but needs to be run within an UImenu
UIcoswave = [ coswave (- $getmillis $[.UImillis_@uiname]) $arg1 ]

UImenu "coswave" [
	uigrid 2 0.01 0.01 [
		UIhsliderslim wavelength 200 10000 0.25 0.04
		uispace 0.01 0 [
			uialign -1 0
			uiclamp 1 1
			uihlist 0 [
				uiclamp 1 1
				uitext (UIcoswave $wavelength) 0.6
				uitext (concatword (+ (round (*f 100 (UIcoswave $wavelength))) 0) "%") 0.8
			]
		]

		uicolor (coswave.RGB24 255 10 20 120 100 230 $wavelength) 0.25 0.5 [
			uivlist 0 [
				uialign 0 1
				uiline 0xFFFFFF 0.25
				uifill 0 (*f (UIcoswave $wavelength) 0.5)
			]
		]
		uicolor (coswave.INT 0x7864E6 0xFF0A14 $wavelength) 0.25 0.5 [
			uivlist 0 [
				uialign 0 1
				uiline 0xFFFFFF 0.25
				uifill 0 (*f (UIcoswave $wavelength) 0.5)
			]
		]
	]
] [ wavelength = 2000 ]



