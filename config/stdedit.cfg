//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Master Scroll Controller                                                                                //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

msc_multiplier = 8

msc_editdelta = [
	cond [&& $enthavesel [||iskeyheld "1" "2" "3" "4" "5"]] [
		loop i 5 [ if (iskeyheld (+ $i 1)) [ entattrdelta $i $arg1 ] ]
	] [iskeyheld "G"] [                                                       // Change grid power
		gridpower (max 0 (min 12 (+ $arg1 $gridpower) (- $mapscale 1)))
	] [iskeyheld "R"] [                                                       // Selection rotate / blend brush rotate
		if $blendpaintmode [ rotateblendbrush $arg1 ] [ editrotate $arg1 ]
	] [iskeyheld "Q"] [                                                       // Push corner closest to the cursor
		editfacewsel $arg1 2
	] [iskeyheld "F"] [                                                       // Push selected face/corners
		editfacewsel $arg1 0
	] [iskeyheld "Y"] [                                                       // Change MRU textures
		edittex $arg1
	] [iskeyheld "Z"] [                                                       // Change floatspeed
		floatspeed (min (max (+ $floatspeed (* 50 $arg1)) 50) 5000)
	] [&& $hmapedit [iskeyheld "H"]] [                                        // Change heightmap brush
		selecthbrush $arg1
	] [&& $blendpaintmode [iskeyheld "B"]] [                                  // Change blendmap mode
		arg1 = (+ $blendpaintmode $arg1)
		cond [< $arg1 1] [ blendpaintmode = 5 ] [> $arg1 5] [ blendpaintmode = 1 ] [ blendpaintmode $arg1 ]
	] [                                                                       // Change blend brush/mode | Push/pull geometry
		if $blendpaintmode [ nextblendbrush $arg1 ] [ editfacewsel $arg1 1 ]
	]
]




///////////////////////////////////////////////////////////////////////////////
//  Entity Primary Actions                                                   //
///////////////////////////////////////////////////////////////////////////////

=enttype = [ || (=s * $arg1) [ =s (enttype) $arg1 ] ]

=entattr = [ || (=s * $arg2) [ = (entattr $arg1) $arg2 ] ]

// clear ents of given type
clearents = [ 
	if $editing [
		entcancel
		entselect [=enttype $arg1]
		echo Deleted (enthavesel) $arg1 entities
		delent
	] 
]

// replace all ents that match current selection
// with the values given
replaceents = [
	if $editing [
		do [
			entfind @(entget)
			entset @(loopconcat+ i 1 $numargs [result $[arg@i]])
		]
		echo Replaced (enthavesel) entities
	]
]

selentedit     = [ saycommand (concat "/entset" (entget)) ]
selreplaceents = [ saycommand (concat "/replaceents" (entget)) ]
selentfindall  = [ do [ entfind @(entget) ] ]

// modify given attribute of ent by a given amount
// arg1 attribute
// arg2 value
entattrdelta = [ entattr $arg1 (+ (entattr $arg1) $arg2) ]

enttypelist = [ 
   light mapmodel playerstart envmap particles sound spotlight decal
   teleport teledest jumppad
   flag
]

enttypeselect = [
	local len next
	len = (listlen $enttypelist)
	next = (mod (+ (indexof $enttypelist (enttype)) $arg1) $len)
	if (< $next 0) [ next = (+ $next $len) ]
	do [ entset @(listsplice (entget) (at $enttypelist $next) 0 1) ]
]




///////////////////////////////////////////////////////////////////////////////
//  Copy & Paste Commands                                                    //
///////////////////////////////////////////////////////////////////////////////

// 3 types of copying and pasting
// 1. select only cubes      -> paste only cubes
// 2. select cubes and ents  -> paste cubes and ents. same relative positions
// 3. select only ents       -> paste last selected ent. if ents are selected, replace attrs as paste

opaquepaste = 1
entcopymode = 0

editcopy = [
	if (|| havesel [! (enthavesel)]) [
		entcopymode = 0
		copy
	] [ entcopymode = 1 ]
	entcopy
]

editpaste = [
	local cancelpaste
	cancelpaste = (! (|| enthavesel havesel))
	if $entcopymode [
		entreplace
		if $cancelpaste cancelsel
	] [
		pastehilite
		reorient // temp; real fix will be in octaedit
		onrelease [
			if $opaquepaste delcube 
			paste
			entpaste
			if $cancelpaste cancelsel
		]
	]
]




///////////////////////////////////////////////////////////////////////////////
//  Selection Commands                                                       //
///////////////////////////////////////////////////////////////////////////////

// select ents with given properties
// '*' is wildcard
entfind = [
	if $numargs [
		entselect (concat [ && [=enttype @@arg1] ] (loopconcat i (- $numargs 1) [
			result [ [=entattr @@i @@[arg@(+ $i 2)]] ]
		]))
	] [ entselect 1 ]
]

entfindinsel = [
	if $numargs [
		entselect (concat [ && insel [=enttype @@arg1] ] (loopconcat i (- $numargs 1) [
			result [ [=entattr @@i @@[arg@(+ $i 2)]] ]
		]))
	] [ entselect insel ]
]

drag       = [ dragging 1 ; onrelease [ dragging 0 ] ]
corners    = [ selectcorners 1 ; dragging 1 ; onrelease [ selectcorners 0 ; dragging 0 ] ]
editmove   = [ moving 1 ; onrelease [ moving 0 ] ; result $moving ]
entdrag    = [ entmoving 1 ; onrelease [ entmoving 0 ] ; result $entmoving ] 
editdrag   = [ cancelsel ; || entdrag drag ]
selcorners = [ if $hmapedit hmapselect [ cancelsel ; || entdrag corners ] ]
editextend = [ || entdrag [ if havesel [ selextend ; reorient ; editmove ] drag ] ]

editmovewith = [
	if (havesel) [
		|| editmove arg1
		onrelease [ moving 0 ; dragging 0 ]
	] arg1
]

editmovecorner = [ editmovewith selcorners ]
editmovedrag   = [ editmovewith editdrag ]




///////////////////////////////////////////////////////////////////////////////
//  Miscellaneous Editing Commands                                           //
///////////////////////////////////////////////////////////////////////////////

editfacewsel = [
	if (|| havesel [! (enthavesel)]) [
		if $moving [ pushsel $arg1 ] [
			entcancel
			editface $arg1 $arg2
		]
	]
]

entdirection = [
	if (&& enthavesel [= $havesel 0]) [
		local i n
		i = (indexof "playerstart teledest mapmodel decal flag" $enttype)
		if (>= $i 0) [
			n = (at "0 0 1 1 0" $i) // the attribute which holds yaw
			if (> $arg1 0) [
				entattrdelta $n $arg2
				if (> (entattr $n) 359) [ entattrdelta $n -360 ]
			] [
				entattrdelta $n (- 0 $arg2)
				if (< (entattr $n) 0) [ entattrdelta $n 360 ]
			]
		]
		result 1
	] [ result 0 ]
]

editdel    = [ if (! $enthavesel) delcube ; delent ]
editflip   = [ flip ; entflip ]
editrotate = [ || (entdirection $arg1 15) [ rotate $arg1 ; entrotate $arg1 ] ]
editcut    = [  
	if (moving 1) [
		if (= $moving 1) selsave
		onrelease [ 
			moving 0
			if (selmoved) [
				selswap
				copy ; entcopy
				delcube ; delent
				selrestore
				paste ; entpaste
			]
		]
	]
]

//setting_entediting = 1

passthrough = [ 
	passthroughsel $arg1
	if $arg1 [
		passthroughcube_bak = $passthroughcube
		passthroughcube 1
	] [ passthroughcube $passthroughcube_bak ]
	entcancel
	if $.entediting [ entediting (! $arg1) ]
]

entcomplete = [ listcomplete $arg1 $enttypelist ]
entcomplete "newent"
entcomplete "entfind"
entcomplete "clearents"

listcomplete editmat "air water clip glass noclip lava gameclip death nogi alpha"
air = [ editmat air ]
looplist i ["" 1 2 3 4] [
	[water@i] = [ editmat water@i ]
	[lava@i]  = [ editmat lava@i  ]
	[glass@i] = [ editmat glass@i ]
]
clip     = [ editmat clip     ]
noclip   = [ editmat noclip   ]
gameclip = [ editmat gameclip ]
death    = [ editmat death    ]
nogi     = [ editmat nogi     ]
alpha    = [ editmat alpha    ]

blendpaintmodes = [ "Off" "Overwrite" "Merge" "Max Opacity" "Inverted Merge" "Erase" ]

minimaphere = [ minimapheight (at $getcampos 2) ]

getsundir = [ sunlightyaw $getcamyaw ; sunlightpitch $getcampitch ]

